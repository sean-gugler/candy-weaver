0 REM COPYRIGHT 2022 BY SEAN GUGLER
10 REM LICENSED UNDER CC BY-NC-SA 4.0
20 T1$="CANDY WEAVER"
30 T2$="V0.2.0 2022-08-12"
40 T3$="BY SEAN GUGLER"
50 T4$="-----------------"
60 GOSUB 1600
70 GOSUB 500
80 DATA 11 , 8,9,9 , 3,3,3 , 4,4,3 , 5,5,3 , 6,8,3 , 8,3,5 , 3,8,5 , 3,3,6 , 4,4,6 , 6,6,4 , 8,9,2
90 READ N
100 FOR R=1 TO N:GOSUB 200:GOSUB 300:GOSUB 2500:NEXT
110 END
200 REM --- LAYOUT A NEW WEAVE
210 READ W,H,CN%:X=9-W:Y=20-H
220 GR:GOSUB 3100:GOSUB 1700:GOSUB 3200
230 X=X+20:GOSUB 1700
240 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 1800
250 CP%=1:CI%=0:GOSUB 1900
260 BP=1:BI=0:GOSUB 2100
270 RETURN
300 REM --- HELP TEXT
310 VTAB 22:HTAB 1
320 IF AX THEN 370
330 CALL(-868):PRINT " AIM: LEFT/RIGHT";
340 HTAB 24:PRINT "[T]OGGLE EXPERT"
350 CALL(-868):PRINT " LAY CANDY: SPACE";
360 RETURN
370 CALL(-868):PRINT "PRESS A LETTER";
380 HTAB X-3:PRINT "COL:";
390 FOR I=0 TO W-1:PRINT " ";CHR$(65+I);:NEXT
400 PRINT:CALL(-868):PRINT "TO FAST-STRIPE";
410 HTAB 26-H:PRINT "ROW:";
420 FOR I=W TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
430 RETURN
500 REM --- WELCOME
510 TEXT:HOME
520 T$=T2$:GOSUB 800
530 T$=T3$:GOSUB 800
540 PRINT
550 T$=T4$:GOSUB 800
560 T$=T1$:GOSUB 800
570 T$=T4$:GOSUB 800
580 PRINT
590 PRINT "FIRST TIME? PRESS [I]"
600 PRINT "FOR INSTRUCTIONS. "
610 PRINT
620 PRINT "OTHERWISE, PRESS [P]"
630 PRINT "TO PLAY!"
640 PRINT
650 GET CH$
660 IF CH$="I" THEN GOSUB 1000:GOTO 500
670 IF CH$<>"P" THEN 650
680 REM SEED "RND" FROM HUMAN REACTION TIME
690 I=RND(-1*(PEEK(78)+256*PEEK(79)))
700 RETURN
800 REM --- PRINT CENTERED
810 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
900 REM --- PROMPT TO CONTINUE
910 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
1000 REM --- TUTORIAL
1010 W=3:H=3:CN%=3:X=9-W:Y=20-H
1020 GR:HOME
1030 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1040 PRINT "CANDY WEAVING!"
1050 GOSUB 900
1060 SEED=-222
1070 T=RND(SEED):REM PREDICTABLE SEQUENCE
1080 HOME:GOSUB 1700:GOSUB 3100:GOSUB 3200
1090 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1100 PRINT "A TASTY TARGET PATTERN."
1110 GOSUB 900
1120 HOME:X=X+20:GOSUB 1700
1130 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1140 PRINT "STRIPES ONE AT A TIME UNTIL THEY"
1150 PRINT "MATCH THE PATTERN."
1160 GOSUB 900
1170 HOME
1180 PRINT "FIRST: SELECT"
1190 GOSUB 1800
1200 PRINT "A FLAVOR OF CANDY"
1210 PRINT "BY PRESSING ITS NUMBER";
1220 GOSUB 2200:CI%=CH-49:IF CI%<0 OR CI%>=CN% THEN 1220
1230 CP%=NOT(CI%-1)+1
1240 HOME:GOSUB 1800:GOSUB 1900
1250 PRINT "NEXT: PRESS THE RIGHT ARROW SEVERAL"
1260 PRINT "TIMES TO MOVE THE CANDY HOSE."
1270 BI=0:GOSUB 2100
1280 GOSUB 2200:IF CH<>21 THEN 1280
1290 GOSUB 2400:IF BI<>0 THEN 1280
1300 HOME:GOSUB 1800:GOSUB 1900
1310 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1320 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1330 GOSUB 2200
1340 IF CH=11 OR CH=8 THEN GOSUB 2300
1350 IF CH=10 OR CH=21 THEN GOSUB 2400
1360 IF CH<>32 THEN 1330:REM SPACE
1370 GOSUB 3300
1380 HOME
1390 PRINT "CONTINUE LAYING"
1400 PRINT "CANDY STRIPES UNTIL"
1410 PRINT "THE PATTERN MATCHES!"
1420 GOSUB 1800:GOSUB 1900
1430 VTAB 22:HTAB 30:PRINT"--DEMO--"
1440 VTAB 23:HTAB 30:PRINT"PRESS [P]"
1450 VTAB 24:HTAB 30:PRINT"TO PLAY!";
1460 FOR I=1 TO 60
1470 IF PEEK(49152)>=128 THEN GET CH$
1480 IF CH$="P" THEN RETURN
1490 IF I<>20 THEN 1520
1500 T=INT(RND(1)*CN%):IF T=CI% THEN 1500
1510 CI%=T:GOSUB 1900:GOSUB 2100
1520 IF I<>40 THEN 1550
1530 T=INT(RND(1)*(W+H)):IF T=BI THEN 1530
1540 BI=T:GOSUB 2100
1550 IF I<>60 THEN 1570
1560 GOSUB 3300
1570 NEXT I:GOTO 1460
1600 REM --- INITIALIZE VARIABLES
1610 DIM C(15)
1620 DIM S(25)
1630 DIM RO(24):REM GR MEM ADDR BY ROW
1640 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
1650 AX=0
1660 RETURN
1700 REM --- DRAW PLAY AREA BORDER
1710 COLOR=15
1720 HLIN X-1,X+W*2+1 AT Y-1
1730 HLIN X-1,X+W*2+1 AT Y+H*2+1
1740 VLIN Y-1,Y+H*2+1 AT X-1
1750 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
1800 REM --- DRAW COLOR PALETTE
1810 VTAB 21:HTAB 20
1820 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
1830 PRINT:RETURN
1900 REM --- SHOW SELECTED COLOR
1910 IF CI%=CP% THEN RETURN
1920 VTAB 21:HTAB 21+CI%*2:INVERSE:PRINT CI%+1;:HTAB 21+CP%*2:NORMAL:PRINT CP%+1;
1930 CP%=CI%:PRINT:RETURN
2000 REM --- CLEAR STRIPE INDICTOR
2010 COLOR=0:BA=BP:GOSUB 3400
2020 GOSUB 1700:RETURN
2100 REM --- SHOW SELECTED STRIPE
2110 GOSUB 2000
2120 COLOR=C(CI%):BA=BI:GOSUB 3400
2130 BP=BI:RETURN
2200 REM --- INPUT KEY
2210 WAIT 49152,128:GET CH$:CH = ASC(CH$)
2220 RETURN
2300 REM --- AIM HOSE
2310 BI=BI-1:IF BI<0 THEN BI=W+H-1
2320 GOSUB 2100
2330 RETURN
2400 BI=BI+1:IF BI=W+H THEN BI=0
2410 GOSUB 2100
2420 RETURN
2500 REM --- MAIN GAME LOOP
2510 GOSUB 2200
2520 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
2530 IF CH=11 OR CH=8 THEN GOSUB 2300:GOTO 2500:REM LEFT
2540 IF CH=10 OR CH=21 THEN GOSUB 2400:GOTO 2500:REM RIGHT
2550 IF CH=32 THEN 2670:REM SPACE
2560 IF CH=84 THEN AX=NOT AX:GOSUB 300:GOTO 2500:REM TOGGLE EXPERT
2570 IF CH=82 THEN V=1:GOSUB 2800:GOTO 2500:REM REVEAL DIFFS
2580 T=CH-49:REM 1-9
2590 IF T>=0 AND T<CN% THEN 2630
2600 T=CH-65:REM A-Z
2610 IF T>=0 AND T<W+H THEN 2650
2620 GOTO 2500:REM OTHER
2630 REM - CHOOSE COLOR
2640 CI%=T:GOSUB 1900:GOSUB 2100:GOTO 2500
2650 REM - CHOOSE LINE
2660 BI=T
2670 GOSUB 2100:GOSUB 3300
2680 VTAB 24:HTAB 1:CALL(-868):PRINT "COMPARING...      ";:HTAB 1
2690 V=0:GOSUB 2800
2700 IF MA% THEN PRINT "PROGRESS: ";MA%;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:GOTO 2500
2710 GOSUB 2000
2720 T=RND(1)*3:INVERSE
2730 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 2760
2740 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 2760
2750 PRINT "COMPLETED! (YUM)";
2760 NORMAL:GOSUB 900:RETURN
2800 REM --- CHECK FOR MATCH
2810 MA%=0
2820 Y2=INT((Y+1)/2):FOR J=Y2 TO Y2+H-1
2830 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
2840 T=(PEEK(I)=PEEK(I-20))
2850 MA%=MA%+T
2860 IF (NOT V) OR T THEN 2920
2870 P=PEEK(I)
2880 POKE(I),255
2890 FOR T=1 TO 200:NEXT
2900 POKE(I),P
2910 IF PEEK(49152)>=128 THEN RETURN
2920 NEXT I:NEXT J
2930 MA%=(W*H-MA%)
2940 RETURN
3000 REM --- CREATE A SHUFFLE 
3010 FOR I=0 TO SN%-1:S(I)=I:NEXT
3020 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
3030 RETURN
3100 REM --- GENERATE COLOR PALETTE
3110 SN%=15:GOSUB 3000
3120 I=0:FOR CI=0 TO CN%-1
3130 T=S(I):I=I+1:IF T=0 OR T=3 OR T=5 OR T=10 THEN 3130
3140 C(CI)=T:NEXT:RETURN
3200 REM --- DRAW PATCH
3210 SN%=W+H:GOSUB 3000
3220 CI%=0
3230 FOR I=0 TO W+H-1:BI=S(I):GOSUB 3300
3240 CI%=CI%+1:IF CI%=CN% THEN CI%=0
3250 NEXT I
3260 RETURN
3300 REM --- DRAW A SINGLE BAR
3310 COLOR=C(CI%)
3320 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
3330 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
3400 REM --- DRAW AXIS INDICATOR
3410 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
3420 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
