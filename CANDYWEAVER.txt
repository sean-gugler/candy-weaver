0 GOSUB 1700:GOSUB 3500
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="V0.3.0 2022-08-16"
50 T3$="BY SEAN GUGLER"
60 T4$="-----------------"
70 GOSUB 600
80 READ N
90 FOR R=1 TO N:GOSUB 200:GOSUB 400:GOSUB 2600:NEXT
100 GOTO 4100
200 REM --- LAYOUT A NEW WEAVE
210 READ W,H,CN,TS:X=9-W:Y=20-H:M=W*H:GOSUB 3000
220 TI=0:TX=0
230 IF NOT TS THEN 270
240 HOME:FLASH:VTAB 22
250 T$="*** TIMED ROUND! ***":GOSUB 900
260 NORMAL:GOSUB 1000
270 GR:HOME:M=0:GOSUB 3600:GOSUB 1800:GOSUB 3800
280 X=X+20:GOSUB 1800
290 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 1900
300 CP=1:CI=0:GOSUB 2000
310 BP=1:BI=0:GOSUB 2200
320 IF TS THEN COLOR=4:HLIN 0,39 AT 0
330 RETURN
400 REM --- HELP TEXT
410 VTAB 22:HTAB 1
420 IF AX THEN 470
430 CALL(-868):PRINT " AIM: LEFT/RIGHT";
440 HTAB 24:PRINT "[T]OGGLE LABELS"
450 CALL(-868):PRINT " LAY CANDY: SPACE";
460 RETURN
470 CALL(-868):PRINT "PRESS A LETTER";
480 HTAB X-3:PRINT "COL:";
490 FOR I=0 TO W-1:PRINT " ";CHR$(65+I);:NEXT
500 PRINT:CALL(-868):PRINT "TO FAST-STRIPE";
510 HTAB 26-H:PRINT "ROW:";
520 FOR I=W TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
530 RETURN
600 REM --- WELCOME
610 TEXT:HOME
620 T$=T2$:GOSUB 900
630 T$=T3$:GOSUB 900
640 PRINT
650 T$=T4$:GOSUB 900
660 T$=T1$:GOSUB 900
670 T$=T4$:GOSUB 900
680 PRINT
690 PRINT "FIRST TIME? PRESS [I]"
700 PRINT "FOR INSTRUCTIONS. "
710 PRINT
720 PRINT "OTHERWISE, PRESS [P]"
730 PRINT "TO PLAY!"
740 PRINT
750 GET CH$
760 IF CH$="I" THEN GOSUB 1100:GOTO 600
770 IF CH$<>"P" THEN 750
780 REM SEED "RND" FROM HUMAN REACTION TIME
790 I=RND(-1*(PEEK(78)+256*PEEK(79)))
800 RETURN
900 REM --- PRINT CENTERED
910 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
1000 REM --- PROMPT TO CONTINUE
1010 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
1100 REM --- TUTORIAL
1110 W=3:H=3:CN=3:X=9-W:Y=20-H
1120 TS=0
1130 GR:HOME
1140 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1150 PRINT "CANDY WEAVING!"
1160 GOSUB 1000
1170 SEED=-222
1180 T=RND(SEED):REM PREDICTABLE SEQUENCE
1190 HOME:GOSUB 1800:GOSUB 3600:GOSUB 3800
1200 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1210 PRINT "A TASTY TARGET PATTERN."
1220 GOSUB 1000
1230 HOME:X=X+20:GOSUB 1800
1240 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1250 PRINT "STRIPES ONE AT A TIME UNTIL THEY"
1260 PRINT "MATCH THE PATTERN."
1270 GOSUB 1000
1280 HOME
1290 PRINT "FIRST: SELECT"
1300 GOSUB 1900
1310 PRINT "A FLAVOR OF CANDY"
1320 PRINT "BY PRESSING ITS NUMBER";
1330 GOSUB 2300:CI=CH-49:IF CI<0 OR CI>=CN THEN 1330
1340 CP=NOT(CI-1)+1
1350 HOME:GOSUB 1900:GOSUB 2000
1360 PRINT "NEXT: PRESS THE RIGHT ARROW SEVERAL"
1370 PRINT "TIMES TO MOVE THE CANDY HOSE."
1380 BI=0:GOSUB 2200
1390 GOSUB 2300:IF CH<>21 THEN 1390
1400 GOSUB 2500:IF BI<>0 THEN 1390
1410 HOME:GOSUB 1900:GOSUB 2000
1420 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1430 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1440 GOSUB 2300
1450 IF CH=11 OR CH=8 THEN GOSUB 2400
1460 IF CH=10 OR CH=21 THEN GOSUB 2500
1470 IF CH<>32 THEN 1440:REM SPACE
1480 GOSUB 3900
1490 HOME
1500 PRINT "CONTINUE LAYING"
1510 PRINT "CANDY STRIPES UNTIL"
1520 PRINT "THE PATTERN MATCHES!"
1530 GOSUB 1900:GOSUB 2000
1540 VTAB 22:HTAB 30:PRINT"--DEMO--"
1550 VTAB 23:HTAB 30:PRINT"PRESS [P]"
1560 VTAB 24:HTAB 30:PRINT"TO PLAY!";
1570 FOR I=1 TO 60
1580 IF PEEK(KY)>=128 THEN GET CH$
1590 IF CH$="P" THEN RETURN
1600 IF I<>20 THEN 1630
1610 T=INT(RND(1)*CN):IF T=CI THEN 1610
1620 CI=T:GOSUB 2000:GOSUB 2200
1630 IF I<>40 THEN 1660
1640 T=INT(RND(1)*(W+H)):IF T=BI THEN 1640
1650 BI=T:GOSUB 2200
1660 IF I<>60 THEN 1680
1670 GOSUB 3900
1680 NEXT I:GOTO 1570
1700 REM --- INITIALIZE VARIABLES
1710 M=0:I=0:O=-20:J=0:A=0:W=0:H=0:X=0:Y=0
1720 KY=49152
1730 DIM C(15)
1740 DIM S(25)
1750 DIM RO(24):REM GR MEM ADDR BY ROW
1760 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
1770 AX=0
1780 RETURN
1800 REM --- DRAW PLAY AREA BORDER
1810 COLOR=15
1820 HLIN X-1,X+W*2+1 AT Y-1
1830 HLIN X-1,X+W*2+1 AT Y+H*2+1
1840 VLIN Y-1,Y+H*2+1 AT X-1
1850 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
1900 REM --- DRAW COLOR PALETTE
1910 VTAB 21:HTAB 20
1920 FOR I=0 TO CN-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
1930 PRINT:RETURN
2000 REM --- SHOW SELECTED COLOR
2010 IF CI=CP THEN RETURN
2020 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+1;:HTAB 21+CP*2:NORMAL:PRINT CP+1;
2030 CP=CI:PRINT:RETURN
2100 REM --- CLEAR STRIPE INDICTOR
2110 COLOR=0:BA=BP:GOSUB 4000
2120 GOSUB 1800:RETURN
2200 REM --- SHOW SELECTED STRIPE
2210 GOSUB 2100
2220 COLOR=C(CI):BA=BI:GOSUB 4000
2230 BP=BI:RETURN
2300 REM --- INPUT KEY
2310 TI=TI+1:IF TI=TS THEN :COLOR=9:PLOT TX,0:TX=TX+1:TI=0
2320 IF TX=40 THEN RETURN
2330 IF PEEK(KY)<128 THEN 2310
2340 GET CH$:CH = ASC(CH$)
2350 RETURN
2400 REM --- AIM HOSE
2410 BI=BI-1:IF BI<0 THEN BI=W+H-1
2420 GOSUB 2200
2430 RETURN
2500 BI=BI+1:IF BI=W+H THEN BI=0
2510 GOSUB 2200
2520 RETURN
2600 REM --- MAIN GAME LOOP
2610 GOSUB 2300
2620 IF TX=40 THEN 2870
2630 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
2640 IF CH=11 OR CH=8 THEN GOSUB 2400:GOTO 2600:REM LEFT
2650 IF CH=10 OR CH=21 THEN GOSUB 2500:GOTO 2600:REM RIGHT
2660 IF CH=32 THEN 2780:REM SPACE
2670 IF CH=84 THEN AX=NOT AX:GOSUB 400:GOTO 2600:REM TOGGLE EXPERT
2680 IF CH=82 THEN GOSUB 3200:GOTO 2600:REM REVEAL DIFFS
2690 T=CH-49:REM 1-9
2700 IF T>=0 AND T<CN THEN 2740
2710 T=CH-65:REM A-Z
2720 IF T>=0 AND T<W+H THEN 2760
2730 GOTO 2600:REM OTHER
2740 REM - CHOOSE COLOR
2750 CI=T:GOSUB 2000:GOSUB 2200:GOTO 2600
2760 REM - CHOOSE LINE
2770 BI=T
2780 GOSUB 2200:GOSUB 3900
2790 VTAB 24:HTAB 1:CALL(-868):PRINT "COMPARING...      ";:HTAB 1:GOSUB 3100
2800 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:GOTO 2600
2810 GOSUB 2100
2820 T=RND(1)*3:INVERSE
2830 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 2860
2840 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 2860
2850 PRINT "COMPLETED! (YUM)";
2860 NORMAL:GOSUB 1000:RETURN
2870 HOME:FLASH:PRINT "OUT OF TIME!"
2880 NORMAL:PRINT "FINAL SCORE: ";M
2890 GOSUB 1000:RETURN
3000 REM --- INIT CHECK VARIABLES
3010 J0=(Y+1)/2:J1=J0+H-1:W2=W*2
3100 REM --- CHECK FOR MATCH
3110 M=0:FOR J=J0 TO J1
3120 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
3130 M=M+(PEEK(I)=PEEK(I+O))
3140 NEXT I:NEXT J
3150 M=(W*H-M)
3160 RETURN
3200 REM --- REVEAL DIFFERENCES
3210 FOR J=J0 TO J1
3220 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
3230 P=PEEK(I):IF P=PEEK(I+O) THEN 3260
3240 POKE(I),255:FOR T=1 TO 200:NEXT:POKE(I),P
3250 IF PEEK(KY)>=128 THEN RETURN
3260 NEXT I:NEXT J:RETURN
3300 REM --- CREATE A SHUFFLE 
3310 FOR I=0 TO SN-1:S(I)=I:NEXT
3320 FOR I=SN-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
3330 RETURN
3400 REM --- SHUFFLE COLOR PALETTE
3410 FOR I=CN-1 TO 0 STEP -1:J=RND(1)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
3420 RETURN
3500 REM --- TABLE OF CLASHING COLORS
3510 DIM CC(15*5-1)
3520 FOR I=0 TO 15:S(I)=0:NEXT
3530 FOR K=1 TO 14
3540 READ I,J
3550 N=S(I):CC(I*5+N)=J:S(I)=N+1
3560 N=S(J):CC(J*5+N)=I:S(J)=N+1
3570 NEXT K
3580 RETURN
3600 REM --- GENERATE COLOR PALETTE
3610 VTAB 21:PRINT "SELECTING FLAVORS..."
3620 M=M+1:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
3630 FOR N=0 TO 13:S(N)=N+1:NEXT
3640 FOR CI=CN-1 TO 0 STEP -1
3650 IF N<=CI THEN 3600
3660 I=RND(1)*N
3670 K=S(I):C(CI)=K
3680 N=N-1:S(I)=S(N)
3690 K=K*5
3700 FOR J=K TO K+4
3710 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
3720 FOR I=T TO N-1
3730 IF A=S(I) THEN N=N-1:S(I)=S(N):I=N
3740 NEXT I
3750 NEXT J
3760 NEXT CI
3770 RETURN
3800 REM --- DRAW PATCH
3810 SN=W+H:GOSUB 3300
3820 CI=0
3830 FOR I=0 TO W+H-1:BI=S(I):GOSUB 3900
3840 CI=CI+1:IF CI=CN THEN CI=0
3850 NEXT I
3860 GOSUB 3400
3870 RETURN
3900 REM --- DRAW A SINGLE BAR
3910 COLOR=C(CI)
3920 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
3930 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
4000 REM --- DRAW AXIS INDICATOR
4010 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
4020 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
4100 REM --- GAME OVER
4110 TEXT:HOME:INVERSE
4120 T$="CONGRATULATIONS!":GOSUB 900
4130 NORMAL:VTAB 4:PRINT "YOU'VE WOVEN ALL THE CANDY"
4140 PRINT:PRINT "YOU ARE A MASTER CANDY WEAVER!"
4150 PRINT
4160 END
4200 DIM CT(39)
4210 I=0
4220 FOR C1=1 TO 13
4230 FOR C2=C1+1 TO 14
4240 IF I=0 THEN GR:HOME
4250 PRINT C1;" ";C2;"  ";
4260 CT(I*5+1)=C1
4270 CT(I*5+2)=C2
4280 CT(I*5+3)=C1
4290 CT(I*5+4)=C2
4300 I=I+1
4310 IF I<8 THEN 4350
4320 FOR I=0 TO 39:COLOR=CT(I):VLIN 0,39 AT I:NEXT
4330 GOSUB 1000
4340 I=0
4350 NEXT C2
4360 NEXT C1
4370 FOR J=0 TO I*5:COLOR=CT(J):VLIN 0,39 AT J:NEXT
4380 END
4390 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
4400 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
4410 DATA 29
4420 DATA 3,3,6,0 , 4,4,8,0 , 5,4,9,0 , 3,3,6,50
4430 DATA 5,3,8,0 , 7,3,9,0 , 8,4,7,0 , 5,4,8,60
4440 DATA 3,6,6,0 , 4,7,5,0 , 5,9,5,0 , 4,6,7,60
4450 DATA 3,3,3,0 , 5,5,5,0 , 7,7,7,0 , 4,4,4,70
4460 DATA 6,3,3,0 , 8,4,4,0 , 4,8,3,0 , 3,3,3,40
4470 DATA 5,2,2,0 , 6,4,2,0 , 3,4,2,50
4480 DATA 8,9,9,0 , 8,9,5,0 , 7,8,2,0 , 3,3,6,20
4490 DATA 8,9,2,0 , 6,7,3,90
