0 REM COPYRIGHT 2022 BY SEAN GUGLER
10 REM LICENSED UNDER CC BY-NC-SA 4.0
20 T1$="CANDY WEAVER"
30 T2$="V0.1.1 2022-08-11"
40 T3$="BY SEAN GUGLER"
50 T4$="-----------------"
60 GOSUB 1400
70 GOSUB 300
80 DATA 11 , 3,3,3 , 4,4,3 , 5,5,3 , 6,8,3 , 8,3,5 , 3,8,5 , 3,3,6 , 4,4,6 , 6,6,4 , 8,9,2 , 8,9,9
90 READ N
100 FOR R=1 TO N:GOSUB 200:GOSUB 2200:NEXT
110 END
200 READ W,H,CN%:X=9-W:Y=20-H
210 GR:GOSUB 2700:GOSUB 1500:GOSUB 2800
220 X=X+20:GOSUB 1500
230 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 1600
240 CP%=1:CI%=0:GOSUB 1700
250 BP=1:BI=0:GOSUB 1800
260 PRINT " AIM: LEFT/RIGHT"
270 PRINT " LAY CANDY: SPACE"
280 RETURN
300 REM --- WELCOME
310 TEXT:HOME
320 T$=T2$:GOSUB 600
330 T$=T3$:GOSUB 600
340 PRINT
350 T$=T4$:GOSUB 600
360 T$=T1$:GOSUB 600
370 T$=T4$:GOSUB 600
380 PRINT
390 PRINT "FIRST TIME? PRESS [I]"
400 PRINT "FOR INSTRUCTIONS. "
410 PRINT
420 PRINT "OTHERWISE, PRESS [P]"
430 PRINT "TO PLAY!"
440 PRINT
450 GET CH$
460 IF CH$="I" THEN GOSUB 800:GOTO 300
470 IF CH$<>"P" THEN 450
480 REM SEED "RND" FROM HUMAN REACTION TIME
490 I=RND(-1*(PEEK(78)+256*PEEK(79)))
500 RETURN
600 REM --- PRINT CENTERED
610 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
700 REM --- PROMPT TO CONTINUE
710 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
800 REM --- TUTORIAL
810 W=3:H=3:CN%=3:X=9-W:Y=20-H
820 GR:HOME
830 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
840 PRINT "CANDY WEAVING!"
850 GOSUB 700
860 SEED=-222
870 T=RND(SEED):REM PREDICTABLE SEQUENCE
880 HOME:GOSUB 1500:GOSUB 2700:GOSUB 2800
890 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
900 PRINT "A TASTY TARGET PATTERN."
910 GOSUB 700
920 HOME:X=X+20:GOSUB 1500
930 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
940 PRINT "STRIPES ONE AT A TIME UNTIL THEY"
950 PRINT "MATCH THE PATTERN."
960 GOSUB 700
970 HOME
980 PRINT "FIRST: SELECT"
990 GOSUB 1600
1000 PRINT "A FLAVOR OF CANDY"
1010 PRINT "BY PRESSING ITS NUMBER";
1020 GOSUB 1900:CI%=CH-49:IF CI%<0 OR CI%>=CN% THEN 1020
1030 CP%=NOT(CI%-1)+1
1040 HOME:GOSUB 1600:GOSUB 1700
1050 PRINT "NEXT: PRESS THE RIGHT ARROW SEVERAL"
1060 PRINT "TIMES TO MOVE THE CANDY HOSE."
1070 BI=0:GOSUB 1800
1080 GOSUB 1900:IF CH<>21 THEN 1080
1090 GOSUB 2100:IF BI<>0 THEN 1080
1100 HOME:GOSUB 1600:GOSUB 1700
1110 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1120 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1130 GOSUB 1900
1140 IF CH=8 THEN GOSUB 2000
1150 IF CH=21 THEN GOSUB 2100
1160 IF CH<>32 THEN 1130:REM SPACE
1170 GOSUB 2900
1180 HOME
1190 PRINT "CONTINUE LAYING"
1200 PRINT "CANDY STRIPES UNTIL"
1210 PRINT "THE PATTERN MATCHES!"
1220 GOSUB 1600:GOSUB 1700
1230 VTAB 22:HTAB 30:PRINT"PRESS [P]"
1240 VTAB 23:HTAB 30:PRINT"TO PLAY";
1250 FOR I=1 TO 60
1260 IF PEEK(49152)>=128 THEN GET CH$
1270 IF CH$="P" THEN RETURN
1280 IF I<>20 THEN 1300
1290 CI%=INT(RND(1)*CN%):GOSUB 1700:GOSUB 1800
1300 IF I<>40 THEN 1320
1310 BI=INT(RND(1)*(W+H)):GOSUB 1800
1320 IF I<>60 THEN 1340
1330 GOSUB 2900
1340 NEXT I:GOTO 1250
1400 REM --- INITIALIZE VARIABLES
1410 DIM C(15)
1420 DIM S(25)
1430 DIM RO(24):REM GR MEM ADDR BY ROW
1440 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
1450 RETURN
1500 REM --- DRAW PLAY AREA BORDER
1510 COLOR=15
1520 HLIN X-1,X+W*2+1 AT Y-1
1530 HLIN X-1,X+W*2+1 AT Y+H*2+1
1540 VLIN Y-1,Y+H*2+1 AT X-1
1550 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
1600 REM --- DRAW COLOR PALETTE
1610 VTAB 21:HTAB 20
1620 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
1630 PRINT:RETURN
1640 PRINT "LAY A CANDY STRIPE:":HTAB X-H*2:PRINT "(";
1650 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
1660 PRINT CHR$(65+I);:NEXT:PRINT ")";
1670 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
1680 RETURN
1700 REM --- SHOW SELECTED COLOR
1710 IF CI%=CP% THEN RETURN
1720 VTAB 21:HTAB 21+CI%*2:INVERSE:PRINT CI%+1;:HTAB 21+CP%*2:NORMAL:PRINT CP%+1;
1730 CP%=CI%:PRINT:RETURN
1800 REM --- SHOW SELECTED STRIPE
1810 COLOR=0:BA=BP:GOSUB 3000
1820 GOSUB 1500
1830 COLOR=C(CI%):BA=BI:GOSUB 3000
1840 BP=BI:RETURN
1900 REM --- INPUT KEY
1910 WAIT 49152,128:GET CH$:CH = ASC(CH$)
1920 RETURN
2000 REM --- AIM HOSE
2010 BI=BI-1:IF BI<0 THEN BI=W+H-1
2020 GOSUB 1800
2030 RETURN
2100 BI=BI+1:IF BI=W+H THEN BI=0
2110 GOSUB 1800
2120 RETURN
2200 REM --- MAIN GAME LOOP
2210 GOSUB 1900
2220 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
2230 IF CH=8 THEN GOSUB 2000:GOTO 2200:REM LEFT
2240 IF CH=21 THEN GOSUB 2100:GOTO 2200:REM RIGHT
2250 IF CH=32 THEN 2350:REM SPACE
2260 T=CH-49:REM 1-9
2270 IF T>=0 AND T<CN% THEN 2310
2280 T=CH-65:REM A-Z
2290 IF T>=0 AND T<W+H THEN 2330
2300 GOTO 2200:REM OTHER
2310 REM - CHOOSE COLOR
2320 CI%=T:GOSUB 1700:GOSUB 1800:GOTO 2200
2330 REM - CHOOSE LINE
2340 BI=T
2350 GOSUB 1800:GOSUB 2900:GOSUB 2500
2360 IF MA% THEN PRINT "PROGRESS: ";MA%;" TO GO ";:GOTO 2200
2370 T=RND(1)*3:INVERSE
2380 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 2410
2390 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 2410
2400 PRINT "COMPLETED! (YUM)";
2410 NORMAL:GOSUB 700:RETURN
2500 REM --- CHECK FOR MATCH
2510 VTAB 24:HTAB 1:PRINT "COMPARING...      ";:HTAB 1
2520 MA%=0
2530 Y2=INT((Y+1)/2):FOR J=Y2 TO Y2+H-1
2540 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
2550 MA%=MA%+(PEEK(I)=PEEK(I-20))
2560 NEXT I:NEXT J
2570 MA%=(W*H-MA%)
2580 RETURN
2600 REM --- CREATE A SHUFFLE 
2610 FOR I=0 TO SN%-1:S(I)=I:NEXT
2620 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
2630 RETURN
2700 REM --- GENERATE COLOR PALETTE
2710 SN%=15:GOSUB 2600
2720 I=0:FOR CI=0 TO CN%-1
2730 T=S(I):I=I+1:IF T=0 OR T=3 OR T=5 OR T=10 THEN 2730
2740 C(CI)=T:NEXT:RETURN
2800 REM --- DRAW PATCH
2810 SN%=W+H:GOSUB 2600
2820 CI%=0
2830 FOR I=0 TO W+H-1:BI=S(I):GOSUB 2900
2840 CI%=CI%+1:IF CI%=CN% THEN CI%=0
2850 NEXT I
2860 RETURN
2900 REM --- DRAW A SINGLE BAR
2910 COLOR=C(CI%)
2920 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
2930 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
3000 REM --- DRAW AXIS INDICATOR
3010 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
3020 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
