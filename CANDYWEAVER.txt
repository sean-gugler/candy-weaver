0 GOSUB 3500
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="VERSION 1.1.0 2022-10-31"
50 T3$="BY SEAN GUGLER"
60 T4$="------------------"
70 GOTO 700
100 REM --- INPUT KEY
110 CH$="":CH=Z
120 FOR T=Z TO F
130 TI=TI+U:IF TS AND TI>=TS THEN TI=TI-TS:COLOR=9:PLOT TX,Z:TX=TX+U:IF TX=40 THEN RETURN
140 IF PEEK(KY)<K8 THEN NEXT
150 GET CH$:CH = ASC(CH$):RETURN
200 REM --- DRAW COLORED HOSE
210 GOSUB 300:BP=BI:COLOR=C(CI):T=BI+BI:PLOT BX(T),BY(T):T=T+U:PLOT BX(T),BY(T):RETURN
300 REM --- CLEAR HOSE
310 COLOR=15:T=BP+BP:PLOT BX(T),BY(T):COLOR=Z:T=T+U:PLOT BX(T),BY(T):RETURN
400 REM --- AIM HOSE
410 BI=BI-U:IF BI<Z THEN BI=W+H-U
420 GOTO 200
500 BI=BI+U:IF BI=W+H THEN BI=Z
510 GOTO 200
600 REM --- DRAW A SINGLE BAR
610 COLOR=C(CI)
620 IF BI<W THEN VLIN Y,Y+H*2 AT U+X+BI*2:RETURN
630 HLIN X,X+W*2 AT U+Y+(BI-W)*2:RETURN
700 REM --- MAIN ROUTINE
710 T=U:GOSUB 800
720 PRINT:PRINT "PREPARING THE KITCHEN...":GOSUB 3800
730 T=Z:GOSUB 800
740 GOSUB 1000
750 GOTO 730
800 REM --- WELCOME
810 TEXT:HOME
820 T$=T2$:GOSUB 4500
830 T$=T3$:GOSUB 4500
840 PRINT
850 T$=T4$:GOSUB 4500
860 IF T THEN FLASH
870 T$=T1$:GOSUB 4500:NORMAL
880 T$=T4$:GOSUB 4500
890 RETURN
1000 REM --- MAIN MENU
1010 PRINT
1020 PRINT " VIEW THE [D]EMO"
1030 PRINT " TAKE THE [T]UTORIAL"
1040 PRINT " PLAY THE [P]ROGRESSION CHALLENGE"
1050 PRINT " PRACTICE [S]PECIFIC WEAVES"
1060 PRINT
1070 PRINT " WHERE TO? ";
1080 GET CH$
1090 IF ASC(CH$)=27 THEN END:REM ESC
1100 IF CH$="D" THEN GOSUB 1200:RETURN
1110 IF CH$="T" THEN GOSUB 1500:RETURN
1120 IF CH$="S" THEN GOSUB 2200:RETURN
1130 IF CH$="P" THEN GOSUB 2600:RETURN
1140 GOTO 1080
1200 REM --- DEMO LOOP
1210 W=5:H=6:CN=7:TS=Z
1220 T=RND(SEED):REM PREDICTABLE SEQUENCE
1230 GOSUB 4800
1240 VTAB 21:PRINT "CHOOSE A FLAVOR,  "
1250 PRINT "AIM THE CANDY HOSE,";:HTAB 28:PRINT "---DEMO---"
1260 PRINT "LAY STRIPES, UNTIL THE";:HTAB 28:PRINT "PRESS A KEY"
1270 PRINT "ENTIRE PATTERN MATCHES";:HTAB 29:PRINT "TO RETURN";
1280 TI=30
1290 FOR J=Z TO MN-1
1300 IF J=13 THEN GOSUB 4100:GOSUB 4100:GOSUB 4100
1310 FOR I=U TO TI*3
1320 IF PEEK(KY)>=K8 THEN GET CH$:RETURN
1330 IF I<>TI*1 THEN 1350
1340 IF CI<>MC(J) THEN CI=MC(J):GOSUB 5300:GOSUB 200
1350 IF I<>TI*2 THEN 1370
1360 IF BI<>MB(J) THEN BI=MB(J):GOSUB 200
1370 IF I<>TI*3 THEN 1390
1380 GOSUB 600
1390 NEXT I:NEXT J
1400 COLOR=0:FOR I=Y TO Y+H*2:HLIN X,X+W*2 AT I:NEXT
1410 GOTO 1290
1500 REM --- TUTORIAL
1510 AP=AX:AX=Z
1520 W=3:H=3:CN=6:TS=Z:TX=Z
1530 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 3900
1540 GR:HOME
1550 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1560 PRINT "CANDY WEAVING!"
1570 GOSUB 4600
1580 T=RND(SEED)
1590 HOME:GOSUB 5100:GOSUB 5600:GOSUB 5400
1600 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1610 PRINT "A TASTY TARGET PATTERN."
1620 GOSUB 4600
1630 HOME:X=X+20:GOSUB 5100
1640 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1650 PRINT "STRIPES ONE AT A TIME UNTIL YOUR"
1660 PRINT "WHOLE PATTERN MATCHES THE TARGET."
1670 GOSUB 4600
1680 HOME
1690 PRINT "FIRST: SELECT"
1700 GOSUB 5200
1710 PRINT "A FLAVOR OF CANDY"
1720 PRINT "BY PRESSING ITS NUMBER";
1730 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1740 CI=CH-49:IF CI<Z OR CI>=CN THEN 1730
1750 HOME:GOSUB 5200:CP=NOT CI:GOSUB 5300
1760 PRINT "NEXT: PRESS THE RIGHT ARROW"
1770 PRINT "TO SEE HOW THE CANDY HOSE MOVES."
1780 BP=U:BI=Z:GOSUB 6000:GOSUB 200
1790 T$="KEEP GOING UNTIL IT TURNS THE CORNER..."
1800 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1810 IF CH<>21 THEN 1800
1820 IF BI=2 THEN T$="KEEP GOING UNTIL IT PASSES THE END..."
1830 VTAB 24:HTAB U:PRINT T$;
1840 GOSUB 500:IF BI<>Z THEN 1800
1850 HOME:GOSUB 5200:CP=NOT CI:GOSUB 5300
1860 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1870 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1880 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1890 IF CH=11 OR CH=8 THEN GOSUB 400
1900 IF CH=10 OR CH=21 THEN GOSUB 500
1910 IF CH<>32 THEN 1880:REM SPACE
1920 GOSUB 600
1930 HOME
1940 PRINT "CONTINUE LAYING"
1950 PRINT "CANDY STRIPES UNTIL"
1960 PRINT "THE PATTERNS MATCH EXACTLY"
1970 GOSUB 5200:CP=NOT CI:GOSUB 5300
2000 REM --- TUTORIAL PLAY LOOP
2010 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
2020 IF CH=11 OR CH=8 THEN GOSUB 400:GOTO 2000:REM LEFT
2030 IF CH=10 OR CH=21 THEN GOSUB 500:GOTO 2000:REM RIGHT
2040 IF CH=32 OR CH=13 THEN 2080:REM SPACE, RETURN
2050 T=CH-49:REM 1-9
2060 IF T<Z OR T>=CN THEN 2000
2070 CI=T:GOSUB 5300:GOSUB 200:GOTO 2000
2080 GOSUB 200:GOSUB 600
2090 GOSUB 4000:IF M THEN 2000
2100 GOSUB 300
2110 VTAB 24:INVERSE:PRINT "I SAY; WELL DONE!";:NORMAL:GOSUB 4600
2120 AX=AP
2130 RETURN
2200 REM --- PRACTICE MENU
2210 VTAB 13:HTAB U
2220 PRINT "  WIDTH (2-8): ";W
2230 PRINT " HEIGHT (2-9): ";H
2240 PRINT "FLAVORS (2-9): ";CN
2250 PRINT "  TIMER (0-9): ";TV
2260 PRINT
2270 HTAB 10:PRINT "[SPACE] TO BEGIN"
2280 HTAB 10:PRINT "[ESC]   TO CANCEL"
2290 S(0)=W:S(1)=H:S(2)=CN:S(3)=TV:TS=Z:TX=Z
2300 I=Z
2310 VTAB 13+I:HTAB 16:INVERSE:PRINT S(I);:NORMAL
2320 GOSUB 100
2330 IF CH=27 THEN RETURN:REM ESC
2340 IF CH=32 THEN 2420:REM SPACE
2350 IF CH=13 THEN CH=S(I)+48:REM RETURN KEY
2360 N=CH-48:REM "0"
2370 IF N<(2 * (I<>3)) OR N>(WM + (I<>Z)) THEN 2320
2380 S(I)=N
2390 HTAB 16:PRINT S(I)
2400 I=I+U:IF I>3 THEN I=Z
2410 GOTO 2310
2420 W=S(0):H=S(1):CN=S(2):TV=S(3):TS=TA(TV):GOSUB 4700
2500 REM --- PLAY A PRACTICE ROUND
2510 GOSUB 4800:GOSUB 4200
2520 GOSUB 2900:IF TX=-1 THEN RETURN
2530 VTAB 24:HTAB 24:PRINT "ANOTHER ROUND?";
2540 GET CH$
2550 IF CH$="Y" OR CH=13 THEN 2500
2560 IF CH$="N" OR CH=27 THEN RETURN
2570 GOTO 2540
2600 REM --- PLAY CHALLENGE WAVES
2610 GOSUB 4700
2620 FOR R=Z TO DN-U
2630 W=DW(R):H=DH(R):CN=DC(R):TS=DT(R)
2640 HOME:PRINT DS$(R);:GOSUB 4600
2650 IF NOT TS THEN 2690
2660 HOME:FLASH:VTAB 22
2670 T$="*** TIMED ROUND! ***":GOSUB 4500
2680 NORMAL:GOSUB 4600
2690 GOSUB 4800:GOSUB 4200
2700 GOSUB 2900:IF TX=-1 THEN RETURN
2710 IF TX<>40 THEN 2770
2720 PRINT "KEEP PRACTICING, YOU CAN DO IT!"
2730 PRINT:HTAB 27:PRINT "PRESS ESC";:TS=0
2740 GOSUB 4100:IF PEEK(KY)>=K8 THEN GOSUB 100
2750 IF CH<>27 THEN 2740
2760 RETURN
2770 GOSUB 4600
2780 NEXT
2790 GR:HOME
2800 INVERSE:T$="CONGRATULATIONS!":GOSUB 4500:NORMAL
2810 PRINT "THE CELEBRATION LASTS FOR DAYS, AND"
2820 PRINT "SHOWERS OUR CHAMPION WITH CANDY PRAISE"
2830 PRINT "(PRESS SPACE TO REST ON YOUR LAURELS)";
2840 IF PEEK(KY)>=K8 THEN GET CH$:RETURN
2850 COLOR=RND(1)*14+1:T=INT(RND(1)*19)*2+1
2860 IF RND(1)<.5 THEN 2880
2870 HLIN Z,38 AT T:GOTO 2840
2880 VLIN Z,38 AT T:GOTO 2840
2900 REM --- MAIN GAME LOOP
2910 FOR Q=Z TO F
2920 GOSUB 100
2930 TI=TI+7
2940 IF TX=40 THEN 3380
2950 IF CH=27 THEN 3310:REM ESC
2960 IF CH=11 OR CH=8 THEN GOSUB 400:TI=TI+2:NEXT Q:REM LEFT
2970 IF CH=10 OR CH=21 THEN GOSUB 500:TI=TI+2:NEXT Q:REM RIGHT
2980 IF CH=32 OR CH=13 THEN 3220:REM SPACE, RETURN
2990 IF CH=84 THEN TI=TI+4:AX=NOT AX:GOSUB 4200:NEXT Q:REM TOGGLE LABELS
3000 IF CH=82 THEN GOSUB 4100:NEXT Q:REM REVEAL DIFFS
3010 T=CH-49:REM 1-9
3020 IF T>=Z AND T<CN THEN 3100
3030 T=CH-65:REM A-Q
3040 IF T>=Z AND T<W THEN 3200
3050 T=T-WM:IF T>=Z AND T<H THEN T=T+W:GOTO 3200
3060 NEXT Q:REM OTHER
3100 REM - CHOOSE COLOR
3110 TI=TI+4:CI=T:GOSUB 5300:GOSUB 200:NEXT Q
3200 REM - CHOOSE LINE
3210 BI=T:TI=TI+5:GOSUB 200
3220 TI=TI+3+(W*H*3/4)
3230 GOSUB 600
3240 VTAB 24:HTAB U:CALL(E):PRINT "COMPARING...      ";:HTAB U:GOSUB 4000
3250 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:NEXT Q
3260 GOSUB 300
3270 T=RND(U)*3:INVERSE
3280 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 3390
3290 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 3390
3300 PRINT "COMPLETED! (YUM)";:GOTO 3390
3310 GOSUB 4400
3320 GOSUB 100
3330 IF TX=40 THEN 3380
3340 IF CH=78 OR CH=27 THEN GOSUB 4200:TI=TI+3:NEXT Q:REM [N]O
3350 IF CH=89 THEN TX=-1:HOME:GOTO 3390:REM [Y]ES
3360 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
3370 TI=TI+3:GOTO 3320
3380 HOME:FLASH:PRINT "OUT OF TIME!"
3390 NORMAL:RETURN
3500 REM --- DECLARE VARIABLES
3510 REM - CONSTANTS
3520 Z=0:U=1
3530 KY=49152:REM PEEK KEYBOARD
3540 K8=128
3550 E=-868:REM CALL ERASE_LINE
3560 F=1E30:REM INFINITY
3570 REM - PERFORMANCE-CRITICAL VARIABLES
3580 M=Z:I=Z:O=-20:J=Z:A=Z:W2=Z:X=Z:Y=Z:J0=Z:J1=Z:WH=9:W=3:H=3
3590 CN=6
3600 TV=Z
3610 AX=Z
3620 WM=8
3630 SEED=-222
3640 DIM RO(24):REM GR MEM ADDR BY ROW
3650 DIM BX(33),BY(33):REM HOSE COORDINATES
3660 DIM C(15):REM COLOR PALETTE
3670 DIM S(25):REM SHUFFLE SEQUENCE
3680 DIM CC(15*5-1):REM COLOR CLASH TABLE
3690 DIM TA(9):REM TIMER PRESETS
3700 DIM DW(30),DH(30),DC(30),DT(30),DS$(30):REM LEVEL DATA
3710 DIM MC(30),MB(30):REM DEMO DATA
3720 RETURN
3800 REM --- INITIALIZE GAME DATA
3810 FOR I=Z TO 7:FOR J=Z TO 2:RO(I+8*J)=1024+128*I+40*J:NEXT J:NEXT I
3820 FOR I=Z TO 9:READ TA(I):NEXT I
3830 GOSUB 5500
3840 READ MN:FOR I=Z TO MN-U:READ MC(I),MB(I):NEXT
3850 READ DN:FOR I=Z TO DN-U:READ DW(I),DH(I),DC(I),DT(I),DS$(I):NEXT
3860 RETURN
3900 REM --- INIT CHECK VARIABLES
3910 J0=(Y+U)/2:J1=J0+H-U:W2=W*2
3920 RETURN
4000 REM --- CHECK FOR MATCH
4010 M=Z:FOR J=J0 TO J1
4020 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
4030 M=M+(PEEK(I)=PEEK(I+O))
4040 NEXT:NEXT J
4050 M=WH-M
4060 RETURN
4100 REM --- REVEAL DIFFERENCES
4110 FOR J=J0 TO J1
4120 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
4130 P=PEEK(I):IF P=PEEK(I+O) THEN 4160
4140 POKE(I),255:FOR T=U TO 200:NEXT:POKE(I),P
4150 IF PEEK(KY)>=K8 THEN RETURN
4160 NEXT I:NEXT J:RETURN
4200 REM --- HELP TEXT
4210 VTAB 22:HTAB U
4220 IF AX THEN 4300
4230 CALL(E):PRINT " AIM: LEFT/RIGHT";
4240 HTAB 24:PRINT "[T]OGGLE LABELS"
4250 CALL(E):PRINT " LAY CANDY: SPACE";
4260 RETURN
4300 CALL(E):PRINT " PRESS A LETTER";
4310 HTAB X-3:PRINT "COL:";
4320 FOR I=Z TO W-U:PRINT " ";CHR$(65+I);:NEXT
4330 PRINT:CALL(E):PRINT " TO FAST-STRIPE";
4340 HTAB 27-H:PRINT "ROW:";
4350 FOR I=WM TO WM+H-U:PRINT " ";CHR$(65+I);:NEXT
4360 RETURN
4400 REM --- ABORT PROMPT
4410 VTAB 22:HTAB U:CALL(E)
4420 HTAB 24:PRINT "ABANDON GAME?"
4430 CALL(E)
4440 RETURN
4500 REM --- PRINT CENTERED
4510 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
4600 REM --- PROMPT TO CONTINUE
4610 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
4700 REM --- SEED "RND" FROM HUMAN REACTION TIME
4710 I=RND(-1*(PEEK(78)+256*PEEK(79))):RETURN
4800 REM --- LAYOUT A NEW WEAVE
4810 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 3900
4820 TI=Z:TX=Z
4830 GR:HOME:GOSUB 5600:GOSUB 5100:GOSUB 5400
4840 X=X+20:GOSUB 5100
4850 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 5200
4860 CP=U:CI=Z:GOSUB 5300
4870 GOSUB 6000:BP=U:BI=Z:GOSUB 200
4880 IF TS THEN COLOR=4:HLIN Z,39 AT Z
4890 RETURN
5100 REM --- DRAW PLAY AREA BORDER
5110 COLOR=15
5120 HLIN X-U,X+W*2+U AT Y-U
5130 HLIN X-U,X+W*2+U AT Y+H*2+U
5140 VLIN Y-U,Y+H*2+U AT X-U
5150 VLIN Y-U,Y+H*2+U AT X+W*2+U
5160 RETURN
5200 REM --- DRAW COLOR PALETTE
5210 VTAB 21:HTAB 20
5220 FOR I=Z TO CN-U:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+U;:NEXT
5230 PRINT:RETURN
5300 REM --- SHOW SELECTED COLOR
5310 IF CI=CP THEN RETURN
5320 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+U;:HTAB 21+CP*2:NORMAL:PRINT CP+U;
5330 CP=CI:PRINT:RETURN
5400 REM --- DRAW PATCH
5410 SN=W+H:GOSUB 5800
5420 CI=Z
5430 FOR I=Z TO W+H-U:BI=S(I):GOSUB 600
5440 CI=CI+U:IF CI=CN THEN CI=Z
5450 NEXT I
5460 GOSUB 5900
5470 RETURN
5500 REM --- TABLE OF CLASHING COLORS
5510 FOR I=Z TO 15:S(I)=Z:NEXT
5520 FOR K=U TO 14
5530 READ I,J
5540 N=S(I):CC(I*5+N)=J:S(I)=N+U
5550 N=S(J):CC(J*5+N)=I:S(J)=N+U
5560 NEXT K
5570 RETURN
5600 REM --- GENERATE COLOR PALETTE
5610 M=Z:VTAB 21:PRINT "SELECTING FLAVORS..."
5620 M=M+U:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
5630 FOR N=Z TO 13:S(N)=N+U:NEXT
5640 FOR CI=CN-U TO Z STEP -1
5650 IF N<=CI THEN 5620
5660 I=RND(U)*N
5670 K=S(I):C(CI)=K
5680 N=N-U:S(I)=S(N)
5690 K=K*5
5700 FOR J=K TO K+4
5710 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
5720 FOR I=T TO N-U
5730 IF A=S(I) THEN N=N-U:S(I)=S(N):I=N
5740 NEXT I
5750 NEXT J
5760 NEXT CI
5770 RETURN
5800 REM --- CREATE A SHUFFLE 
5810 FOR I=Z TO SN-U:S(I)=I:NEXT
5820 FOR I=SN-U TO Z STEP -1:J=RND(U)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
5830 RETURN
5900 REM --- SHUFFLE COLOR PALETTE
5910 FOR I=CN-U TO Z STEP -1:J=RND(U)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
5920 RETURN
6000 REM --- INITIALIZE HOSE COORDINATES
6010 Y1=Y-2:Y0=Y1+U:T=U+X
6020 FOR I=Z TO (W-U)*2 STEP 2
6030 BX(I)=T:BX(I+U)=T
6040 BY(I)=Y0:BY(I+U)=Y1
6050 T=T+2:NEXT
6060 X0=X+W*2+U:X1=X0+U:T=U+Y
6070 FOR I=I TO (W+H-U)*2 STEP 2
6080 BX(I)=X0:BX(I+U)=X1
6090 BY(I)=T:BY(I+U)=T
6100 T=T+2:NEXT
6110 RETURN
6200 REM - TIMER PRESETS
6210 DATA 0,90,80,70,60,50,40,30,20,10
6220 REM - CLASHING COLOR PAIRS
6230 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
6240 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
6250 REM - DEMO SEQUENCE
6260 DATA 22
6270 DATA 4,6 , 2,10 , 3,3 , 3,0, 0,0
6280 DATA 6,9 , 4,1 , 1,5 , 6,7 , 2,7
6290 DATA 3,8 , 0,4 , 5,2
6300 DATA 1,5 , 6,9 , 4,1 , 2,7 , 3,8
6310 DATA 0,4 , 0,4 , 0,4 , 0,4
6320 REM - CAMPAIGN SEQUENCE
6330 DATA 25
6340 DATA 5,3,6,0,"MEET MR. WYDE-LOWE, THE LOCAL DANDY.    SIDEWAYS IS HOW HE LIKES HIS CANDY!"
6350 DATA 6,3,6,0,"A VERY GOOD START. BUT NOW HE WANTS MORESO MAKE HIM A WIDER ONE THAN BEFORE.
6360 DATA 7,3,6,0,"WIDER! WIDER! AND WIDER STILL!          SURELY THEN HE WILL HAVE HIS FILL."
6370 DATA 4,3,6,70,"FOR DEAR MR. WYDE-LOWE, A FINAL REQUEST:A QUICK LITTLE ONE TO GIVE TO HIS GUEST."
6380 DATA 3,4,6,0,"THE LADY HIGHTOWER IS RATHER IMPRESSED. CAN YOU MAKE A TALL ONE UNLIKE THE REST?"
6390 DATA 3,6,6,0,"NOT A BAD START, BUT SHE HAS IN MIND    A WEAVE OF THE TALLER AND TALLERER KIND!"
6400 DATA 3,8,6,0,"SPLENDID! THIS CANDY DELIGHTS HER SO.   SHE WONDERS ALOUD, HOW HIGH CAN YOU GO?"
6410 DATA 3,4,6,70,"THE LADY HAS SUDDENLY ONE FINAL THOUGHT:A GIFT FOR MY NEPHEW! I ALMOST FORGOT."
6420 DATA 5,5,5,0,"DISMAY! NEPHEW SQUAREJAW CANNOT ABIDE   LOPSIDED CANDY TOO LONG ON ONE SIDE."
6430 DATA 6,6,6,0,"SQUAREJAW'S GAZE REMAINS RATHER TOUGH.  THAT WEAVE WAS SIMPLY NOT BIG ENOUGH."
6440 DATA 7,7,7,0,"GREEDY DESIRES NOW SHOW IN HIS GRIN;    BIGGER AND BIGGER ONES LIFT UP HIS CHIN!"
6450 DATA 4,4,4,60,"CRACKING HIS KNUCKLES, SQUAREJAW DARES: HOW FAST CAN YOU ASSEMBLE THESE SQUARES?"
6460 DATA 8,4,9,0,"THE RAINBOW TWINS RUSH UP WITH DEMANDS. FERRARI: A LOW ONE MADE BY YOUR HANDS!"
6470 DATA 4,8,9,0,"THE TWINS CAN NEVER SEEM TO AGREE.      ROCKET: A TOWERING CANDY FOR ME!"
6480 DATA 4,4,8,40,"THEIR TEMPERS ERUPT OVER WHICH IS BEST. MAKE THEM A COMPROMISE! PUT IT TO REST!"
6490 DATA 8,6,2,0,"PACIFIED, THEY HAVE A RIDDLE FOR YOU.   DO YOU KNOW HOW TO MAKE ONE WITH TWO?"
6500 DATA 6,9,2,0,"BY NOW YOU PROBABLY KNOW WHAT IS NEXT.  FLIP IT ON END BEFORE WE GET VEXED!"
6510 DATA 8,9,2,0,"TO FINISH OFF THE RIDDLE WE'VE SPUN:    DO BOTH SHAPES TOGETHER; TWO INTO ONE."
6520 DATA 5,6,2,90,"LEADER MCGRIMLY NOW BEGS FOR YOUR SKILLSON A SERIOUS TASK. WORK FAST, NO FRILLS!"
6530 DATA 7,8,5,0,"OUR CANDY DOME IS UNDER ATTACK!         MAKE A BIG NET SO WE CAN FIGHT BACK."
6540 DATA 7,8,7,0,"THE SWEET-TOOTH, ALAS, EVADED OUR NET.  WE NEED TO WEAVE IN MORE FLAVORS, I BET."
6550 DATA 7,8,9,0,"USE ALL THE FLAVORS, SPARE NO EXPENSE!  OUR SITUATION IS GETTING INTENSE."
6560 DATA 5,6,9,60,"OH NO! IT ATE A BIG HOLE IN THE DOME!   WEAVE LIKE THE WIND AND RESCUE OUR HOME!"
6570 DATA 3,3,6,20,"IS THERE NO END TO THIS CANDY DISASTER? HURRY, SCURRY! WEAVE FASTER AND FASTER!"
6580 DATA 8,9,9,0,"THE CREATURE IS GONE - THE DAY IS WON!  A VICTORY BANNER WE NEED; JUST FOR FUN."
