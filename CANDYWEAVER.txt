0 GOSUB 3200
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="VERSION 1.0.1 2022-09-16"
50 T3$="BY SEAN GUGLER"
60 T4$="------------------"
70 GOTO 700
100 REM --- INPUT KEY
110 CH$="":CH=Z
120 FOR T=Z TO F
130 TI=TI+U:IF TI=TS THEN TI=Z:COLOR=9:PLOT TX,Z:TX=TX+U:IF TX=40 THEN RETURN
140 IF PEEK(KY)<K8 THEN NEXT
150 GET CH$:CH = ASC(CH$):RETURN
200 REM --- DRAW COLORED HOSE
210 GOSUB 300:BP=BI:COLOR=C(CI):T=BI+BI:PLOT BX(T),BY(T):T=T+U:PLOT BX(T),BY(T):RETURN
300 REM --- CLEAR HOSE
310 COLOR=15:T=BP+BP:PLOT BX(T),BY(T):COLOR=Z:T=T+U:PLOT BX(T),BY(T):RETURN
400 REM --- AIM HOSE
410 BI=BI-U:IF BI<Z THEN BI=W+H-U
420 GOTO 200
500 BI=BI+U:IF BI=W+H THEN BI=Z
510 GOTO 200
600 REM --- DRAW A SINGLE BAR
610 COLOR=C(CI)
620 IF BI<W THEN VLIN Y,Y+H*2 AT U+X+BI*2:RETURN
630 HLIN X,X+W*2 AT U+Y+(BI-W)*2:RETURN
700 REM --- MAIN ROUTINE
710 T=U:GOSUB 800
720 PRINT:PRINT "PREPARING THE KITCHEN...":GOSUB 3400
730 HOME:T=Z:GOSUB 800
740 GOSUB 1000
750 GOTO 730
800 REM --- WELCOME
810 TEXT:HOME
820 T$=T2$:GOSUB 4100
830 T$=T3$:GOSUB 4100
840 PRINT
850 T$=T4$:GOSUB 4100
860 IF T THEN FLASH
870 T$=T1$:GOSUB 4100:NORMAL
880 T$=T4$:GOSUB 4100
890 RETURN
1000 REM --- MAIN MENU
1010 PRINT
1020 PRINT " VIEW THE [D]EMO"
1030 PRINT " TAKE THE [T]UTORIAL"
1040 PRINT " PLAY THE [P]ROGRESSION CHALLENGE"
1050 PRINT " PRACTICE [S]PECIFIC WEAVES"
1060 PRINT
1070 PRINT " WHERE TO? ";
1080 GET CH$
1090 IF ASC(CH$)=27 THEN END:REM ESC
1100 IF CH$="D" THEN GOSUB 1200:RETURN
1110 IF CH$="T" THEN GOSUB 1400:RETURN
1120 IF CH$="S" THEN GOSUB 2100:RETURN
1130 IF CH$="P" THEN GOSUB 2500:RETURN
1140 GOTO 1080
1200 REM --- DEMO LOOP
1210 W=5:H=6:CN=7:TS=Z
1220 T=RND(SEED):REM PREDICTABLE SEQUENCE
1230 GOSUB 4400
1240 VTAB 21:PRINT "CHOOSE A FLAVOR,  "
1250 PRINT "AIM THE CANDY HOSE,";:HTAB 28:PRINT "---DEMO---"
1260 PRINT "LAY STRIPES, UNTIL THE";:HTAB 28:PRINT "PRESS A KEY"
1270 PRINT "ENTIRE PATTERN MATCHES";:HTAB 29:PRINT "TO RETURN";
1280 FOR I=U TO 60
1290 IF PEEK(KY)>=K8 THEN GET CH$:RETURN
1300 IF I<>20 THEN 1330
1310 T=INT(RND(U)*CN):IF T=CI THEN 1310
1320 CI=T:GOSUB 4800:GOSUB 200
1330 IF I<>40 THEN 1360
1340 T=INT(RND(U)*(W+H)):IF T=BI THEN 1340
1350 BI=T:GOSUB 200
1360 IF I<>60 THEN 1380
1370 GOSUB 600
1380 NEXT I:GOTO 1280
1400 REM --- TUTORIAL
1410 AP=AX:AX=Z
1420 W=3:H=3:CN=6:TS=Z:TX=Z
1430 X=9-W:Y=20-H:GOSUB 3500
1440 GR:HOME
1450 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1460 PRINT "CANDY WEAVING!"
1470 GOSUB 4200
1480 T=RND(SEED)
1490 HOME:GOSUB 4600:GOSUB 5100:GOSUB 4900
1500 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1510 PRINT "A TASTY TARGET PATTERN."
1520 GOSUB 4200
1530 HOME:X=X+20:GOSUB 4600
1540 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1550 PRINT "STRIPES ONE AT A TIME UNTIL YOUR"
1560 PRINT "WHOLE PATTERN MATCHES THE TARGET."
1570 GOSUB 4200
1580 HOME
1590 PRINT "FIRST: SELECT"
1600 GOSUB 4700
1610 PRINT "A FLAVOR OF CANDY"
1620 PRINT "BY PRESSING ITS NUMBER";
1630 GOSUB 100:CI=CH-49:IF CI<Z OR CI>=CN THEN 1630
1640 CP=NOT(CI-U)+U
1650 HOME:GOSUB 4700:GOSUB 4800
1660 PRINT "NEXT: PRESS THE RIGHT ARROW"
1670 PRINT "TO SEE HOW THE CANDY HOSE MOVES."
1680 BI=Z:GOSUB 5500:GOSUB 200
1690 T$="KEEP GOING UNTIL IT TURNS THE CORNER..."
1700 GOSUB 100:IF CH<>21 THEN 1700
1710 IF BI=2 THEN T$="KEEP GOING UNTIL IT PASSES THE END..."
1720 VTAB 24:HTAB U:PRINT T$;
1730 GOSUB 500:IF BI<>Z THEN 1700
1740 HOME:GOSUB 4700:CP=NOT CI:GOSUB 4800
1750 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1760 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1770 GOSUB 100
1780 IF CH=11 OR CH=8 THEN GOSUB 400
1790 IF CH=10 OR CH=21 THEN GOSUB 500
1800 IF CH<>32 THEN 1770:REM SPACE
1810 GOSUB 600
1820 HOME
1830 PRINT "CONTINUE LAYING"
1840 PRINT "CANDY STRIPES UNTIL"
1850 PRINT "THE PATTERNS MATCH EXACTLY"
1860 GOSUB 4700:CP=NOT CI:GOSUB 4800
1900 REM --- TUTORIAL PLAY LOOP
1910 GOSUB 100
1920 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
1930 IF CH=11 OR CH=8 THEN GOSUB 400:GOTO 1900:REM LEFT
1940 IF CH=10 OR CH=21 THEN GOSUB 500:GOTO 1900:REM RIGHT
1950 IF CH=32 OR CH=13 THEN 1990:REM SPACE, RETURN
1960 T=CH-49:REM 1-9
1970 IF T<Z OR T>=CN THEN 1900
1980 CI=T:GOSUB 4800:GOSUB 200:GOTO 1900
1990 GOSUB 200:GOSUB 600
2000 GOSUB 3600:IF M THEN 1900
2010 GOSUB 300
2020 VTAB 24:INVERSE:PRINT "I SAY; WELL DONE!";:NORMAL:GOSUB 4200
2030 AX=AP
2040 RETURN
2100 REM --- PRACTICE MENU
2110 VTAB 13:HTAB U
2120 PRINT "  WIDTH (2-8): ";W
2130 PRINT " HEIGHT (2-9): ";H
2140 PRINT "FLAVORS (2-9): ";CN
2150 PRINT "  TIMER (0-9): ";TV
2160 PRINT
2170 HTAB 10:PRINT "[SPACE] TO BEGIN"
2180 HTAB 10:PRINT "[ESC]   TO CANCEL"
2190 S(0)=W:S(1)=H:S(2)=CN:S(3)=TV:TS=Z:TX=Z
2200 I=Z
2210 VTAB 13+I:HTAB 16:INVERSE:PRINT S(I);:NORMAL
2220 GOSUB 100
2230 IF CH=27 THEN RETURN:REM ESC
2240 IF CH=32 THEN 2320:REM SPACE
2250 IF CH=13 THEN CH=S(I)+48:REM RETURN KEY
2260 N=CH-48:REM "0"
2270 IF N<(2 * (I<>3)) OR N>(8 + (I<>Z)) THEN 2220
2280 S(I)=N
2290 HTAB 16:PRINT S(I)
2300 I=I+U:IF I>3 THEN I=Z
2310 GOTO 2210
2320 W=S(0):H=S(1):CN=S(2):TV=S(3):TS=TA(TV):GOSUB 4300
2400 REM --- PLAY A PRACTICE ROUND
2410 GOSUB 4400:GOSUB 3800
2420 GOSUB 2800:IF TX=-1 THEN RETURN
2430 VTAB 24:HTAB 24:PRINT "ANOTHER ROUND?";
2440 GET CH$
2450 IF CH$="Y" OR CH=13 THEN 2400
2460 IF CH$="N" OR CH=27 THEN RETURN
2470 GOTO 2440
2500 REM --- PLAY CHALLENGE WAVES
2510 GOSUB 4300
2520 FOR R=Z TO DN-U
2530 W=DW(R):H=DH(R):CN=DC(R):TS=DT(R)
2540 IF NOT TS THEN 2580
2550 HOME:FLASH:VTAB 22
2560 T$="*** TIMED ROUND! ***":GOSUB 4100
2570 NORMAL:GOSUB 4200
2580 GOSUB 4400:GOSUB 3800
2590 GOSUB 2800:IF TX=-1 THEN RETURN
2600 IF TX<>40 THEN 2640
2610 PRINT "BETTER LUCK NEXT TIME!"
2620 GOSUB 4200
2630 RETURN
2640 GOSUB 4200
2650 NEXT
2660 TEXT:HOME:INVERSE
2670 T$="CONGRATULATIONS!":GOSUB 4100
2680 NORMAL:VTAB 4:PRINT "YOU'VE WOVEN ALL THE CANDY"
2690 PRINT:PRINT "YOU ARE A MASTER CANDY WEAVER!"
2700 GOSUB 4200
2710 RETURN
2800 REM --- MAIN GAME LOOP
2810 FOR Q=Z TO F
2820 GOSUB 100
2830 IF TX=40 THEN 3140
2840 IF CH=27 THEN 3070:REM ESC
2850 IF CH=11 OR CH=8 THEN GOSUB 400:NEXT Q:REM LEFT
2860 IF CH=10 OR CH=21 THEN GOSUB 500:NEXT Q:REM RIGHT
2870 IF CH=32 OR CH=13 THEN 2990:REM SPACE, RETURN
2880 IF CH=84 THEN AX=NOT AX:GOSUB 3800:NEXT Q:REM TOGGLE EXPERT
2890 IF CH=82 THEN GOSUB 3700:NEXT Q:REM REVEAL DIFFS
2900 T=CH-49:REM 1-9
2910 IF T>=Z AND T<CN THEN 2950
2920 T=CH-65:REM A-Z
2930 IF T>=Z AND T<W+H THEN 2970
2940 NEXT Q:REM OTHER
2950 REM - CHOOSE COLOR
2960 CI=T:GOSUB 4800:GOSUB 200:NEXT Q
2970 REM - CHOOSE LINE
2980 BI=T:GOSUB 200
2990 GOSUB 600
3000 VTAB 24:HTAB U:CALL(-868):PRINT "COMPARING...      ";:HTAB U:GOSUB 3600
3010 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:NEXT Q
3020 GOSUB 300
3030 T=RND(U)*3:INVERSE
3040 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 3150
3050 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 3150
3060 PRINT "COMPLETED! (YUM)";:GOTO 3150
3070 GOSUB 4000
3080 GOSUB 100
3090 IF TX=40 THEN 3140
3100 IF CH=78 OR CH=27 THEN GOSUB 3800:NEXT Q:REM [N]O
3110 IF CH=89 THEN TX=-1:HOME:GOTO 3150:REM [Y]ES
3120 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
3130 GOTO 3080
3140 HOME:FLASH:PRINT "OUT OF TIME!"
3150 NORMAL:RETURN
3200 REM --- DECLARE VARIABLES
3210 Z=0:U=1:KY=49152:K8=128:F=1E30
3220 M=Z:I=Z:O=-20:J=Z:A=Z:W2=Z:X=Z:Y=Z:J0=Z:J1=Z:WH=9:W=3:H=3
3230 CN=6
3240 TV=Z
3250 AX=Z
3260 SEED=-222
3270 DIM RO(24):REM GR MEM ADDR BY ROW
3280 DIM BX(33),BY(33):REM HOSE COORDINATES
3290 DIM C(15):REM COLOR PALETTE
3300 DIM S(25):REM SHUFFLE SEQUENCE
3310 DIM CC(15*5-1):REM COLOR CLASH TABLE
3320 DIM TA(9):REM TIMER PRESETS
3330 DIM DW(30),DH(30),DC(30),DT(30):REM LEVEL DATA
3340 RETURN
3400 REM --- INITIALIZE GAME DATA
3410 FOR I=Z TO 7:FOR J=Z TO 2:RO(I+8*J)=1024+128*I+40*J:NEXT J:NEXT I
3420 FOR I=Z TO 9:READ TA(I):NEXT I
3430 GOSUB 5000
3440 READ DN:FOR I=Z TO DN-U:READ DW(I),DH(I),DC(I),DT(I):NEXT
3450 RETURN
3500 REM --- INIT CHECK VARIABLES
3510 J0=(Y+U)/2:J1=J0+H-U:W2=W*2
3520 RETURN
3600 REM --- CHECK FOR MATCH
3610 M=Z:FOR J=J0 TO J1
3620 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
3630 M=M+(PEEK(I)=PEEK(I+O))
3640 NEXT:NEXT J
3650 M=WH-M
3660 RETURN
3700 REM --- REVEAL DIFFERENCES
3710 FOR J=J0 TO J1
3720 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
3730 P=PEEK(I):IF P=PEEK(I+O) THEN 3760
3740 POKE(I),255:FOR T=U TO 200:NEXT:POKE(I),P
3750 IF PEEK(KY)>=K8 THEN RETURN
3760 NEXT I:NEXT J:RETURN
3800 REM --- HELP TEXT
3810 VTAB 22:HTAB U
3820 IF AX THEN 3870
3830 CALL(-868):PRINT " AIM: LEFT/RIGHT";
3840 HTAB 24:PRINT "[T]OGGLE LABELS"
3850 CALL(-868):PRINT " LAY CANDY: SPACE";
3860 RETURN
3870 CALL(-868):PRINT "PRESS A LETTER";
3880 HTAB X-3:PRINT "COL:";
3890 FOR I=Z TO W-U:PRINT " ";CHR$(65+I);:NEXT
3900 PRINT:CALL(-868):PRINT "TO FAST-STRIPE";
3910 HTAB 26-H:PRINT "ROW:";
3920 FOR I=W TO W+H-U:PRINT " ";CHR$(65+I);:NEXT
3930 RETURN
4000 REM --- ABORT PROMPT
4010 VTAB 22:HTAB U:CALL(-868)
4020 HTAB 24:PRINT "ABANDON GAME?"
4030 CALL(-868)
4040 RETURN
4100 REM --- PRINT CENTERED
4110 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
4200 REM --- PROMPT TO CONTINUE
4210 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
4300 REM --- SEED "RND" FROM HUMAN REACTION TIME
4310 I=RND(-1*(PEEK(78)+256*PEEK(79))):RETURN
4400 REM --- LAYOUT A NEW WEAVE
4410 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 3500
4420 TI=Z:TX=Z
4430 GR:HOME:GOSUB 5100:GOSUB 4600:GOSUB 4900
4440 X=X+20:GOSUB 4600
4450 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 4700
4460 CP=U:CI=Z:GOSUB 4800
4470 GOSUB 5500:BP=U:BI=Z:GOSUB 200
4480 IF TS THEN COLOR=4:HLIN Z,39 AT Z
4490 RETURN
4600 REM --- DRAW PLAY AREA BORDER
4610 COLOR=15
4620 HLIN X-U,X+W*2+U AT Y-U
4630 HLIN X-U,X+W*2+U AT Y+H*2+U
4640 VLIN Y-U,Y+H*2+U AT X-U
4650 VLIN Y-U,Y+H*2+U AT X+W*2+U:RETURN
4700 REM --- DRAW COLOR PALETTE
4710 VTAB 21:HTAB 20
4720 FOR I=Z TO CN-U:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+U;:NEXT
4730 PRINT:RETURN
4800 REM --- SHOW SELECTED COLOR
4810 IF CI=CP THEN RETURN
4820 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+U;:HTAB 21+CP*2:NORMAL:PRINT CP+U;
4830 CP=CI:PRINT:RETURN
4900 REM --- DRAW PATCH
4910 SN=W+H:GOSUB 5300
4920 CI=Z
4930 FOR I=Z TO W+H-U:BI=S(I):GOSUB 600
4940 CI=CI+U:IF CI=CN THEN CI=Z
4950 NEXT I
4960 GOSUB 5400
4970 RETURN
5000 REM --- TABLE OF CLASHING COLORS
5010 FOR I=Z TO 15:S(I)=Z:NEXT
5020 FOR K=U TO 14
5030 READ I,J
5040 N=S(I):CC(I*5+N)=J:S(I)=N+U
5050 N=S(J):CC(J*5+N)=I:S(J)=N+U
5060 NEXT K
5070 RETURN
5100 REM --- GENERATE COLOR PALETTE
5110 M=Z:VTAB 21:PRINT "SELECTING FLAVORS..."
5120 M=M+U:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
5130 FOR N=Z TO 13:S(N)=N+U:NEXT
5140 FOR CI=CN-U TO Z STEP -1
5150 IF N<=CI THEN 5120
5160 I=RND(U)*N
5170 K=S(I):C(CI)=K
5180 N=N-U:S(I)=S(N)
5190 K=K*5
5200 FOR J=K TO K+4
5210 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
5220 FOR I=T TO N-U
5230 IF A=S(I) THEN N=N-U:S(I)=S(N):I=N
5240 NEXT I
5250 NEXT J
5260 NEXT CI
5270 RETURN
5300 REM --- CREATE A SHUFFLE 
5310 FOR I=Z TO SN-U:S(I)=I:NEXT
5320 FOR I=SN-U TO Z STEP -1:J=RND(U)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
5330 RETURN
5400 REM --- SHUFFLE COLOR PALETTE
5410 FOR I=CN-U TO Z STEP -1:J=RND(U)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
5420 RETURN
5500 REM --- INITIALIZE HOSE COORDINATES
5510 Y1=Y-2:Y0=Y1+U:T=U+X
5520 FOR I=Z TO (W-U)*2 STEP 2
5530 BX(I)=T:BX(I+U)=T
5540 BY(I)=Y0:BY(I+U)=Y1
5550 T=T+2:NEXT
5560 X0=X+W*2+U:X1=X0+U:T=U+Y
5570 FOR I=I TO (W+H-U)*2 STEP 2
5580 BX(I)=X0:BX(I+U)=X1
5590 BY(I)=T:BY(I+U)=T
5600 T=T+2:NEXT
5610 RETURN
5620 DATA 0,90,80,70,60,50,40,30,20,10
5630 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
5640 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
5650 DATA 29
5660 DATA 3,3,6,0 , 4,4,8,0 , 5,4,9,0 , 3,3,6,50
5670 DATA 5,3,8,0 , 7,3,9,0 , 8,4,7,0 , 5,4,8,60
5680 DATA 3,6,6,0 , 4,7,5,0 , 5,9,5,0 , 4,6,7,60
5690 DATA 3,3,3,0 , 5,5,5,0 , 7,7,7,0 , 4,4,4,70
5700 DATA 6,3,3,0 , 8,4,4,0 , 4,8,3,0 , 3,3,3,40
5710 DATA 5,2,2,0 , 6,4,2,0 , 3,4,2,50
5720 DATA 8,9,9,0 , 8,9,5,0 , 7,8,2,0 , 3,3,6,20
5730 DATA 8,9,2,0 , 6,7,3,90
