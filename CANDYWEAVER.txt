0 REM COPYRIGHT 2022 BY SEAN GUGLER
10 REM LICENSED UNDER CC BY-NC-SA 4.0
20 T1$="CANDY WEAVER"
30 T2$="V0.0.3 2022-08-10"
40 T3$="BY SEAN GUGLER"
50 T4$="-----------------"
60 GOSUB 600
70 GOSUB 300
80 DATA 8 , 3,3,3 , 4,4,3 , 5,5,3 , 3,3,6 , 4,4,6 , 6,6,4 , 8,8,2 , 9,9,9
90 READ N
100 FOR R=1 TO N:GOSUB 200:GOSUB 900:NEXT
110 END
200 READ W,H,CN%:X=10-W:Y=20-H
210 GR:GOSUB 1300
220 GOSUB 1400
230 X=X+20:GOSUB 700
240 CP%=1:CI%=0:GOSUB 800
250 RETURN
300 REM --- WELCOME
310 TEXT:HOME
320 T$=T2$:GOSUB 500
330 T$=T3$:GOSUB 500
340 PRINT
350 T$=T4$:GOSUB 500
360 T$=T1$:GOSUB 500
370 T$=T4$:GOSUB 500
380 PRINT
390 PRINT "WEAVE YOUR CANDY STRIPES ON THE RIGHT"
400 PRINT "TO MATCH THE PATTERN ON THE LEFT."
410 PRINT
420 PRINT "ROLL UP YOUR SLEEVES; HERE WE GO! ";
430 GET CH$
440 I=RND(-1*(PEEK(78)+256*PEEK(79))):REM SEED FROM HUMAN REACTION TIME
450 RETURN
500 REM --- PRINT CENTERED
510 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
600 REM --- INITIALIZE VARIABLES
610 DIM C(15)
620 DIM S(25)
630 DIM RO(24):REM GR MEM ADDR BY ROW
640 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
650 RETURN
700 REM --- DRAW GAME MENU
710 HOME:PRINT "CHOOSE A FLAVOR:";:HTAB 20
720 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
730 PRINT
740 PRINT "LAY A CANDY STRIPE:":HTAB X-H*2:PRINT "(";
750 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
760 PRINT CHR$(65+I);:NEXT:PRINT ")";
770 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
780 RETURN
800 REM --- ACTIVATE A COLOR
810 IF CI%=CP% THEN RETURN
820 VTAB 21:HTAB 21+CI%*2:INVERSE:PRINT CI%+1;:HTAB 21+CP%*2:NORMAL:PRINT CP%+1;:COLOR=C(CI%)
830 CP%=CI%:RETURN
900 REM --- MAIN GAME LOOP
910 WAIT 49152,128:GET CH$
920 CH = ASC(CH$)
930 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
940 CI%=CH-49:REM 1-9
950 IF CI%>=0 AND CI%<CN% THEN 990
960 BI=CH-65:REM A-Z
970 IF BI>=0 AND BI<W+H THEN 1010
980 GOTO 900:REM OTHER
990 REM - CHOOSE COLOR
1000 GOSUB 800:GOTO 900
1010 REM - CHOOSE LINE
1020 GOSUB 1500
1030 GOSUB 1100
1040 IF MA% THEN PRINT MA%;" MORE      ";:GOTO 900
1050 PRINT "CONFECTION COMPLETE! YUM!";
1060 RETURN
1100 REM --- CHECK FOR MATCH
1110 VTAB 24:HTAB 1:PRINT "COMPARING...";:HTAB 1
1120 MA%=0
1130 Y2=INT((Y+1)/2):FOR J=Y2 TO Y2+H-1
1140 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
1150 MA%=MA%+(PEEK(I)=PEEK(I-20))
1160 NEXT I:NEXT J
1170 MA%=(W*H-MA%)
1180 RETURN
1200 REM --- CREATE A SHUFFLE 
1210 FOR I=0 TO SN%-1:S(I)=I:NEXT
1220 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
1230 RETURN
1300 REM --- GENERATE COLOR PALETTE
1310 SN%=16:GOSUB 1200
1320 I=0:FOR CI=0 TO CN%-1
1330 T=S(I):I=I+1:IF T=0 OR T=3 OR T=5 OR T=10 THEN 1330
1340 C(CI)=T:NEXT:RETURN
1400 REM --- DRAW PATCH
1410 SN%=W+H:GOSUB 1200
1420 CI%=0
1430 FOR I=0 TO W+H-1
1440 COLOR=C(CI%):BI=S(I):GOSUB 1500
1450 CI%=CI%+1:IF CI%=CN% THEN CI%=0
1460 NEXT I
1470 RETURN
1500 REM --- DRAW A SINGLE BAR
1510 IF BI<H THEN HLIN X,X+W*2 AT 1+Y+BI*2:RETURN
1520 VLIN Y,Y+H*2 AT 1+X+(BI-H)*2:RETURN
