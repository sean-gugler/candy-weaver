0 GOSUB 3400
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="VERSION 1.0.2 2022-09-17"
50 T3$="BY SEAN GUGLER"
60 T4$="------------------"
70 GOTO 700
100 REM --- INPUT KEY
110 CH$="":CH=Z
120 FOR T=Z TO F
130 TI=TI+U:IF TI=TS THEN TI=Z:COLOR=9:PLOT TX,Z:TX=TX+U:IF TX=40 THEN RETURN
140 IF PEEK(KY)<K8 THEN NEXT
150 GET CH$:CH = ASC(CH$):RETURN
200 REM --- DRAW COLORED HOSE
210 GOSUB 300:BP=BI:COLOR=C(CI):T=BI+BI:PLOT BX(T),BY(T):T=T+U:PLOT BX(T),BY(T):RETURN
300 REM --- CLEAR HOSE
310 COLOR=15:T=BP+BP:PLOT BX(T),BY(T):COLOR=Z:T=T+U:PLOT BX(T),BY(T):RETURN
400 REM --- AIM HOSE
410 BI=BI-U:IF BI<Z THEN BI=W+H-U
420 GOTO 200
500 BI=BI+U:IF BI=W+H THEN BI=Z
510 GOTO 200
600 REM --- DRAW A SINGLE BAR
610 COLOR=C(CI)
620 IF BI<W THEN VLIN Y,Y+H*2 AT U+X+BI*2:RETURN
630 HLIN X,X+W*2 AT U+Y+(BI-W)*2:RETURN
700 REM --- MAIN ROUTINE
710 T=U:GOSUB 800
720 PRINT:PRINT "PREPARING THE KITCHEN...":GOSUB 3700
730 T=Z:GOSUB 800
740 GOSUB 1000
750 GOTO 730
800 REM --- WELCOME
810 TEXT:HOME
820 T$=T2$:GOSUB 4400
830 T$=T3$:GOSUB 4400
840 PRINT
850 T$=T4$:GOSUB 4400
860 IF T THEN FLASH
870 T$=T1$:GOSUB 4400:NORMAL
880 T$=T4$:GOSUB 4400
890 RETURN
1000 REM --- MAIN MENU
1010 PRINT
1020 PRINT " VIEW THE [D]EMO"
1030 PRINT " TAKE THE [T]UTORIAL"
1040 PRINT " PLAY THE [P]ROGRESSION CHALLENGE"
1050 PRINT " PRACTICE [S]PECIFIC WEAVES"
1060 PRINT
1070 PRINT " WHERE TO? ";
1080 GET CH$
1090 IF ASC(CH$)=27 THEN END:REM ESC
1100 IF CH$="D" THEN GOSUB 1200:RETURN
1110 IF CH$="T" THEN GOSUB 1500:RETURN
1120 IF CH$="S" THEN GOSUB 2200:RETURN
1130 IF CH$="P" THEN GOSUB 2600:RETURN
1140 GOTO 1080
1200 REM --- DEMO LOOP
1210 W=5:H=6:CN=7:TS=Z
1220 T=RND(SEED):REM PREDICTABLE SEQUENCE
1230 GOSUB 4700
1240 VTAB 21:PRINT "CHOOSE A FLAVOR,  "
1250 PRINT "AIM THE CANDY HOSE,";:HTAB 28:PRINT "---DEMO---"
1260 PRINT "LAY STRIPES, UNTIL THE";:HTAB 28:PRINT "PRESS A KEY"
1270 PRINT "ENTIRE PATTERN MATCHES";:HTAB 29:PRINT "TO RETURN";
1280 TI=30
1290 FOR J=Z TO MN-1
1300 IF J=13 THEN GOSUB 4000:GOSUB 4000:GOSUB 4000
1310 FOR I=U TO TI*3
1320 IF PEEK(KY)>=K8 THEN GET CH$:RETURN
1330 IF I<>TI*1 THEN 1350
1340 IF CI<>MC(J) THEN CI=MC(J):GOSUB 5200:GOSUB 200
1350 IF I<>TI*2 THEN 1370
1360 IF BI<>MB(J) THEN BI=MB(J):GOSUB 200
1370 IF I<>TI*3 THEN 1390
1380 GOSUB 600
1390 NEXT I:NEXT J
1400 COLOR=0:FOR I=Y TO Y+H*2:HLIN X,X+W*2 AT I:NEXT
1410 GOTO 1290
1500 REM --- TUTORIAL
1510 AP=AX:AX=Z
1520 W=3:H=3:CN=6:TS=Z:TX=Z
1530 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 3800
1540 GR:HOME
1550 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1560 PRINT "CANDY WEAVING!"
1570 GOSUB 4500
1580 T=RND(SEED)
1590 HOME:GOSUB 5000:GOSUB 5500:GOSUB 5300
1600 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1610 PRINT "A TASTY TARGET PATTERN."
1620 GOSUB 4500
1630 HOME:X=X+20:GOSUB 5000
1640 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1650 PRINT "STRIPES ONE AT A TIME UNTIL YOUR"
1660 PRINT "WHOLE PATTERN MATCHES THE TARGET."
1670 GOSUB 4500
1680 HOME
1690 PRINT "FIRST: SELECT"
1700 GOSUB 5100
1710 PRINT "A FLAVOR OF CANDY"
1720 PRINT "BY PRESSING ITS NUMBER";
1730 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1740 CI=CH-49:IF CI<Z OR CI>=CN THEN 1730
1750 HOME:GOSUB 5100:CP=NOT CI:GOSUB 5200
1760 PRINT "NEXT: PRESS THE RIGHT ARROW"
1770 PRINT "TO SEE HOW THE CANDY HOSE MOVES."
1780 BP=U:BI=Z:GOSUB 5900:GOSUB 200
1790 T$="KEEP GOING UNTIL IT TURNS THE CORNER..."
1800 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1810 IF CH<>21 THEN 1800
1820 IF BI=2 THEN T$="KEEP GOING UNTIL IT PASSES THE END..."
1830 VTAB 24:HTAB U:PRINT T$;
1840 GOSUB 500:IF BI<>Z THEN 1800
1850 HOME:GOSUB 5100:CP=NOT CI:GOSUB 5200
1860 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1870 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1880 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1890 IF CH=11 OR CH=8 THEN GOSUB 400
1900 IF CH=10 OR CH=21 THEN GOSUB 500
1910 IF CH<>32 THEN 1880:REM SPACE
1920 GOSUB 600
1930 HOME
1940 PRINT "CONTINUE LAYING"
1950 PRINT "CANDY STRIPES UNTIL"
1960 PRINT "THE PATTERNS MATCH EXACTLY"
1970 GOSUB 5100:CP=NOT CI:GOSUB 5200
2000 REM --- TUTORIAL PLAY LOOP
2010 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
2020 IF CH=11 OR CH=8 THEN GOSUB 400:GOTO 2000:REM LEFT
2030 IF CH=10 OR CH=21 THEN GOSUB 500:GOTO 2000:REM RIGHT
2040 IF CH=32 OR CH=13 THEN 2080:REM SPACE, RETURN
2050 T=CH-49:REM 1-9
2060 IF T<Z OR T>=CN THEN 2000
2070 CI=T:GOSUB 5200:GOSUB 200:GOTO 2000
2080 GOSUB 200:GOSUB 600
2090 GOSUB 3900:IF M THEN 2000
2100 GOSUB 300
2110 VTAB 24:INVERSE:PRINT "I SAY; WELL DONE!";:NORMAL:GOSUB 4500
2120 AX=AP
2130 RETURN
2200 REM --- PRACTICE MENU
2210 VTAB 13:HTAB U
2220 PRINT "  WIDTH (2-8): ";W
2230 PRINT " HEIGHT (2-9): ";H
2240 PRINT "FLAVORS (2-9): ";CN
2250 PRINT "  TIMER (0-9): ";TV
2260 PRINT
2270 HTAB 10:PRINT "[SPACE] TO BEGIN"
2280 HTAB 10:PRINT "[ESC]   TO CANCEL"
2290 S(0)=W:S(1)=H:S(2)=CN:S(3)=TV:TS=Z:TX=Z
2300 I=Z
2310 VTAB 13+I:HTAB 16:INVERSE:PRINT S(I);:NORMAL
2320 GOSUB 100
2330 IF CH=27 THEN RETURN:REM ESC
2340 IF CH=32 THEN 2420:REM SPACE
2350 IF CH=13 THEN CH=S(I)+48:REM RETURN KEY
2360 N=CH-48:REM "0"
2370 IF N<(2 * (I<>3)) OR N>(8 + (I<>Z)) THEN 2320
2380 S(I)=N
2390 HTAB 16:PRINT S(I)
2400 I=I+U:IF I>3 THEN I=Z
2410 GOTO 2310
2420 W=S(0):H=S(1):CN=S(2):TV=S(3):TS=TA(TV):GOSUB 4600
2500 REM --- PLAY A PRACTICE ROUND
2510 GOSUB 4700:GOSUB 4100
2520 GOSUB 2900:IF TX=-1 THEN RETURN
2530 VTAB 24:HTAB 24:PRINT "ANOTHER ROUND?";
2540 GET CH$
2550 IF CH$="Y" OR CH=13 THEN 2500
2560 IF CH$="N" OR CH=27 THEN RETURN
2570 GOTO 2540
2600 REM --- PLAY CHALLENGE WAVES
2610 GOSUB 4600
2620 FOR R=Z TO DN-U
2630 W=DW(R):H=DH(R):CN=DC(R):TS=DT(R)
2640 IF NOT TS THEN 2680
2650 HOME:FLASH:VTAB 22
2660 T$="*** TIMED ROUND! ***":GOSUB 4400
2670 NORMAL:GOSUB 4500
2680 GOSUB 4700:GOSUB 4100
2690 GOSUB 2900:IF TX=-1 THEN RETURN
2700 IF TX<>40 THEN 2740
2710 PRINT "BETTER LUCK NEXT TIME!"
2720 GOSUB 4500
2730 RETURN
2740 GOSUB 4500
2750 NEXT
2760 TEXT:HOME:INVERSE
2770 T$="CONGRATULATIONS!":GOSUB 4400
2780 NORMAL:VTAB 4:PRINT "YOU'VE WOVEN ALL THE CANDY"
2790 PRINT:PRINT "YOU ARE A MASTER CANDY WEAVER!"
2800 GOSUB 4500
2810 RETURN
2900 REM --- MAIN GAME LOOP
2910 FOR Q=Z TO F
2920 GOSUB 100
2930 IF TX=40 THEN 3370
2940 IF CH=27 THEN 3300:REM ESC
2950 IF CH=11 OR CH=8 THEN GOSUB 400:NEXT Q:REM LEFT
2960 IF CH=10 OR CH=21 THEN GOSUB 500:NEXT Q:REM RIGHT
2970 IF CH=32 OR CH=13 THEN 3220:REM SPACE, RETURN
2980 IF CH=84 THEN AX=NOT AX:GOSUB 4100:NEXT Q:REM TOGGLE EXPERT
2990 IF CH=82 THEN GOSUB 4000:NEXT Q:REM REVEAL DIFFS
3000 T=CH-49:REM 1-9
3010 IF T>=Z AND T<CN THEN 3100
3020 T=CH-65:REM A-Z
3030 IF T>=Z AND T<W+H THEN 3200
3040 NEXT Q:REM OTHER
3100 REM - CHOOSE COLOR
3110 CI=T:GOSUB 5200:GOSUB 200:NEXT Q
3200 REM - CHOOSE LINE
3210 BI=T:GOSUB 200
3220 GOSUB 600
3230 VTAB 24:HTAB U:CALL(E):PRINT "COMPARING...      ";:HTAB U:GOSUB 3900
3240 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:NEXT Q
3250 GOSUB 300
3260 T=RND(U)*3:INVERSE
3270 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 3380
3280 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 3380
3290 PRINT "COMPLETED! (YUM)";:GOTO 3380
3300 GOSUB 4300
3310 GOSUB 100
3320 IF TX=40 THEN 3370
3330 IF CH=78 OR CH=27 THEN GOSUB 4100:NEXT Q:REM [N]O
3340 IF CH=89 THEN TX=-1:HOME:GOTO 3380:REM [Y]ES
3350 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
3360 GOTO 3310
3370 HOME:FLASH:PRINT "OUT OF TIME!"
3380 NORMAL:RETURN
3400 REM --- DECLARE VARIABLES
3410 REM - CONSTANTS
3420 Z=0:U=1
3430 KY=49152:REM PEEK KEYBOARD
3440 K8=128
3450 E=-868:REM CALL ERASE_LINE
3460 F=1E30:REM INFINITY
3470 REM - PERFORMANCE-CRITICAL VARIABLES
3480 M=Z:I=Z:O=-20:J=Z:A=Z:W2=Z:X=Z:Y=Z:J0=Z:J1=Z:WH=9:W=3:H=3
3490 CN=6
3500 TV=Z
3510 AX=Z
3520 SEED=-222
3530 DIM RO(24):REM GR MEM ADDR BY ROW
3540 DIM BX(33),BY(33):REM HOSE COORDINATES
3550 DIM C(15):REM COLOR PALETTE
3560 DIM S(25):REM SHUFFLE SEQUENCE
3570 DIM CC(15*5-1):REM COLOR CLASH TABLE
3580 DIM TA(9):REM TIMER PRESETS
3590 DIM DW(30),DH(30),DC(30),DT(30):REM LEVEL DATA
3600 DIM MC(30),MB(30):REM DEMO DATA
3610 RETURN
3700 REM --- INITIALIZE GAME DATA
3710 FOR I=Z TO 7:FOR J=Z TO 2:RO(I+8*J)=1024+128*I+40*J:NEXT J:NEXT I
3720 FOR I=Z TO 9:READ TA(I):NEXT I
3730 GOSUB 5400
3740 READ MN:FOR I=Z TO MN-U:READ MC(I),MB(I):NEXT
3750 READ DN:FOR I=Z TO DN-U:READ DW(I),DH(I),DC(I),DT(I):NEXT
3760 RETURN
3800 REM --- INIT CHECK VARIABLES
3810 J0=(Y+U)/2:J1=J0+H-U:W2=W*2
3820 RETURN
3900 REM --- CHECK FOR MATCH
3910 M=Z:FOR J=J0 TO J1
3920 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
3930 M=M+(PEEK(I)=PEEK(I+O))
3940 NEXT:NEXT J
3950 M=WH-M
3960 RETURN
4000 REM --- REVEAL DIFFERENCES
4010 FOR J=J0 TO J1
4020 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
4030 P=PEEK(I):IF P=PEEK(I+O) THEN 4060
4040 POKE(I),255:FOR T=U TO 200:NEXT:POKE(I),P
4050 IF PEEK(KY)>=K8 THEN RETURN
4060 NEXT I:NEXT J:RETURN
4100 REM --- HELP TEXT
4110 VTAB 22:HTAB U
4120 IF AX THEN 4200
4130 CALL(E):PRINT " AIM: LEFT/RIGHT";
4140 HTAB 24:PRINT "[T]OGGLE LABELS"
4150 CALL(E):PRINT " LAY CANDY: SPACE";
4160 RETURN
4200 CALL(E):PRINT " PRESS A LETTER";
4210 HTAB X-3:PRINT "COL:";
4220 FOR I=Z TO W-U:PRINT " ";CHR$(65+I);:NEXT
4230 PRINT:CALL(E):PRINT " TO FAST-STRIPE";
4240 HTAB 27-H:PRINT "ROW:";
4250 FOR I=W TO W+H-U:PRINT " ";CHR$(65+I);:NEXT
4260 RETURN
4300 REM --- ABORT PROMPT
4310 VTAB 22:HTAB U:CALL(E)
4320 HTAB 24:PRINT "ABANDON GAME?"
4330 CALL(E)
4340 RETURN
4400 REM --- PRINT CENTERED
4410 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
4500 REM --- PROMPT TO CONTINUE
4510 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
4600 REM --- SEED "RND" FROM HUMAN REACTION TIME
4610 I=RND(-1*(PEEK(78)+256*PEEK(79))):RETURN
4700 REM --- LAYOUT A NEW WEAVE
4710 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 3800
4720 TI=Z:TX=Z
4730 GR:HOME:GOSUB 5500:GOSUB 5000:GOSUB 5300
4740 X=X+20:GOSUB 5000
4750 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 5100
4760 CP=U:CI=Z:GOSUB 5200
4770 GOSUB 5900:BP=U:BI=Z:GOSUB 200
4780 IF TS THEN COLOR=4:HLIN Z,39 AT Z
4790 RETURN
5000 REM --- DRAW PLAY AREA BORDER
5010 COLOR=15
5020 HLIN X-U,X+W*2+U AT Y-U
5030 HLIN X-U,X+W*2+U AT Y+H*2+U
5040 VLIN Y-U,Y+H*2+U AT X-U
5050 VLIN Y-U,Y+H*2+U AT X+W*2+U
5060 RETURN
5100 REM --- DRAW COLOR PALETTE
5110 VTAB 21:HTAB 20
5120 FOR I=Z TO CN-U:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+U;:NEXT
5130 PRINT:RETURN
5200 REM --- SHOW SELECTED COLOR
5210 IF CI=CP THEN RETURN
5220 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+U;:HTAB 21+CP*2:NORMAL:PRINT CP+U;
5230 CP=CI:PRINT:RETURN
5300 REM --- DRAW PATCH
5310 SN=W+H:GOSUB 5700
5320 CI=Z
5330 FOR I=Z TO W+H-U:BI=S(I):GOSUB 600
5340 CI=CI+U:IF CI=CN THEN CI=Z
5350 NEXT I
5360 GOSUB 5800
5370 RETURN
5400 REM --- TABLE OF CLASHING COLORS
5410 FOR I=Z TO 15:S(I)=Z:NEXT
5420 FOR K=U TO 14
5430 READ I,J
5440 N=S(I):CC(I*5+N)=J:S(I)=N+U
5450 N=S(J):CC(J*5+N)=I:S(J)=N+U
5460 NEXT K
5470 RETURN
5500 REM --- GENERATE COLOR PALETTE
5510 M=Z:VTAB 21:PRINT "SELECTING FLAVORS..."
5520 M=M+U:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
5530 FOR N=Z TO 13:S(N)=N+U:NEXT
5540 FOR CI=CN-U TO Z STEP -1
5550 IF N<=CI THEN 5520
5560 I=RND(U)*N
5570 K=S(I):C(CI)=K
5580 N=N-U:S(I)=S(N)
5590 K=K*5
5600 FOR J=K TO K+4
5610 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
5620 FOR I=T TO N-U
5630 IF A=S(I) THEN N=N-U:S(I)=S(N):I=N
5640 NEXT I
5650 NEXT J
5660 NEXT CI
5670 RETURN
5700 REM --- CREATE A SHUFFLE 
5710 FOR I=Z TO SN-U:S(I)=I:NEXT
5720 FOR I=SN-U TO Z STEP -1:J=RND(U)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
5730 RETURN
5800 REM --- SHUFFLE COLOR PALETTE
5810 FOR I=CN-U TO Z STEP -1:J=RND(U)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
5820 RETURN
5900 REM --- INITIALIZE HOSE COORDINATES
5910 Y1=Y-2:Y0=Y1+U:T=U+X
5920 FOR I=Z TO (W-U)*2 STEP 2
5930 BX(I)=T:BX(I+U)=T
5940 BY(I)=Y0:BY(I+U)=Y1
5950 T=T+2:NEXT
5960 X0=X+W*2+U:X1=X0+U:T=U+Y
5970 FOR I=I TO (W+H-U)*2 STEP 2
5980 BX(I)=X0:BX(I+U)=X1
5990 BY(I)=T:BY(I+U)=T
6000 T=T+2:NEXT
6010 RETURN
6100 REM - TIMER PRESETS
6110 DATA 0,90,80,70,60,50,40,30,20,10
6120 REM - CLASHING COLOR PAIRS
6130 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
6140 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
6150 REM - DEMO SEQUENCE
6160 DATA 22
6170 DATA 4,6 , 2,10 , 3,3 , 3,0, 0,0
6180 DATA 6,9 , 4,1 , 1,5 , 6,7 , 2,7
6190 DATA 3,8 , 0,4 , 5,2
6200 DATA 1,5 , 6,9 , 4,1 , 2,7 , 3,8
6210 DATA 0,4 , 0,4 , 0,4 , 0,4
6220 REM - CAMPAIGN SEQUENCE
6230 DATA 29
6240 DATA 3,3,6,0 , 4,4,8,0 , 5,4,9,0 , 3,3,6,50
6250 DATA 5,3,8,0 , 7,3,9,0 , 8,4,7,0 , 5,4,8,60
6260 DATA 3,6,6,0 , 4,7,5,0 , 5,9,5,0 , 4,6,7,60
6270 DATA 3,3,3,0 , 5,5,5,0 , 7,7,7,0 , 4,4,4,70
6280 DATA 6,3,3,0 , 8,4,4,0 , 4,8,3,0 , 3,3,3,40
6290 DATA 5,2,2,0 , 6,4,2,0 , 3,4,2,50
6300 DATA 8,9,9,0 , 8,9,5,0 , 7,8,2,0 , 3,3,6,20
6310 DATA 8,9,2,0 , 6,7,3,90
