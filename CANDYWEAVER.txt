0 GOSUB 3800
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="VERSION 1.2.0 2022-11-02"
50 T3$="BY SEAN GUGLER"
60 T4$="------------------"
70 GOTO 800
100 REM --- INPUT KEY
110 CH$="":CH=Z
120 FOR T=Z TO F
130 GOSUB 200:IF TX=40 THEN RETURN
140 IF PEEK(KY)<K8 THEN NEXT
150 GET CH$:CH = ASC(CH$):RETURN
200 REM --- TICK TIMER
210 TI=TI+U:IF TS AND TI>=TS THEN TI=TI-TS:COLOR=9:PLOT TX,Z:TX=TX+U
220 RETURN
300 REM --- DRAW COLORED HOSE
310 GOSUB 400:BP=BI:COLOR=C(CI):T=BI+BI:PLOT BX(T),BY(T):T=T+U:PLOT BX(T),BY(T):RETURN
400 REM --- CLEAR HOSE
410 COLOR=15:T=BP+BP:PLOT BX(T),BY(T):COLOR=Z:T=T+U:PLOT BX(T),BY(T):RETURN
500 REM --- AIM HOSE
510 BI=BI-U:IF BI<Z THEN BI=W+H-U
520 GOTO 300
600 BI=BI+U:IF BI=W+H THEN BI=Z
610 GOTO 300
700 REM --- DRAW A SINGLE BAR
710 COLOR=C(CI)
720 IF BI<W THEN VLIN Y,Y+H*2 AT U+X+BI*2:RETURN
730 HLIN X,X+W*2 AT U+Y+(BI-W)*2:RETURN
800 REM --- MAIN ROUTINE
810 T=U:GOSUB 900
820 PRINT:PRINT "PREPARING THE KITCHEN...":GOSUB 4100
830 T=Z:GOSUB 900
840 GOSUB 1100
850 GOTO 830
900 REM --- WELCOME
910 TEXT:HOME
920 T$=T2$:GOSUB 4800
930 T$=T3$:GOSUB 4800
940 PRINT
950 T$=T4$:GOSUB 4800
960 IF T THEN FLASH
970 T$=T1$:GOSUB 4800:NORMAL
980 T$=T4$:GOSUB 4800
990 RETURN
1100 REM --- MAIN MENU
1110 PRINT
1120 PRINT " VIEW THE [D]EMO"
1130 PRINT " TAKE THE [T]UTORIAL"
1140 PRINT " PLAY THE [P]ROGRESSION CHALLENGE"
1150 PRINT " PRACTICE [S]PECIFIC WEAVES"
1160 PRINT " MARATHON [R]ANDOM WEAVES"
1170 PRINT
1180 PRINT " WHERE TO? ";
1190 GET CH$
1200 IF ASC(CH$)=27 THEN END:REM ESC
1210 IF CH$="D" THEN GOSUB 1300:RETURN
1220 IF CH$="T" THEN GOSUB 1600:RETURN
1230 IF CH$="P" THEN GOSUB 2900:RETURN
1240 IF CH$="S" THEN GOSUB 2300:RETURN
1250 IF CH$="R" THEN GOSUB 2700:RETURN
1260 GOTO 1190
1300 REM --- DEMO LOOP
1310 W=5:H=6:CN=7:TS=Z
1320 T=RND(SEED):REM PREDICTABLE SEQUENCE
1330 GOSUB 5100
1340 VTAB 21:PRINT "CHOOSE A FLAVOR,  "
1350 PRINT "AIM THE CANDY HOSE,";:HTAB 28:PRINT "---DEMO---"
1360 PRINT "LAY STRIPES, UNTIL THE";:HTAB 28:PRINT "PRESS A KEY"
1370 PRINT "ENTIRE PATTERN MATCHES";:HTAB 29:PRINT "TO RETURN";
1380 TI=30
1390 FOR J=Z TO MN-1
1400 IF J=13 THEN GOSUB 4400:GOSUB 4400:GOSUB 4400
1410 FOR I=U TO TI*3
1420 IF PEEK(KY)>=K8 THEN GET CH$:RETURN
1430 IF I<>TI*1 THEN 1450
1440 IF CI<>MC(J) THEN CI=MC(J):GOSUB 5600:GOSUB 300
1450 IF I<>TI*2 THEN 1470
1460 IF BI<>MB(J) THEN BI=MB(J):GOSUB 300
1470 IF I<>TI*3 THEN 1490
1480 GOSUB 700
1490 NEXT I:NEXT J
1500 COLOR=0:FOR I=Y TO Y+H*2:HLIN X,X+W*2 AT I:NEXT
1510 GOTO 1390
1600 REM --- TUTORIAL
1610 AP=AX:AX=Z
1620 W=3:H=3:CN=6:TS=Z:TX=Z
1630 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 4200
1640 GR:HOME
1650 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1660 PRINT "CANDY WEAVING!"
1670 GOSUB 4900
1680 T=RND(SEED)
1690 HOME:GOSUB 5400:GOSUB 5900:GOSUB 5700
1700 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1710 PRINT "A TASTY TARGET PATTERN."
1720 GOSUB 4900
1730 HOME:X=X+20:GOSUB 5400
1740 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1750 PRINT "STRIPES ONE AT A TIME UNTIL YOUR"
1760 PRINT "WHOLE PATTERN MATCHES THE TARGET."
1770 GOSUB 4900
1780 HOME
1790 PRINT "FIRST: SELECT"
1800 GOSUB 5500
1810 PRINT "A FLAVOR OF CANDY"
1820 PRINT "BY PRESSING ITS NUMBER";
1830 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1840 CI=CH-49:IF CI<Z OR CI>=CN THEN 1830
1850 HOME:GOSUB 5500:CP=NOT CI:GOSUB 5600
1860 PRINT "NEXT: PRESS THE RIGHT ARROW"
1870 PRINT "TO SEE HOW THE CANDY HOSE MOVES."
1880 BP=U:BI=Z:GOSUB 6300:GOSUB 300
1890 T$="KEEP GOING UNTIL IT TURNS THE CORNER..."
1900 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1910 IF CH<>21 THEN 1900
1920 IF BI=2 THEN T$="KEEP GOING UNTIL IT PASSES THE END..."
1930 VTAB 24:HTAB U:PRINT T$;
1940 GOSUB 600:IF BI<>Z THEN 1900
1950 HOME:GOSUB 5500:CP=NOT CI:GOSUB 5600
1960 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1970 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1980 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
1990 IF CH=11 OR CH=8 THEN GOSUB 500
2000 IF CH=10 OR CH=21 THEN GOSUB 600
2010 IF CH<>32 THEN 1980:REM SPACE
2020 GOSUB 700
2030 HOME
2040 PRINT "CONTINUE LAYING"
2050 PRINT "CANDY STRIPES UNTIL"
2060 PRINT "THE PATTERNS MATCH EXACTLY"
2070 GOSUB 5500:CP=NOT CI:GOSUB 5600
2100 REM --- TUTORIAL PLAY LOOP
2110 GOSUB 100:IF CH=27 THEN RETURN:REM ESC
2120 IF CH=11 OR CH=8 THEN GOSUB 500:GOTO 2100:REM LEFT
2130 IF CH=10 OR CH=21 THEN GOSUB 600:GOTO 2100:REM RIGHT
2140 IF CH=32 OR CH=13 THEN 2180:REM SPACE, RETURN
2150 T=CH-49:REM 1-9
2160 IF T<Z OR T>=CN THEN 2100
2170 CI=T:GOSUB 5600:GOSUB 300:GOTO 2100
2180 GOSUB 300:GOSUB 700
2190 GOSUB 4300:IF M THEN 2100
2200 GOSUB 400
2210 VTAB 24:INVERSE:PRINT "I SAY; WELL DONE!";:NORMAL:GOSUB 4900
2220 AX=AP
2230 RETURN
2300 REM --- PRACTICE MENU
2310 VTAB 14:HTAB U
2320 PRINT "  WIDTH (2-8): ";W
2330 PRINT " HEIGHT (2-9): ";H
2340 PRINT "FLAVORS (2-9): ";CN
2350 PRINT "  TIMER (0-9): ";TV
2360 PRINT
2370 HTAB 10:PRINT "[SPACE] TO BEGIN"
2380 HTAB 10:PRINT "[ESC]   TO CANCEL"
2390 S(0)=W:S(1)=H:S(2)=CN:S(3)=TV:TS=Z:TX=Z
2400 I=Z
2410 VTAB 14+I:HTAB 16:INVERSE:PRINT S(I);:NORMAL
2420 GOSUB 100
2430 IF CH=27 THEN RETURN:REM ESC
2440 IF CH=32 THEN 2520:REM SPACE
2450 IF CH=13 THEN CH=S(I)+48:REM RETURN KEY
2460 N=CH-48:REM "0"
2470 IF N<(2 * (I<>3)) OR N>(WM + (I<>Z)) THEN 2420
2480 S(I)=N
2490 HTAB 16:PRINT S(I)
2500 I=I+U:IF I>3 THEN I=Z
2510 GOTO 2410
2520 W=S(0):H=S(1):CN=S(2):TV=S(3):TS=TA(TV):GOSUB 5000
2600 GOSUB 2800
2610 IF AG THEN 2600
2620 RETURN
2700 REM --- ENDLESS RANDOMIZED ROUNDS
2710 W=INT(RND(1)*7+2)
2720 H=INT(RND(1)*8+2)
2730 CN=INT(RND(1)*8+2)
2740 TS=0
2750 GOSUB 2800
2760 IF AG THEN 2700
2770 RETURN
2800 REM --- PLAY ONE ROUND
2810 GOSUB 5100:GOSUB 4500
2820 GOSUB 3200
2830 VTAB 24:HTAB 24:PRINT "ANOTHER ROUND?";
2840 GET CH$
2850 IF CH$="Y" OR CH=13 THEN AG=U:RETURN
2860 IF CH$="N" OR CH=27 THEN AG=Z:RETURN
2870 GOTO 2840
2900 REM --- PLAY CHALLENGE WAVES
2910 GOSUB 5000
2920 FOR R=Z TO DN-U
2930 W=DW(R):H=DH(R):CN=DC(R):TS=DT(R)
2940 HOME:PRINT DS$(R);:GOSUB 4900
2950 IF NOT TS THEN 2990
2960 HOME:FLASH:VTAB 22
2970 T$="*** TIMED ROUND! ***":GOSUB 4800
2980 NORMAL:GOSUB 4900
2990 GOSUB 5100:GOSUB 4500
3000 GOSUB 3200:IF TX=-1 THEN RETURN
3010 IF TX<>40 THEN 3070
3020 PRINT "KEEP PRACTICING, YOU CAN DO IT!"
3030 PRINT:HTAB 27:PRINT "PRESS ESC";:TS=0
3040 GOSUB 4400:IF PEEK(KY)>=K8 THEN GOSUB 100
3050 IF CH<>27 THEN 3040
3060 RETURN
3070 GOSUB 4900
3080 NEXT
3090 GR:HOME
3100 INVERSE:T$="CONGRATULATIONS!":GOSUB 4800:NORMAL
3110 PRINT "THE CELEBRATION LASTS FOR DAYS, AND"
3120 PRINT "SHOWERS OUR CHAMPION WITH CANDY PRAISE"
3130 PRINT "(PRESS SPACE TO REST ON YOUR LAURELS)";
3140 IF PEEK(KY)>=K8 THEN GET CH$:RETURN
3150 COLOR=RND(1)*14+1:T=INT(RND(1)*19)*2+1
3160 IF RND(1)<.5 THEN 3180
3170 HLIN Z,38 AT T:GOTO 3140
3180 VLIN Z,38 AT T:GOTO 3140
3200 REM --- MAIN GAME LOOP
3210 FOR Q=Z TO F
3220 GOSUB 100
3230 TI=TI+7
3240 IF TX=40 THEN 3680
3250 IF CH=27 THEN 3610:REM ESC
3260 IF CH=11 OR CH=8 THEN GOSUB 500:TI=TI+2:NEXT Q:REM LEFT
3270 IF CH=10 OR CH=21 THEN GOSUB 600:TI=TI+2:NEXT Q:REM RIGHT
3280 IF CH=32 OR CH=13 THEN 3520:REM SPACE, RETURN
3290 IF CH=84 THEN TI=TI+4:AX=NOT AX:GOSUB 4500:NEXT Q:REM TOGGLE LABELS
3300 IF CH=82 THEN GOSUB 4400:NEXT Q:REM REVEAL DIFFS
3310 T=CH-49:REM 1-9
3320 IF T>=Z AND T<CN THEN 3400
3330 T=CH-65:REM A-Q
3340 IF T>=Z AND T<W THEN 3500
3350 T=T-WM:IF T>=Z AND T<H THEN T=T+W:GOTO 3500
3360 NEXT Q:REM OTHER
3400 REM - CHOOSE COLOR
3410 TI=TI+4:CI=T:GOSUB 5600:GOSUB 300:NEXT Q
3500 REM - CHOOSE LINE
3510 BI=T:TI=TI+5:GOSUB 300
3520 TI=TI+3+(W*H*3/4)
3530 GOSUB 700
3540 VTAB 24:HTAB U:CALL(E):PRINT "COMPARING...      ";:HTAB U:GOSUB 4300
3550 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:NEXT Q
3560 GOSUB 400
3570 T=RND(U)*3:INVERSE
3580 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 3690
3590 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 3690
3600 PRINT "COMPLETED! (YUM)";:GOTO 3690
3610 GOSUB 4700
3620 GOSUB 100
3630 IF TX=40 THEN 3680
3640 IF CH=78 OR CH=27 THEN GOSUB 4500:TI=TI+3:NEXT Q:REM [N]O
3650 IF CH=89 THEN TX=-1:HOME:GOTO 3690:REM [Y]ES
3660 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
3670 TI=TI+3:GOTO 3620
3680 HOME:FLASH:PRINT "OUT OF TIME!"
3690 NORMAL:RETURN
3800 REM --- DECLARE VARIABLES
3810 REM - CONSTANTS
3820 Z=0:U=1
3830 KY=49152:REM PEEK KEYBOARD
3840 K8=128
3850 E=-868:REM CALL ERASE_LINE
3860 F=1E30:REM INFINITY
3870 REM - PERFORMANCE-CRITICAL VARIABLES
3880 M=Z:I=Z:O=-20:J=Z:A=Z:W2=Z:X=Z:Y=Z:J0=Z:J1=Z:WH=9:W=3:H=3
3890 CN=6
3900 TV=Z
3910 AX=Z
3920 WM=8
3930 SEED=-222
3940 DIM RO(24):REM GR MEM ADDR BY ROW
3950 DIM BX(33),BY(33):REM HOSE COORDINATES
3960 DIM C(15):REM COLOR PALETTE
3970 DIM S(25):REM SHUFFLE SEQUENCE
3980 DIM CC(15*5-1):REM COLOR CLASH TABLE
3990 DIM TA(9):REM TIMER PRESETS
4000 DIM DW(30),DH(30),DC(30),DT(30),DS$(30):REM LEVEL DATA
4010 DIM MC(30),MB(30):REM DEMO DATA
4020 RETURN
4100 REM --- INITIALIZE GAME DATA
4110 FOR I=Z TO 7:FOR J=Z TO 2:RO(I+8*J)=1024+128*I+40*J:NEXT J:NEXT I
4120 FOR I=Z TO 9:READ TA(I):NEXT I
4130 GOSUB 5800
4140 READ MN:FOR I=Z TO MN-U:READ MC(I),MB(I):NEXT
4150 READ DN:FOR I=Z TO DN-U:READ DW(I),DH(I),DC(I),DT(I),DS$(I):NEXT
4160 RETURN
4200 REM --- INIT CHECK VARIABLES
4210 J0=(Y+U)/2:J1=J0+H-U:W2=W*2
4220 RETURN
4300 REM --- CHECK FOR MATCH
4310 M=Z:FOR J=J0 TO J1
4320 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
4330 M=M+(PEEK(I)=PEEK(I+O))
4340 NEXT:NEXT J
4350 M=WH-M
4360 RETURN
4400 REM --- REVEAL DIFFERENCES
4410 FOR J=J0 TO J1
4420 A=RO(J)+X:FOR I=A+U TO A+W2 STEP 2
4430 TI=TI+1:GOSUB 200:IF TX=40 THEN RETURN
4440 P=PEEK(I):IF P=PEEK(I+O) THEN 4470
4450 POKE(I),255:FOR T=U TO 200:NEXT:POKE(I),P:TI=TI+9
4460 IF PEEK(KY)>=K8 THEN RETURN
4470 NEXT I:NEXT J:RETURN
4500 REM --- HELP TEXT
4510 VTAB 22:HTAB U
4520 IF AX THEN 4600
4530 CALL(E):PRINT " AIM: LEFT/RIGHT";
4540 HTAB 24:PRINT "[T]OGGLE LABELS"
4550 CALL(E):PRINT " LAY CANDY: SPACE";
4560 RETURN
4600 CALL(E):PRINT " PRESS A LETTER";
4610 HTAB X-3:PRINT "COL:";
4620 FOR I=Z TO W-U:PRINT " ";CHR$(65+I);:NEXT
4630 PRINT:CALL(E):PRINT " TO FAST-STRIPE";
4640 HTAB 27-H:PRINT "ROW:";
4650 FOR I=WM TO WM+H-U:PRINT " ";CHR$(65+I);:NEXT
4660 RETURN
4700 REM --- ABORT PROMPT
4710 VTAB 22:HTAB U:CALL(E)
4720 HTAB 24:PRINT "GIVE UP?"
4730 CALL(E)
4740 RETURN
4800 REM --- PRINT CENTERED
4810 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
4900 REM --- PROMPT TO CONTINUE
4910 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
5000 REM --- SEED "RND" FROM HUMAN REACTION TIME
5010 I=RND(-1*(PEEK(78)+256*PEEK(79))):RETURN
5100 REM --- LAYOUT A NEW WEAVE
5110 X=9-W:Y=20-H:WH=W*H:M=WH:GOSUB 4200
5120 TI=Z:TX=Z
5130 GR:HOME:GOSUB 5900:GOSUB 5400:GOSUB 5700
5140 X=X+20:GOSUB 5400
5150 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 5500
5160 CP=U:CI=Z:GOSUB 5600
5170 GOSUB 6300:BP=U:BI=Z:GOSUB 300
5180 IF TS THEN COLOR=4:HLIN Z,39 AT Z
5190 RETURN
5400 REM --- DRAW PLAY AREA BORDER
5410 COLOR=15
5420 HLIN X-U,X+W*2+U AT Y-U
5430 HLIN X-U,X+W*2+U AT Y+H*2+U
5440 VLIN Y-U,Y+H*2+U AT X-U
5450 VLIN Y-U,Y+H*2+U AT X+W*2+U
5460 RETURN
5500 REM --- DRAW COLOR PALETTE
5510 VTAB 21:HTAB 20
5520 FOR I=Z TO CN-U:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+U;:NEXT
5530 PRINT:RETURN
5600 REM --- SHOW SELECTED COLOR
5610 IF CI=CP THEN RETURN
5620 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+U;:HTAB 21+CP*2:NORMAL:PRINT CP+U;
5630 CP=CI:PRINT:RETURN
5700 REM --- DRAW PATCH
5710 SN=W+H:GOSUB 6100
5720 CI=Z
5730 FOR I=Z TO W+H-U:BI=S(I):GOSUB 700
5740 CI=CI+U:IF CI=CN THEN CI=Z
5750 NEXT I
5760 GOSUB 6200
5770 RETURN
5800 REM --- TABLE OF CLASHING COLORS
5810 FOR I=Z TO 15:S(I)=Z:NEXT
5820 FOR K=U TO 14
5830 READ I,J
5840 N=S(I):CC(I*5+N)=J:S(I)=N+U
5850 N=S(J):CC(J*5+N)=I:S(J)=N+U
5860 NEXT K
5870 RETURN
5900 REM --- GENERATE COLOR PALETTE
5910 M=Z:VTAB 21:PRINT "SELECTING FLAVORS..."
5920 M=M+U:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
5930 FOR N=Z TO 13:S(N)=N+U:NEXT
5940 FOR CI=CN-U TO Z STEP -1
5950 IF N<=CI THEN 5920
5960 I=RND(U)*N
5970 K=S(I):C(CI)=K
5980 N=N-U:S(I)=S(N)
5990 K=K*5
6000 FOR J=K TO K+4
6010 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
6020 FOR I=T TO N-U
6030 IF A=S(I) THEN N=N-U:S(I)=S(N):I=N
6040 NEXT I
6050 NEXT J
6060 NEXT CI
6070 RETURN
6100 REM --- CREATE A SHUFFLE 
6110 FOR I=Z TO SN-U:S(I)=I:NEXT
6120 FOR I=SN-U TO Z STEP -1:J=RND(U)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
6130 RETURN
6200 REM --- SHUFFLE COLOR PALETTE
6210 FOR I=CN-U TO Z STEP -1:J=RND(U)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
6220 RETURN
6300 REM --- INITIALIZE HOSE COORDINATES
6310 Y1=Y-2:Y0=Y1+U:T=U+X
6320 FOR I=Z TO (W-U)*2 STEP 2
6330 BX(I)=T:BX(I+U)=T
6340 BY(I)=Y0:BY(I+U)=Y1
6350 T=T+2:NEXT
6360 X0=X+W*2+U:X1=X0+U:T=U+Y
6370 FOR I=I TO (W+H-U)*2 STEP 2
6380 BX(I)=X0:BX(I+U)=X1
6390 BY(I)=T:BY(I+U)=T
6400 T=T+2:NEXT
6410 RETURN
6500 REM - TIMER PRESETS
6510 DATA 0,90,80,70,60,50,40,30,20,10
6520 REM - CLASHING COLOR PAIRS
6530 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
6540 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
6550 REM - DEMO SEQUENCE
6560 DATA 22
6570 DATA 4,6 , 2,10 , 3,3 , 3,0, 0,0
6580 DATA 6,9 , 4,1 , 1,5 , 6,7 , 2,7
6590 DATA 3,8 , 0,4 , 5,2
6600 DATA 1,5 , 6,9 , 4,1 , 2,7 , 3,8
6610 DATA 0,4 , 0,4 , 0,4 , 0,4
6620 REM - CAMPAIGN SEQUENCE
6630 DATA 25
6640 DATA 5,3,6,0,"MEET MR. WYDE-LOWE, THE LOCAL DANDY.    SIDEWAYS IS HOW HE LIKES HIS CANDY!"
6650 DATA 6,3,6,0,"A VERY GOOD START. BUT NOW HE WANTS MORESO MAKE HIM A WIDER ONE THAN BEFORE.
6660 DATA 7,3,6,0,"WIDER! WIDER! AND WIDER STILL!          SURELY THEN HE WILL HAVE HIS FILL."
6670 DATA 4,3,6,70,"FOR DEAR MR. WYDE-LOWE, A FINAL REQUEST:A QUICK LITTLE ONE TO GIVE TO HIS GUEST."
6680 DATA 3,4,6,0,"THE LADY HIGHTOWER IS RATHER IMPRESSED. CAN YOU MAKE A TALL ONE UNLIKE THE REST?"
6690 DATA 3,6,6,0,"NOT A BAD START, BUT SHE HAS IN MIND    A WEAVE OF THE TALLER AND TALLERER KIND!"
6700 DATA 3,8,6,0,"SPLENDID! THIS CANDY DELIGHTS HER SO.   SHE WONDERS ALOUD, HOW HIGH CAN YOU GO?"
6710 DATA 3,4,6,70,"THE LADY HAS SUDDENLY ONE FINAL THOUGHT:A GIFT FOR MY NEPHEW! I ALMOST FORGOT."
6720 DATA 5,5,5,0,"DISMAY! NEPHEW SQUAREJAW CANNOT ABIDE   LOPSIDED CANDY TOO LONG ON ONE SIDE."
6730 DATA 6,6,6,0,"SQUAREJAW'S GAZE REMAINS RATHER TOUGH.  THAT WEAVE WAS SIMPLY NOT BIG ENOUGH."
6740 DATA 7,7,7,0,"GREEDY DESIRES NOW SHOW IN HIS GRIN;    BIGGER AND BIGGER ONES LIFT UP HIS CHIN!"
6750 DATA 4,4,4,60,"CRACKING HIS KNUCKLES, SQUAREJAW DARES: HOW FAST CAN YOU ASSEMBLE THESE SQUARES?"
6760 DATA 8,4,9,0,"THE RAINBOW TWINS RUSH UP WITH DEMANDS. FERRARI: A LOW ONE MADE BY YOUR HANDS!"
6770 DATA 4,8,9,0,"THE TWINS CAN NEVER SEEM TO AGREE.      ROCKET: A TOWERING CANDY FOR ME!"
6780 DATA 4,4,8,40,"THEIR TEMPERS ERUPT OVER WHICH IS BEST. MAKE THEM A COMPROMISE! PUT IT TO REST!"
6790 DATA 8,6,2,0,"PACIFIED, THEY HAVE A RIDDLE FOR YOU.   DO YOU KNOW HOW TO MAKE ONE WITH TWO?"
6800 DATA 6,9,2,0,"BY NOW YOU PROBABLY KNOW WHAT IS NEXT.  FLIP IT ON END BEFORE WE GET VEXED!"
6810 DATA 8,9,2,0,"TO FINISH OFF THE RIDDLE WE'VE SPUN:    DO BOTH SHAPES TOGETHER; TWO INTO ONE."
6820 DATA 5,6,2,90,"LEADER MCGRIMLY NOW BEGS FOR YOUR SKILLSON A SERIOUS TASK. WORK FAST, NO FRILLS!"
6830 DATA 7,8,5,0,"OUR CANDY DOME IS UNDER ATTACK!         MAKE A BIG NET SO WE CAN FIGHT BACK."
6840 DATA 7,8,7,0,"THE SWEET-TOOTH, ALAS, EVADED OUR NET.  WE NEED TO WEAVE IN MORE FLAVORS, I BET."
6850 DATA 7,8,9,0,"USE ALL THE FLAVORS, SPARE NO EXPENSE!  OUR SITUATION IS GETTING INTENSE."
6860 DATA 5,6,9,60,"OH NO! IT ATE A BIG HOLE IN THE DOME!   WEAVE LIKE THE WIND AND RESCUE OUR HOME!"
6870 DATA 3,3,6,20,"IS THERE NO END TO THIS CANDY DISASTER? HURRY, SCURRY! WEAVE FASTER AND FASTER!"
6880 DATA 8,9,9,0,"THE CREATURE IS GONE - THE DAY IS WON!  A VICTORY BANNER WE NEED; JUST FOR FUN."
