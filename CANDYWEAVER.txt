0 REM COPYRIGHT 2022 BY SEAN GUGLER
10 REM LICENSED UNDER CC BY-NC-SA 4.0
20 T1$="CANDY WEAVER"
30 T2$="V0.1.0 2022-08-11"
40 T3$="BY SEAN GUGLER"
50 T4$="-----------------"
60 GOSUB 1300
70 GOSUB 300
80 DATA 11 , 3,3,3 , 4,4,3 , 5,5,3 , 6,8,3 , 8,3,5 , 3,8,5 , 3,3,6 , 4,4,6 , 6,6,4 , 8,9,2 , 8,9,9
90 READ N
100 FOR R=1 TO N:GOSUB 200:GOSUB 2100:NEXT
110 END
200 READ W,H,CN%:X=9-W:Y=20-H
210 GR:GOSUB 2600:GOSUB 1400:GOSUB 2700
220 X=X+20:GOSUB 1400
230 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 1500
240 CP%=1:CI%=0:GOSUB 1600
250 BP=1:BI=0:GOSUB 1700
260 PRINT " AIM: LEFT/RIGHT"
270 PRINT " LAY CANDY: SPACE"
280 RETURN
300 REM --- WELCOME
310 TEXT:HOME
320 T$=T2$:GOSUB 600
330 T$=T3$:GOSUB 600
340 PRINT
350 T$=T4$:GOSUB 600
360 T$=T1$:GOSUB 600
370 T$=T4$:GOSUB 600
380 PRINT
390 PRINT "FIRST TIME? PRESS [I]"
400 PRINT "FOR INSTRUCTIONS. "
410 PRINT
420 PRINT "OTHERWISE, PRESS [P]"
430 PRINT "TO PLAY!"
440 PRINT
450 GET CH$
460 IF CH$="I" THEN GOSUB 800:GOTO 300
470 IF CH$<>"P" THEN 450
480 REM SEED "RND" FROM HUMAN REACTION TIME
490 I=RND(-1*(PEEK(78)+256*PEEK(79)))
500 RETURN
600 REM --- PRINT CENTERED
610 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
700 REM --- PROMPT TO CONTINUE
710 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
800 REM --- TUTORIAL
810 W=3:H=3:CN%=3:X=9-W:Y=20-H
820 GR:HOME
830 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
840 PRINT "CANDY WEAVING!"
850 GOSUB 700
860 SEED=-222
870 T=RND(SEED):REM PREDICTABLE SEQUENCE
880 HOME:GOSUB 1400:GOSUB 2600:GOSUB 2700
890 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
900 PRINT "A TASTY TARGET PATTERN."
910 GOSUB 700
920 HOME:X=X+20:GOSUB 1400
930 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
940 PRINT "STRIPES ONE AT A TIME UNTIL THEY"
950 PRINT "MATCH THE PATTERN."
960 GOSUB 700
970 HOME
980 PRINT "FIRST, SELECT"
990 GOSUB 1500
1000 PRINT "A FLAVOR OF CANDY"
1010 PRINT "BY PRESSING ITS NUMBER";
1020 GOSUB 1800:CI%=CH-49:IF CI%<0 OR CI%>=CN% THEN 1020
1030 HOME:GOSUB 1500:CP%=NOT(CI%-1)+1:GOSUB 1600
1040 PRINT "NEXT, USE LEFT AND RIGHT ARROWS TO"
1050 PRINT "AIM THE CANDY HOSE. TRY IT OUT!"
1060 T=0:BI=2:GOSUB 1700
1070 GOSUB 1800
1080 IF CH=8 THEN T=T+1:GOSUB 1900:REM LEFT
1090 IF CH=21 THEN T=T+1:GOSUB 2000:REM RIGHT
1100 IF T<5 THEN 1070
1110 IF T=5 THEN PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE.";
1120 IF CH<>32 THEN 1070:REM SPACE
1130 GOSUB 2800
1140 HOME
1150 PRINT "CONTINUE LAYING"
1160 PRINT "CANDY STRIPES UNTIL"
1170 PRINT "THE PATTERN MATCHES!"
1180 VTAB 22:HTAB 30:PRINT"PRESS"
1190 VTAB 23:HTAB 30:PRINT"[P]LAY"
1200 VTAB 24:HTAB 30:PRINT"[M]ENU";
1210 IF PEEK(49152)>=128 THEN RETURN
1220 CI%=INT(RND(1)*CN%):GOSUB 1600
1230 BI=INT(RND(1)*(W+H)):GOSUB 1700:GOSUB 2800
1240 FOR T=1 TO 500:NEXT
1250 GOTO 1210
1300 REM --- INITIALIZE VARIABLES
1310 DIM C(15)
1320 DIM S(25)
1330 DIM RO(24):REM GR MEM ADDR BY ROW
1340 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
1350 RETURN
1400 REM --- DRAW PLAY AREA BORDER
1410 COLOR=15
1420 HLIN X-1,X+W*2+1 AT Y-1
1430 HLIN X-1,X+W*2+1 AT Y+H*2+1
1440 VLIN Y-1,Y+H*2+1 AT X-1
1450 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
1500 REM --- DRAW COLOR PALETTE
1510 VTAB 21:HTAB 20
1520 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
1530 PRINT:RETURN
1540 PRINT "LAY A CANDY STRIPE:":HTAB X-H*2:PRINT "(";
1550 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
1560 PRINT CHR$(65+I);:NEXT:PRINT ")";
1570 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
1580 RETURN
1600 REM --- SHOW SELECTED COLOR
1610 IF CI%=CP% THEN RETURN
1620 VTAB 21:HTAB 21+CI%*2:INVERSE:PRINT CI%+1;:HTAB 21+CP%*2:NORMAL:PRINT CP%+1;
1630 CP%=CI%:PRINT:RETURN
1700 REM --- SHOW SELECTED STRIPE
1710 COLOR=0:BA=BP:GOSUB 2900
1720 GOSUB 1400
1730 COLOR=C(CI%):BA=BI:GOSUB 2900
1740 BP=BI:RETURN
1800 REM --- INPUT KEY
1810 WAIT 49152,128:GET CH$:CH = ASC(CH$)
1820 RETURN
1900 REM --- AIM HOSE
1910 BI=BI-1:IF BI<0 THEN BI=W+H-1
1920 GOSUB 1700
1930 RETURN
2000 BI=BI+1:IF BI=W+H THEN BI=0
2010 GOSUB 1700
2020 RETURN
2100 REM --- MAIN GAME LOOP
2110 GOSUB 1800
2120 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
2130 IF CH=8 THEN GOSUB 1900:GOTO 2100:REM LEFT
2140 IF CH=21 THEN GOSUB 2000:GOTO 2100:REM RIGHT
2150 IF CH=32 THEN 2250:REM SPACE
2160 T=CH-49:REM 1-9
2170 IF T>=0 AND T<CN% THEN 2210
2180 T=CH-65:REM A-Z
2190 IF T>=0 AND T<W+H THEN 2230
2200 GOTO 2100:REM OTHER
2210 REM - CHOOSE COLOR
2220 CI%=T:GOSUB 1600:GOSUB 1700:GOTO 2100
2230 REM - CHOOSE LINE
2240 BI=T
2250 GOSUB 1700:GOSUB 2800:GOSUB 2400
2260 IF MA% THEN PRINT MA%;" MISMATCH  ";:GOTO 2100
2270 T=RND(1)*3:INVERSE
2280 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 2310
2290 IF T<2 THEN PRINT "DELICIOUS!";:GOTO 2310
2300 PRINT "COMPLETED! (YUM)";
2310 NORMAL:GOSUB 700:RETURN
2400 REM --- CHECK FOR MATCH
2410 VTAB 24:HTAB 1:PRINT "COMPARING...";:HTAB 1
2420 MA%=0
2430 Y2=INT((Y+1)/2):FOR J=Y2 TO Y2+H-1
2440 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
2450 MA%=MA%+(PEEK(I)=PEEK(I-20))
2460 NEXT I:NEXT J
2470 MA%=(W*H-MA%)
2480 RETURN
2500 REM --- CREATE A SHUFFLE 
2510 FOR I=0 TO SN%-1:S(I)=I:NEXT
2520 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
2530 RETURN
2600 REM --- GENERATE COLOR PALETTE
2610 SN%=15:GOSUB 2500
2620 I=0:FOR CI=0 TO CN%-1
2630 T=S(I):I=I+1:IF T=0 OR T=3 OR T=5 OR T=10 THEN 2630
2640 C(CI)=T:NEXT:RETURN
2700 REM --- DRAW PATCH
2710 SN%=W+H:GOSUB 2500
2720 CI%=0
2730 FOR I=0 TO W+H-1:BI=S(I):GOSUB 2800
2740 CI%=CI%+1:IF CI%=CN% THEN CI%=0
2750 NEXT I
2760 RETURN
2800 REM --- DRAW A SINGLE BAR
2810 COLOR=C(CI%)
2820 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
2830 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
2900 REM --- DRAW AXIS INDICATOR
2910 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
2920 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
