0 GOSUB 2800
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="VERSION 1.0.0 2022-08-31"
50 T3$="BY SEAN GUGLER"
60 T4$="------------------"
70 T=1:GOSUB 900:GOSUB 3000
80 HOME:T=0:GOSUB 900
90 GOSUB 1100
100 GOTO 80
200 REM --- PLAY CHALLENGE WAVES
210 GOSUB 3100
220 FOR R=0 TO DN-1
230 W=DW(R):H=DH(R):CN=DC(R):TS=DT(R)
240 IF NOT TS THEN 280
250 HOME:FLASH:VTAB 22
260 T$="*** TIMED ROUND! ***":GOSUB 1300
270 NORMAL:GOSUB 1400
280 GOSUB 400:GOSUB 700
290 GOSUB 4000:IF TX=-1 THEN RETURN
300 IF TX<>40 THEN 340
310 PRINT "BETTER LUCK NEXT TIME!"
320 GOSUB 1400
330 RETURN
340 GOSUB 1400
350 NEXT
360 GOTO 5500
400 REM --- LAYOUT A NEW WEAVE
410 X=9-W:Y=20-H:M=W*H:GOSUB 4400
420 TI=0:TX=0
430 GR:HOME:GOSUB 5000:GOSUB 3200:GOSUB 5200
440 X=X+20:GOSUB 3200
450 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 3300
460 CP=1:CI=0:GOSUB 3400
470 BP=1:BI=0:GOSUB 3600
480 IF TS THEN COLOR=4:HLIN 0,39 AT 0
490 RETURN
600 REM --- ABORT PROMPT
610 VTAB 22:HTAB 1:CALL(-868)
620 HTAB 24:PRINT "ABANDON GAME?"
630 CALL(-868)
640 RETURN
700 REM --- HELP TEXT
710 VTAB 22:HTAB 1
720 IF AX THEN 770
730 CALL(-868):PRINT " AIM: LEFT/RIGHT";
740 HTAB 24:PRINT "[T]OGGLE LABELS"
750 CALL(-868):PRINT " LAY CANDY: SPACE";
760 RETURN
770 CALL(-868):PRINT "PRESS A LETTER";
780 HTAB X-3:PRINT "COL:";
790 FOR I=0 TO W-1:PRINT " ";CHR$(65+I);:NEXT
800 PRINT:CALL(-868):PRINT "TO FAST-STRIPE";
810 HTAB 26-H:PRINT "ROW:";
820 FOR I=W TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
830 RETURN
900 REM --- WELCOME
910 TEXT:HOME
920 T$=T2$:GOSUB 1300
930 T$=T3$:GOSUB 1300
940 PRINT
950 T$=T4$:GOSUB 1300
960 IF T THEN FLASH
970 T$=T1$:GOSUB 1300:NORMAL
980 T$=T4$:GOSUB 1300
990 RETURN
1100 REM --- MAIN MENU
1110 PRINT
1120 PRINT " VIEW THE [D]EMO"
1130 PRINT " TAKE THE [T]UTORIAL"
1140 PRINT " PLAY THE [P]ROGRESSION CHALLENGE"
1150 PRINT " PRACTICE [S]PECIFIC WEAVES"
1160 PRINT
1170 PRINT " WHERE TO? ";
1180 GET CH$
1190 IF ASC(CH$)=27 THEN END:REM ESC
1200 IF CH$="D" THEN GOSUB 2200:RETURN
1210 IF CH$="T" THEN GOSUB 1500:RETURN
1220 IF CH$="S" THEN GOSUB 2400:RETURN
1230 IF CH$="P" THEN GOSUB 200:RETURN
1240 GOTO 1180
1300 REM --- PRINT CENTERED
1310 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
1400 REM --- PROMPT TO CONTINUE
1410 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
1500 REM --- TUTORIAL
1510 AP=AX:AX=0
1520 W=3:H=3:CN=6:TS=0:TX=0
1530 X=9-W:Y=20-H:GOSUB 4400
1540 GR:HOME
1550 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1560 PRINT "CANDY WEAVING!"
1570 GOSUB 1400
1580 T=RND(SEED)
1590 HOME:GOSUB 3200:GOSUB 5000:GOSUB 5200
1600 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1610 PRINT "A TASTY TARGET PATTERN."
1620 GOSUB 1400
1630 IF CH$="R" THEN SEED=SEED-1:GOTO 1580
1640 HOME:X=X+20:GOSUB 3200
1650 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1660 PRINT "STRIPES ONE AT A TIME UNTIL YOUR"
1670 PRINT "WHOLE PATTERN MATCHES THE TARGET."
1680 GOSUB 1400
1690 HOME
1700 PRINT "FIRST: SELECT"
1710 GOSUB 3300
1720 PRINT "A FLAVOR OF CANDY"
1730 PRINT "BY PRESSING ITS NUMBER";
1740 GOSUB 3700:CI=CH-49:IF CI<0 OR CI>=CN THEN 1740
1750 CP=NOT(CI-1)+1
1760 HOME:GOSUB 3300:GOSUB 3400
1770 PRINT "NEXT: PRESS THE RIGHT ARROW"
1780 PRINT "TO MOVE THE CANDY HOSE."
1790 BI=0:GOSUB 3600
1800 GOSUB 3700:IF CH<>21 THEN 1800
1810 VTAB 24:HTAB 1:PRINT "KEEP GOING! WATCH HOW IT WRAPS...";
1820 GOSUB 3900:IF BI<>0 THEN 1800
1830 HOME:GOSUB 3300:CP=NOT CI:GOSUB 3400
1840 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1850 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1860 GOSUB 3700
1870 IF CH=11 OR CH=8 THEN GOSUB 3800
1880 IF CH=10 OR CH=21 THEN GOSUB 3900
1890 IF CH<>32 THEN 1860:REM SPACE
1900 GOSUB 5300
1910 HOME
1920 PRINT "CONTINUE LAYING"
1930 PRINT "CANDY STRIPES UNTIL"
1940 PRINT "THE PATTERNS MATCH EXACTLY"
1950 GOSUB 3300:CP=NOT CI:GOSUB 3400
2000 REM --- TUTORIAL PLAY LOOP
2010 GOSUB 3700
2020 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
2030 IF CH=11 OR CH=8 THEN GOSUB 3800:GOTO 2000:REM LEFT
2040 IF CH=10 OR CH=21 THEN GOSUB 3900:GOTO 2000:REM RIGHT
2050 IF CH=32 OR CH=13 THEN 2090:REM SPACE, RETURN
2060 T=CH-49:REM 1-9
2070 IF T<0 OR T>=CN THEN 2000
2080 CI=T:GOSUB 3400:GOSUB 3600:GOTO 2000
2090 GOSUB 3600:GOSUB 5300
2100 GOSUB 4500:IF M THEN 2000
2110 GOSUB 3500
2120 VTAB 24:INVERSE:PRINT "I SAY; WELL DONE!";:NORMAL:GOSUB 1400
2130 AX=AP
2140 RETURN
2200 REM --- DEMO LOOP
2210 W=5:H=6:CN=7:TS=0
2220 T=RND(SEED):REM PREDICTABLE SEQUENCE
2230 GOSUB 400
2240 VTAB 21:PRINT "CHOOSE A FLAVOR,  "
2250 PRINT "AIM THE CANDY HOSE,";:HTAB 28:PRINT "---DEMO---"
2260 PRINT "LAY STRIPES, UNTIL THE";:HTAB 28:PRINT "PRESS A KEY"
2270 PRINT "ENTIRE PATTERN MATCHES";:HTAB 29:PRINT "TO RETURN";
2280 FOR I=1 TO 60
2290 IF PEEK(KY)>=128 THEN GET CH$:RETURN
2300 IF I<>20 THEN 2330
2310 T=INT(RND(1)*CN):IF T=CI THEN 2310
2320 CI=T:GOSUB 3400:GOSUB 3600
2330 IF I<>40 THEN 2360
2340 T=INT(RND(1)*(W+H)):IF T=BI THEN 2340
2350 BI=T:GOSUB 3600
2360 IF I<>60 THEN 2380
2370 GOSUB 5300
2380 NEXT I:GOTO 2280
2400 REM --- PRACTICE MENU
2410 VTAB 13:HTAB 1
2420 PRINT "  WIDTH (2-8): ";W
2430 PRINT " HEIGHT (2-9): ";H
2440 PRINT "FLAVORS (2-9): ";CN
2450 PRINT "  TIMER (0-9): ";TV
2460 PRINT
2470 HTAB 10:PRINT "[SPACE] TO BEGIN"
2480 HTAB 10:PRINT "[ESC]   TO CANCEL"
2490 S(0)=W:S(1)=H:S(2)=CN:S(3)=TV:TS=0:TX=0
2500 I=0
2510 VTAB 13+I:HTAB 16:INVERSE:PRINT S(I);:NORMAL
2520 GOSUB 3700
2530 IF CH=27 THEN RETURN:REM ESC
2540 IF CH=32 THEN 2620:REM SPACE
2550 IF CH=13 THEN CH=S(I)+48:REM RETURN KEY
2560 N=CH-48:REM "0"
2570 IF N<(2 * (I<>3)) OR N>(8 + (I<>0)) THEN 2520
2580 S(I)=N
2590 HTAB 16:PRINT S(I)
2600 I=I+1:IF I>3 THEN I=0
2610 GOTO 2510
2620 W=S(0):H=S(1):CN=S(2):TV=S(3):TS=TA(TV):GOSUB 3100
2700 REM --- PLAY A PRACTICE ROUND
2710 GOSUB 400:GOSUB 700
2720 GOSUB 4000:IF TX=-1 THEN RETURN
2730 VTAB 24:HTAB 24:PRINT "ANOTHER ROUND?";
2740 GET CH$
2750 IF CH$="Y" OR CH=13 THEN 2700
2760 IF CH$="N" OR CH=27 THEN RETURN
2770 GOTO 2740
2800 REM --- DECLARE VARIABLES
2810 M=0:I=0:O=-20:J=0:A=0:W=3:H=3:X=0:Y=0
2820 KY=49152
2830 CN=6
2840 TV=0
2850 AX=0
2860 SEED=-222
2870 DIM CC(15*5-1):REM COLOR CLASH TABLE
2880 DIM RO(24):REM GR MEM ADDR BY ROW
2890 DIM C(15):REM COLOR PALETTE
2900 DIM S(25):REM SHUFFLE SEQUENCE
2910 DIM TA(9):REM TIMER PRESETS
2920 REM LEVEL DATA
2930 DIM DW(30):DIM DH(30):DIM DC(30):DIM DT(30)
2940 RETURN
3000 REM --- INITIALIZE GAME DATA
3010 FOR I=0 TO 7:FOR J=0 TO 2:RO(I+8*J)=1024+128*I+40*J:NEXT J:NEXT I
3020 FOR I=0 TO 9:READ TA(I):NEXT I
3030 GOSUB 4900
3040 READ DN:FOR I=0 TO DN-1:READ DW(I),DH(I),DC(I),DT(I):NEXT
3050 RETURN
3100 REM --- SEED "RND" FROM HUMAN REACTION TIME
3110 I=RND(-1*(PEEK(78)+256*PEEK(79)))
3120 RETURN
3200 REM --- DRAW PLAY AREA BORDER
3210 COLOR=15
3220 HLIN X-1,X+W*2+1 AT Y-1
3230 HLIN X-1,X+W*2+1 AT Y+H*2+1
3240 VLIN Y-1,Y+H*2+1 AT X-1
3250 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
3300 REM --- DRAW COLOR PALETTE
3310 VTAB 21:HTAB 20
3320 FOR I=0 TO CN-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
3330 PRINT:RETURN
3400 REM --- SHOW SELECTED COLOR
3410 IF CI=CP THEN RETURN
3420 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+1;:HTAB 21+CP*2:NORMAL:PRINT CP+1;
3430 CP=CI:PRINT:RETURN
3500 REM --- CLEAR STRIPE INDICTOR
3510 COLOR=0:BA=BP:GOSUB 5400
3520 GOSUB 3200:RETURN
3600 REM --- SHOW SELECTED STRIPE
3610 GOSUB 3500
3620 COLOR=C(CI):BA=BI:GOSUB 5400
3630 BP=BI:RETURN
3700 REM --- INPUT KEY
3710 CH$="":CH=0
3720 TI=TI+1:IF TI=TS THEN :COLOR=9:PLOT TX,0:TX=TX+1:TI=0
3730 IF TX=40 THEN RETURN
3740 IF PEEK(KY)<128 THEN 3720
3750 GET CH$:CH = ASC(CH$)
3760 RETURN
3800 REM --- AIM HOSE
3810 BI=BI-1:IF BI<0 THEN BI=W+H-1
3820 GOSUB 3600
3830 RETURN
3900 BI=BI+1:IF BI=W+H THEN BI=0
3910 GOSUB 3600
3920 RETURN
4000 REM --- MAIN GAME LOOP
4010 GOSUB 3700
4020 IF TX=40 THEN 4330
4030 IF CH=27 THEN 4260:REM ESC
4040 IF CH=11 OR CH=8 THEN GOSUB 3800:GOTO 4000:REM LEFT
4050 IF CH=10 OR CH=21 THEN GOSUB 3900:GOTO 4000:REM RIGHT
4060 IF CH=32 OR CH=13 THEN 4180:REM SPACE, RETURN
4070 IF CH=84 THEN AX=NOT AX:GOSUB 700:GOTO 4000:REM TOGGLE EXPERT
4080 IF CH=82 THEN GOSUB 4600:GOTO 4000:REM REVEAL DIFFS
4090 T=CH-49:REM 1-9
4100 IF T>=0 AND T<CN THEN 4140
4110 T=CH-65:REM A-Z
4120 IF T>=0 AND T<W+H THEN 4160
4130 GOTO 4000:REM OTHER
4140 REM - CHOOSE COLOR
4150 CI=T:GOSUB 3400:GOSUB 3600:GOTO 4000
4160 REM - CHOOSE LINE
4170 BI=T
4180 GOSUB 3600:GOSUB 5300
4190 VTAB 24:HTAB 1:CALL(-868):PRINT "COMPARING...      ";:HTAB 1:GOSUB 4500
4200 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:GOTO 4000
4210 GOSUB 3500
4220 T=RND(1)*3:INVERSE
4230 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 4340
4240 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 4340
4250 PRINT "COMPLETED! (YUM)";:GOTO 4340
4260 GOSUB 600
4270 GOSUB 3700
4280 IF TX=40 THEN 4330
4290 IF CH=78 OR CH=27 THEN GOSUB 700:GOTO 4000:REM [N]O
4300 IF CH=89 THEN TX=-1:HOME:GOTO 4340:REM [Y]ES
4310 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
4320 GOTO 4270
4330 HOME:FLASH:PRINT "OUT OF TIME!"
4340 NORMAL:RETURN
4400 REM --- INIT CHECK VARIABLES
4410 J0=(Y+1)/2:J1=J0+H-1:W2=W*2
4500 REM --- CHECK FOR MATCH
4510 M=0:FOR J=J0 TO J1
4520 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
4530 M=M+(PEEK(I)=PEEK(I+O))
4540 NEXT I:NEXT J
4550 M=(W*H-M)
4560 RETURN
4600 REM --- REVEAL DIFFERENCES
4610 FOR J=J0 TO J1
4620 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
4630 P=PEEK(I):IF P=PEEK(I+O) THEN 4660
4640 POKE(I),255:FOR T=1 TO 200:NEXT:POKE(I),P
4650 IF PEEK(KY)>=128 THEN RETURN
4660 NEXT I:NEXT J:RETURN
4700 REM --- CREATE A SHUFFLE 
4710 FOR I=0 TO SN-1:S(I)=I:NEXT
4720 FOR I=SN-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
4730 RETURN
4800 REM --- SHUFFLE COLOR PALETTE
4810 FOR I=CN-1 TO 0 STEP -1:J=RND(1)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
4820 RETURN
4900 REM --- TABLE OF CLASHING COLORS
4910 FOR I=0 TO 15:S(I)=0:NEXT
4920 FOR K=1 TO 14
4930 READ I,J
4940 N=S(I):CC(I*5+N)=J:S(I)=N+1
4950 N=S(J):CC(J*5+N)=I:S(J)=N+1
4960 NEXT K
4970 RETURN
5000 REM --- GENERATE COLOR PALETTE
5010 M=0:VTAB 21:PRINT "SELECTING FLAVORS..."
5020 M=M+1:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
5030 FOR N=0 TO 13:S(N)=N+1:NEXT
5040 FOR CI=CN-1 TO 0 STEP -1
5050 IF N<=CI THEN 5020
5060 I=RND(1)*N
5070 K=S(I):C(CI)=K
5080 N=N-1:S(I)=S(N)
5090 K=K*5
5100 FOR J=K TO K+4
5110 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
5120 FOR I=T TO N-1
5130 IF A=S(I) THEN N=N-1:S(I)=S(N):I=N
5140 NEXT I
5150 NEXT J
5160 NEXT CI
5170 RETURN
5200 REM --- DRAW PATCH
5210 SN=W+H:GOSUB 4700
5220 CI=0
5230 FOR I=0 TO W+H-1:BI=S(I):GOSUB 5300
5240 CI=CI+1:IF CI=CN THEN CI=0
5250 NEXT I
5260 GOSUB 4800
5270 RETURN
5300 REM --- DRAW A SINGLE BAR
5310 COLOR=C(CI)
5320 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
5330 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
5400 REM --- DRAW AXIS INDICATOR
5410 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
5420 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
5500 REM --- GAME OVER
5510 TEXT:HOME:INVERSE
5520 T$="CONGRATULATIONS!":GOSUB 1300
5530 NORMAL:VTAB 4:PRINT "YOU'VE WOVEN ALL THE CANDY"
5540 PRINT:PRINT "YOU ARE A MASTER CANDY WEAVER!"
5550 PRINT
5560 END
5600 DIM CT(39)
5610 I=0
5620 FOR C1=1 TO 13
5630 FOR C2=C1+1 TO 14
5640 IF I=0 THEN GR:HOME
5650 PRINT C1;" ";C2;"  ";
5660 CT(I*5+1)=C1
5670 CT(I*5+2)=C2
5680 CT(I*5+3)=C1
5690 CT(I*5+4)=C2
5700 I=I+1
5710 IF I<8 THEN 5750
5720 FOR I=0 TO 39:COLOR=CT(I):VLIN 0,39 AT I:NEXT
5730 GOSUB 1400
5740 I=0
5750 NEXT C2
5760 NEXT C1
5770 FOR J=0 TO I*5:COLOR=CT(J):VLIN 0,39 AT J:NEXT
5780 END
5790 DATA 0,90,80,70,60,50,40,30,20,10
5800 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
5810 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
5820 DATA 29
5830 DATA 3,3,6,0 , 4,4,8,0 , 5,4,9,0 , 3,3,6,50
5840 DATA 5,3,8,0 , 7,3,9,0 , 8,4,7,0 , 5,4,8,60
5850 DATA 3,6,6,0 , 4,7,5,0 , 5,9,5,0 , 4,6,7,60
5860 DATA 3,3,3,0 , 5,5,5,0 , 7,7,7,0 , 4,4,4,70
5870 DATA 6,3,3,0 , 8,4,4,0 , 4,8,3,0 , 3,3,3,40
5880 DATA 5,2,2,0 , 6,4,2,0 , 3,4,2,50
5890 DATA 8,9,9,0 , 8,9,5,0 , 7,8,2,0 , 3,3,6,20
5900 DATA 8,9,2,0 , 6,7,3,90
