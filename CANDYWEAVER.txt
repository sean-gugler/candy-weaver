0 GOSUB 1800
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="V0.2.1 2022-08-12"
50 T3$="BY SEAN GUGLER"
60 T4$="-----------------"
70 GOSUB 700
80 DATA 29
90 DATA 3,3,6,0 , 4,4,8,0 , 5,4,9,0 , 3,3,6,50
100 DATA 5,3,8,0 , 7,3,9,0 , 8,4,7,0 , 5,4,8,60
110 DATA 3,6,6,0 , 4,7,5,0 , 5,9,5,0 , 4,6,7,60
120 DATA 3,3,3,0 , 5,5,5,0 , 7,7,7,0 , 4,4,4,70
130 DATA 6,3,3,0 , 8,4,4,0 , 4,8,3,0 , 3,3,3,40
140 DATA 5,2,2,0 , 6,4,2,0 , 3,4,2,50
150 DATA 8,9,9,0 , 8,9,5,0 , 7,8,2,0 , 3,3,6,20
160 DATA 8,9,2,0 , 6,7,3,90
170 READ N
180 FOR R=1 TO N:GOSUB 300:GOSUB 500:GOSUB 2700:NEXT
190 GOTO 3900
300 REM --- LAYOUT A NEW WEAVE
310 READ W,H,CN,TS:X=9-W:Y=20-H:M=W*H:GOSUB 3100
320 TI=0:TX=0
330 IF NOT TS THEN 370
340 HOME:FLASH:VTAB 22
350 T$="*** TIMED ROUND! ***":GOSUB 1000
360 NORMAL:GOSUB 1100
370 GR:GOSUB 3500:GOSUB 1900:GOSUB 3600
380 X=X+20:GOSUB 1900
390 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 2000
400 CP=1:CI=0:GOSUB 2100
410 BP=1:BI=0:GOSUB 2300
420 IF TS THEN COLOR=4:HLIN 0,39 AT 0
430 RETURN
500 REM --- HELP TEXT
510 VTAB 22:HTAB 1
520 IF AX THEN 570
530 CALL(-868):PRINT " AIM: LEFT/RIGHT";
540 HTAB 24:PRINT "[T]OGGLE EXPERT"
550 CALL(-868):PRINT " LAY CANDY: SPACE";
560 RETURN
570 CALL(-868):PRINT "PRESS A LETTER";
580 HTAB X-3:PRINT "COL:";
590 FOR I=0 TO W-1:PRINT " ";CHR$(65+I);:NEXT
600 PRINT:CALL(-868):PRINT "TO FAST-STRIPE";
610 HTAB 26-H:PRINT "ROW:";
620 FOR I=W TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
630 RETURN
700 REM --- WELCOME
710 TEXT:HOME
720 T$=T2$:GOSUB 1000
730 T$=T3$:GOSUB 1000
740 PRINT
750 T$=T4$:GOSUB 1000
760 T$=T1$:GOSUB 1000
770 T$=T4$:GOSUB 1000
780 PRINT
790 PRINT "FIRST TIME? PRESS [I]"
800 PRINT "FOR INSTRUCTIONS. "
810 PRINT
820 PRINT "OTHERWISE, PRESS [P]"
830 PRINT "TO PLAY!"
840 PRINT
850 GET CH$
860 IF CH$="I" THEN GOSUB 1200:GOTO 700
870 IF CH$<>"P" THEN 850
880 REM SEED "RND" FROM HUMAN REACTION TIME
890 I=RND(-1*(PEEK(78)+256*PEEK(79)))
900 RETURN
1000 REM --- PRINT CENTERED
1010 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
1100 REM --- PROMPT TO CONTINUE
1110 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
1200 REM --- TUTORIAL
1210 W=3:H=3:CN=3:X=9-W:Y=20-H
1220 TS=0
1230 GR:HOME
1240 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1250 PRINT "CANDY WEAVING!"
1260 GOSUB 1100
1270 SEED=-222
1280 T=RND(SEED):REM PREDICTABLE SEQUENCE
1290 HOME:GOSUB 1900:GOSUB 3500:GOSUB 3600
1300 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1310 PRINT "A TASTY TARGET PATTERN."
1320 GOSUB 1100
1330 HOME:X=X+20:GOSUB 1900
1340 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1350 PRINT "STRIPES ONE AT A TIME UNTIL THEY"
1360 PRINT "MATCH THE PATTERN."
1370 GOSUB 1100
1380 HOME
1390 PRINT "FIRST: SELECT"
1400 GOSUB 2000
1410 PRINT "A FLAVOR OF CANDY"
1420 PRINT "BY PRESSING ITS NUMBER";
1430 GOSUB 2400:CI=CH-49:IF CI<0 OR CI>=CN THEN 1430
1440 CP=NOT(CI-1)+1
1450 HOME:GOSUB 2000:GOSUB 2100
1460 PRINT "NEXT: PRESS THE RIGHT ARROW SEVERAL"
1470 PRINT "TIMES TO MOVE THE CANDY HOSE."
1480 BI=0:GOSUB 2300
1490 GOSUB 2400:IF CH<>21 THEN 1490
1500 GOSUB 2600:IF BI<>0 THEN 1490
1510 HOME:GOSUB 2000:GOSUB 2100
1520 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1530 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1540 GOSUB 2400
1550 IF CH=11 OR CH=8 THEN GOSUB 2500
1560 IF CH=10 OR CH=21 THEN GOSUB 2600
1570 IF CH<>32 THEN 1540:REM SPACE
1580 GOSUB 3700
1590 HOME
1600 PRINT "CONTINUE LAYING"
1610 PRINT "CANDY STRIPES UNTIL"
1620 PRINT "THE PATTERN MATCHES!"
1630 GOSUB 2000:GOSUB 2100
1640 VTAB 22:HTAB 30:PRINT"--DEMO--"
1650 VTAB 23:HTAB 30:PRINT"PRESS [P]"
1660 VTAB 24:HTAB 30:PRINT"TO PLAY!";
1670 FOR I=1 TO 60
1680 IF PEEK(KY)>=128 THEN GET CH$
1690 IF CH$="P" THEN RETURN
1700 IF I<>20 THEN 1730
1710 T=INT(RND(1)*CN):IF T=CI THEN 1710
1720 CI=T:GOSUB 2100:GOSUB 2300
1730 IF I<>40 THEN 1760
1740 T=INT(RND(1)*(W+H)):IF T=BI THEN 1740
1750 BI=T:GOSUB 2300
1760 IF I<>60 THEN 1780
1770 GOSUB 3700
1780 NEXT I:GOTO 1670
1800 REM --- INITIALIZE VARIABLES
1810 M=0:I=0:O=-20:J=0:A=0:W=0:H=0:X=0:Y=0
1820 KY=49152
1830 DIM C(15)
1840 DIM S(25)
1850 DIM RO(24):REM GR MEM ADDR BY ROW
1860 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
1870 AX=0
1880 RETURN
1900 REM --- DRAW PLAY AREA BORDER
1910 COLOR=15
1920 HLIN X-1,X+W*2+1 AT Y-1
1930 HLIN X-1,X+W*2+1 AT Y+H*2+1
1940 VLIN Y-1,Y+H*2+1 AT X-1
1950 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
2000 REM --- DRAW COLOR PALETTE
2010 VTAB 21:HTAB 20
2020 FOR I=0 TO CN-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
2030 PRINT:RETURN
2100 REM --- SHOW SELECTED COLOR
2110 IF CI=CP THEN RETURN
2120 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+1;:HTAB 21+CP*2:NORMAL:PRINT CP+1;
2130 CP=CI:PRINT:RETURN
2200 REM --- CLEAR STRIPE INDICTOR
2210 COLOR=0:BA=BP:GOSUB 3800
2220 GOSUB 1900:RETURN
2300 REM --- SHOW SELECTED STRIPE
2310 GOSUB 2200
2320 COLOR=C(CI):BA=BI:GOSUB 3800
2330 BP=BI:RETURN
2400 REM --- INPUT KEY
2410 TI=TI+1:IF TI=TS THEN :COLOR=1:PLOT TX,0:TX=TX+1:TI=0
2420 IF TX=40 THEN RETURN
2430 IF PEEK(KY)<128 THEN 2410
2440 GET CH$:CH = ASC(CH$)
2450 RETURN
2500 REM --- AIM HOSE
2510 BI=BI-1:IF BI<0 THEN BI=W+H-1
2520 GOSUB 2300
2530 RETURN
2600 BI=BI+1:IF BI=W+H THEN BI=0
2610 GOSUB 2300
2620 RETURN
2700 REM --- MAIN GAME LOOP
2710 GOSUB 2400
2720 IF TX=40 THEN 2970
2730 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
2740 IF CH=11 OR CH=8 THEN GOSUB 2500:GOTO 2700:REM LEFT
2750 IF CH=10 OR CH=21 THEN GOSUB 2600:GOTO 2700:REM RIGHT
2760 IF CH=32 THEN 2880:REM SPACE
2770 IF CH=84 THEN AX=NOT AX:GOSUB 500:GOTO 2700:REM TOGGLE EXPERT
2780 IF CH=82 THEN GOSUB 3300:GOTO 2700:REM REVEAL DIFFS
2790 T=CH-49:REM 1-9
2800 IF T>=0 AND T<CN THEN 2840
2810 T=CH-65:REM A-Z
2820 IF T>=0 AND T<W+H THEN 2860
2830 GOTO 2700:REM OTHER
2840 REM - CHOOSE COLOR
2850 CI=T:GOSUB 2100:GOSUB 2300:GOTO 2700
2860 REM - CHOOSE LINE
2870 BI=T
2880 GOSUB 2300:GOSUB 3700
2890 VTAB 24:HTAB 1:CALL(-868):PRINT "COMPARING...      ";:HTAB 1:GOSUB 3200
2900 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:GOTO 2700
2910 GOSUB 2200
2920 T=RND(1)*3:INVERSE
2930 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 2960
2940 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 2960
2950 PRINT "COMPLETED! (YUM)";
2960 NORMAL:GOSUB 1100:RETURN
2970 HOME:FLASH:PRINT "OUT OF TIME!"
2980 NORMAL:PRINT "FINAL SCORE: ";M
2990 GOSUB 1100:RETURN
3100 REM --- INIT CHECK VARIABLES
3110 J0=(Y+1)/2:J1=J0+H-1:W2=W*2
3200 REM --- CHECK FOR MATCH
3210 M=0:FOR J=J0 TO J1
3220 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
3230 M=M+(PEEK(I)=PEEK(I+O))
3240 NEXT I:NEXT J
3250 M=(W*H-M)
3260 RETURN
3300 REM --- REVEAL DIFFERENCES
3310 FOR J=J0 TO J1
3320 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
3330 P=PEEK(I):IF P=PEEK(I+O) THEN 3360
3340 POKE(I),255:FOR T=1 TO 200:NEXT:POKE(I),P
3350 IF PEEK(KY)>=128 THEN RETURN
3360 NEXT I:NEXT J:RETURN
3400 REM --- CREATE A SHUFFLE 
3410 FOR I=0 TO SN-1:S(I)=I:NEXT
3420 FOR I=SN-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
3430 RETURN
3500 REM --- GENERATE COLOR PALETTE
3510 SN=15:GOSUB 3400
3520 I=0:FOR CI=0 TO CN-1
3530 T=S(I):I=I+1:IF T=0 OR T=3 OR T=5 OR T=10 THEN 3530
3540 C(CI)=T:NEXT:RETURN
3600 REM --- DRAW PATCH
3610 SN=W+H:GOSUB 3400
3620 CI=0
3630 FOR I=0 TO W+H-1:BI=S(I):GOSUB 3700
3640 CI=CI+1:IF CI=CN THEN CI=0
3650 NEXT I
3660 RETURN
3700 REM --- DRAW A SINGLE BAR
3710 COLOR=C(CI)
3720 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
3730 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
3800 REM --- DRAW AXIS INDICATOR
3810 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
3820 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
3900 REM --- GAME OVER
3910 TEXT:HOME:INVERSE
3920 T$="CONGRATULATIONS!":GOSUB 1000
3930 NORMAL:VTAB 4:PRINT "YOU'VE WOVEN ALL THE CANDY"
3940 PRINT:PRINT "YOU ARE A MASTER CANDY WEAVER!"
3950 PRINT
3960 END
