0 GOSUB 2500
10 REM COPYRIGHT 2022 BY SEAN GUGLER
20 REM LICENSED UNDER CC BY-NC-SA 4.0
30 T1$="CANDY WEAVER"
40 T2$="V0.4.0 2022-08-23"
50 T3$="BY SEAN GUGLER"
60 T4$="-----------------"
70 GOSUB 700
80 READ N
90 FOR R=1 TO N
100 READ W,H,CN,TS
110 IF NOT TS THEN 150
120 HOME:FLASH:VTAB 22
130 T$="*** TIMED ROUND! ***":GOSUB 1000
140 NORMAL:GOSUB 1100
150 GOSUB 300:GOSUB 500:GOSUB 3500
160 IF TX<>40 THEN 170:PRINT "FINAL SCORE: ";M
170 GOSUB 1100
180 NEXT
190 GOTO 4900
300 REM --- LAYOUT A NEW WEAVE
310 X=9-W:Y=20-H:M=W*H:GOSUB 3800
320 TI=0:TX=0
330 GR:HOME:GOSUB 4400:GOSUB 2700:GOSUB 4600
340 X=X+20:GOSUB 2700
350 HOME:PRINT "AVAILABLE FLAVORS:":GOSUB 2800
360 CP=1:CI=0:GOSUB 2900
370 BP=1:BI=0:GOSUB 3100
380 IF TS THEN COLOR=4:HLIN 0,39 AT 0
390 RETURN
500 REM --- HELP TEXT
510 VTAB 22:HTAB 1
520 IF AX THEN 570
530 CALL(-868):PRINT " AIM: LEFT/RIGHT";
540 HTAB 24:PRINT "[T]OGGLE LABELS"
550 CALL(-868):PRINT " LAY CANDY: SPACE";
560 RETURN
570 CALL(-868):PRINT "PRESS A LETTER";
580 HTAB X-3:PRINT "COL:";
590 FOR I=0 TO W-1:PRINT " ";CHR$(65+I);:NEXT
600 PRINT:CALL(-868):PRINT "TO FAST-STRIPE";
610 HTAB 26-H:PRINT "ROW:";
620 FOR I=W TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
630 RETURN
700 REM --- WELCOME
710 TEXT:HOME
720 T$=T2$:GOSUB 1000
730 T$=T3$:GOSUB 1000
740 PRINT
750 T$=T4$:GOSUB 1000
760 T$=T1$:GOSUB 1000
770 T$=T4$:GOSUB 1000
780 PRINT
790 PRINT " VIEW THE [D]EMO"
800 PRINT " TAKE THE [T]UTORIAL"
810 PRINT " PLAY THE [P]ROGRESSION CHALLENGE"
820 PRINT " PRACTICE [S]PECIFIC WEAVES"
830 PRINT
840 PRINT " WHERE TO? ";
850 GET CH$
860 IF ASC(CH$)=27 THEN END:REM ESC
870 IF CH$="D" THEN GOSUB 1900:GOTO 700
880 IF CH$="T" THEN GOSUB 1200:GOTO 700
890 IF CH$="S" THEN GOSUB 2100:GOTO 700
900 IF CH$<>"P" THEN 850
910 REM SEED "RND" FROM HUMAN REACTION TIME
920 I=RND(-1*(PEEK(78)+256*PEEK(79)))
930 RETURN
1000 REM --- PRINT CENTERED
1010 HTAB 20-LEN(T$)/2:PRINT T$:RETURN
1100 REM --- PROMPT TO CONTINUE
1110 VTAB 24:HTAB 27:PRINT "PRESS SPACE:";:GET CH$:RETURN
1200 REM --- TUTORIAL
1210 AP=AX:AX=0
1220 W=3:H=3:CN=6:TS=0
1230 X=9-W:Y=20-H:GOSUB 3800
1240 GR:HOME
1250 PRINT "WELCOME TO THE WONDERFUL WORLD OF"
1260 PRINT "CANDY WEAVING!"
1270 GOSUB 1100
1280 T=RND(SEED)
1290 HOME:GOSUB 2700:GOSUB 4400:GOSUB 4600
1300 PRINT "ABOVE, ON THE LEFT, YOU WILL SEE"
1310 PRINT "A TASTY TARGET PATTERN."
1320 GOSUB 1100
1330 IF CH$="R" THEN SEED=SEED-1:GOTO 1280
1340 HOME:X=X+20:GOSUB 2700
1350 PRINT "ON THE RIGHT, YOU WILL LAY CANDY"
1360 PRINT "STRIPES ONE AT A TIME UNTIL YOUR"
1370 PRINT "WHOLE PATTERN MATCHES THE TARGET."
1380 GOSUB 1100
1390 HOME
1400 PRINT "FIRST: SELECT"
1410 GOSUB 2800
1420 PRINT "A FLAVOR OF CANDY"
1430 PRINT "BY PRESSING ITS NUMBER";
1440 GOSUB 3200:CI=CH-49:IF CI<0 OR CI>=CN THEN 1440
1450 CP=NOT(CI-1)+1
1460 HOME:GOSUB 2800:GOSUB 2900
1470 PRINT "NEXT: PRESS THE RIGHT ARROW"
1480 PRINT "TO MOVE THE CANDY HOSE."
1490 BI=0:GOSUB 3100
1500 GOSUB 3200:IF CH<>21 THEN 1500
1510 VTAB 24:HTAB 1:PRINT "KEEP GOING! WATCH HOW IT WRAPS...";
1520 GOSUB 3400:IF BI<>0 THEN 1500
1530 HOME:GOSUB 2800:CP=NOT CI:GOSUB 2900
1540 PRINT "USE LEFT AND RIGHT ARROWS TO AIM, THEN"
1550 PRINT "PRESS SPACE TO LAY DOWN A CANDY STRIPE."
1560 GOSUB 3200
1570 IF CH=11 OR CH=8 THEN GOSUB 3300
1580 IF CH=10 OR CH=21 THEN GOSUB 3400
1590 IF CH<>32 THEN 1560:REM SPACE
1600 GOSUB 4700
1610 HOME
1620 PRINT "CONTINUE LAYING"
1630 PRINT "CANDY STRIPES UNTIL"
1640 PRINT "THE PATTERNS MATCH EXACTLY"
1650 GOSUB 2800:CP=NOT CI:GOSUB 2900
1700 REM --- TUTORIAL PLAY LOOP
1710 GOSUB 3200
1720 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
1730 IF CH=11 OR CH=8 THEN GOSUB 3300:GOTO 1700:REM LEFT
1740 IF CH=10 OR CH=21 THEN GOSUB 3400:GOTO 1700:REM RIGHT
1750 IF CH=32 OR CH=13 THEN 1790:REM SPACE, RETURN
1760 T=CH-49:REM 1-9
1770 IF T<0 OR T>=CN THEN 1700
1780 CI=T:GOSUB 2900:GOSUB 3100:GOTO 1700
1790 GOSUB 3100:GOSUB 4700
1800 GOSUB 3900:IF M THEN 1700
1810 GOSUB 3000
1820 VTAB 24:INVERSE:PRINT "I SAY; WELL DONE!";:NORMAL:GOSUB 1100
1830 AX=AP
1840 RETURN
1900 REM --- DEMO LOOP
1910 W=5:H=6:CN=7:TS=0
1920 T=RND(SEED):REM PREDICTABLE SEQUENCE
1930 GOSUB 300
1940 VTAB 21:PRINT "CHOOSE A FLAVOR,  "
1950 PRINT "AIM THE CANDY HOSE,";:HTAB 28:PRINT "---DEMO---"
1960 PRINT "LAY STRIPES, UNTIL THE";:HTAB 28:PRINT "PRESS A KEY"
1970 PRINT "ENTIRE PATTERN MATCHES";:HTAB 29:PRINT "TO RETURN";
1980 FOR I=1 TO 60
1990 IF PEEK(KY)>=128 THEN GET CH$:RETURN
2000 IF I<>20 THEN 2030
2010 T=INT(RND(1)*CN):IF T=CI THEN 2010
2020 CI=T:GOSUB 2900:GOSUB 3100
2030 IF I<>40 THEN 2060
2040 T=INT(RND(1)*(W+H)):IF T=BI THEN 2040
2050 BI=T:GOSUB 3100
2060 IF I<>60 THEN 2080
2070 GOSUB 4700
2080 NEXT I:GOTO 1980
2100 REM --- PRACTICE MENU
2110 VTAB 13:HTAB 1
2120 PRINT "  WIDTH (2-8): ";W
2130 PRINT " HEIGHT (2-9): ";H
2140 PRINT "FLAVORS (2-9): ";CN
2150 PRINT "  TIMER (0-9): ";TV
2160 PRINT
2170 HTAB 10:PRINT "[SPACE] TO BEGIN"
2180 HTAB 10:PRINT "[ESC]   TO CANCEL"
2190 S(0)=W:S(1)=H:S(2)=CN:S(3)=TV
2200 I=0
2210 VTAB 13+I:HTAB 16:INVERSE:PRINT S(I);:NORMAL
2220 GOSUB 3200
2230 IF CH=27 THEN RETURN:REM ESC
2240 IF CH=32 THEN 2320:REM SPACE
2250 IF CH=13 THEN CH=S(I)+48:REM RETURN KEY
2260 N=CH-48:REM "0"
2270 IF N<(2 * (I<>3)) OR N>(8 + (I<>0)) THEN 2220
2280 S(I)=N
2290 HTAB 16:PRINT S(I)
2300 I=I+1:IF I>3 THEN I=0
2310 GOTO 2210
2320 W=S(0):H=S(1):CN=S(2):TS=TA(TV)
2400 REM --- PLAY A PRACTICE ROUND
2410 GOSUB 300:GOSUB 500:GOSUB 3500
2420 VTAB 24:HTAB 24:PRINT "ANOTHER ROUND?";
2430 GET CH$
2440 IF CH$="Y" OR CH=13 THEN 2400
2450 IF CH$="N" OR CH=27 THEN RETURN
2460 GOTO 2430
2500 REM --- INITIALIZE VARIABLES
2510 M=0:I=0:O=-20:J=0:A=0:W=3:H=3:X=0:Y=0
2520 KY=49152
2530 CN=6
2540 TV=0
2550 AX=0
2560 SEED=-222
2570 DIM C(15)
2580 DIM S(25)
2590 DIM RO(24):REM GR MEM ADDR BY ROW
2600 FOR I=0 TO 7:FOR J=0 TO 2:RO(I+8*J)=1024+128*I+40*J:NEXT J:NEXT I
2610 DIM TA(9):REM TIMER PRESETS
2620 FOR I=0 TO 9:READ TA(I):NEXT I
2630 GOSUB 4300
2640 RETURN
2700 REM --- DRAW PLAY AREA BORDER
2710 COLOR=15
2720 HLIN X-1,X+W*2+1 AT Y-1
2730 HLIN X-1,X+W*2+1 AT Y+H*2+1
2740 VLIN Y-1,Y+H*2+1 AT X-1
2750 VLIN Y-1,Y+H*2+1 AT X+W*2+1:RETURN
2800 REM --- DRAW COLOR PALETTE
2810 VTAB 21:HTAB 20
2820 FOR I=0 TO CN-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
2830 PRINT:RETURN
2900 REM --- SHOW SELECTED COLOR
2910 IF CI=CP THEN RETURN
2920 VTAB 21:HTAB 21+CI*2:INVERSE:PRINT CI+1;:HTAB 21+CP*2:NORMAL:PRINT CP+1;
2930 CP=CI:PRINT:RETURN
3000 REM --- CLEAR STRIPE INDICTOR
3010 COLOR=0:BA=BP:GOSUB 4800
3020 GOSUB 2700:RETURN
3100 REM --- SHOW SELECTED STRIPE
3110 GOSUB 3000
3120 COLOR=C(CI):BA=BI:GOSUB 4800
3130 BP=BI:RETURN
3200 REM --- INPUT KEY
3210 TI=TI+1:IF TI=TS THEN :COLOR=9:PLOT TX,0:TX=TX+1:TI=0
3220 IF TX=40 THEN RETURN
3230 IF PEEK(KY)<128 THEN 3210
3240 GET CH$:CH = ASC(CH$)
3250 RETURN
3300 REM --- AIM HOSE
3310 BI=BI-1:IF BI<0 THEN BI=W+H-1
3320 GOSUB 3100
3330 RETURN
3400 BI=BI+1:IF BI=W+H THEN BI=0
3410 GOSUB 3100
3420 RETURN
3500 REM --- MAIN GAME LOOP
3510 GOSUB 3200
3520 IF TX=40 THEN 3760
3530 IF CH=83 THEN RETURN:REM DEBUG [S]KIP
3540 IF CH=11 OR CH=8 THEN GOSUB 3300:GOTO 3500:REM LEFT
3550 IF CH=10 OR CH=21 THEN GOSUB 3400:GOTO 3500:REM RIGHT
3560 IF CH=32 OR CH=13 THEN 3680:REM SPACE, RETURN
3570 IF CH=84 THEN AX=NOT AX:GOSUB 500:GOTO 3500:REM TOGGLE EXPERT
3580 IF CH=82 THEN GOSUB 4000:GOTO 3500:REM REVEAL DIFFS
3590 T=CH-49:REM 1-9
3600 IF T>=0 AND T<CN THEN 3640
3610 T=CH-65:REM A-Z
3620 IF T>=0 AND T<W+H THEN 3660
3630 GOTO 3500:REM OTHER
3640 REM - CHOOSE COLOR
3650 CI=T:GOSUB 2900:GOSUB 3100:GOTO 3500
3660 REM - CHOOSE LINE
3670 BI=T
3680 GOSUB 3100:GOSUB 4700
3690 VTAB 24:HTAB 1:CALL(-868):PRINT "COMPARING...      ";:HTAB 1:GOSUB 3900
3700 IF M THEN PRINT "PROGRESS: ";M;" TO GO ";:HTAB 24:PRINT "[R]EVEAL DIFFS";:GOTO 3500
3710 GOSUB 3000
3720 T=RND(1)*3:INVERSE
3730 IF T<1 THEN PRINT "A PERFECT MATCH!";:GOTO 3770
3740 IF T<2 THEN PRINT "YES! DELICIOUS!";:GOTO 3770
3750 PRINT "COMPLETED! (YUM)";:GOTO 3770
3760 HOME:FLASH:PRINT "OUT OF TIME!"
3770 NORMAL:RETURN
3800 REM --- INIT CHECK VARIABLES
3810 J0=(Y+1)/2:J1=J0+H-1:W2=W*2
3900 REM --- CHECK FOR MATCH
3910 M=0:FOR J=J0 TO J1
3920 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
3930 M=M+(PEEK(I)=PEEK(I+O))
3940 NEXT I:NEXT J
3950 M=(W*H-M)
3960 RETURN
4000 REM --- REVEAL DIFFERENCES
4010 FOR J=J0 TO J1
4020 A=RO(J)+X:FOR I=A+1 TO A+W2 STEP 2
4030 P=PEEK(I):IF P=PEEK(I+O) THEN 4060
4040 POKE(I),255:FOR T=1 TO 200:NEXT:POKE(I),P
4050 IF PEEK(KY)>=128 THEN RETURN
4060 NEXT I:NEXT J:RETURN
4100 REM --- CREATE A SHUFFLE 
4110 FOR I=0 TO SN-1:S(I)=I:NEXT
4120 FOR I=SN-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
4130 RETURN
4200 REM --- SHUFFLE COLOR PALETTE
4210 FOR I=CN-1 TO 0 STEP -1:J=RND(1)*I:T=C(I):C(I)=C(J):C(J)=T:NEXT
4220 RETURN
4300 REM --- TABLE OF CLASHING COLORS
4310 DIM CC(15*5-1)
4320 FOR I=0 TO 15:S(I)=0:NEXT
4330 FOR K=1 TO 14
4340 READ I,J
4350 N=S(I):CC(I*5+N)=J:S(I)=N+1
4360 N=S(J):CC(J*5+N)=I:S(J)=N+1
4370 NEXT K
4380 RETURN
4400 REM --- GENERATE COLOR PALETTE
4410 M=0:VTAB 21:PRINT "SELECTING FLAVORS..."
4420 M=M+1:IF M=3 THEN PRINT "TASTE TESTING FOR QUALITY..."
4430 FOR N=0 TO 13:S(N)=N+1:NEXT
4440 FOR CI=CN-1 TO 0 STEP -1
4450 IF N<=CI THEN 4420
4460 I=RND(1)*N
4470 K=S(I):C(CI)=K
4480 N=N-1:S(I)=S(N)
4490 K=K*5
4500 FOR J=K TO K+4
4510 A=CC(J):T=(NOT A)*N:IF T THEN J=J+5
4520 FOR I=T TO N-1
4530 IF A=S(I) THEN N=N-1:S(I)=S(N):I=N
4540 NEXT I
4550 NEXT J
4560 NEXT CI
4570 RETURN
4600 REM --- DRAW PATCH
4610 SN=W+H:GOSUB 4100
4620 CI=0
4630 FOR I=0 TO W+H-1:BI=S(I):GOSUB 4700
4640 CI=CI+1:IF CI=CN THEN CI=0
4650 NEXT I
4660 GOSUB 4200
4670 RETURN
4700 REM --- DRAW A SINGLE BAR
4710 COLOR=C(CI)
4720 IF BI<W THEN VLIN Y,Y+H*2 AT 1+X+BI*2:RETURN
4730 HLIN X,X+W*2 AT 1+Y+(BI-W)*2:RETURN
4800 REM --- DRAW AXIS INDICATOR
4810 IF BA<W THEN VLIN Y-3,Y-1 AT 1+X+BA*2:RETURN
4820 T=X+W*2+1:HLIN T,T+1 AT 1+Y+(BA-W)*2:RETURN
4900 REM --- GAME OVER
4910 TEXT:HOME:INVERSE
4920 T$="CONGRATULATIONS!":GOSUB 1000
4930 NORMAL:VTAB 4:PRINT "YOU'VE WOVEN ALL THE CANDY"
4940 PRINT:PRINT "YOU ARE A MASTER CANDY WEAVER!"
4950 PRINT
4960 END
5000 DIM CT(39)
5010 I=0
5020 FOR C1=1 TO 13
5030 FOR C2=C1+1 TO 14
5040 IF I=0 THEN GR:HOME
5050 PRINT C1;" ";C2;"  ";
5060 CT(I*5+1)=C1
5070 CT(I*5+2)=C2
5080 CT(I*5+3)=C1
5090 CT(I*5+4)=C2
5100 I=I+1
5110 IF I<8 THEN 5150
5120 FOR I=0 TO 39:COLOR=CT(I):VLIN 0,39 AT I:NEXT
5130 GOSUB 1100
5140 I=0
5150 NEXT C2
5160 NEXT C1
5170 FOR J=0 TO I*5:COLOR=CT(J):VLIN 0,39 AT J:NEXT
5180 END
5190 DATA 0,90,80,70,60,50,40,30,20,10
5200 DATA 5,10 , 7,10 , 7,11 , 7,14 , 10,11
5210 DATA 1,3 , 2,4 , 3,5 , 3,9 , 3,10 , 3,11 , 5,7 , 6,14 , 12,14
5220 DATA 29
5230 DATA 3,3,6,0 , 4,4,8,0 , 5,4,9,0 , 3,3,6,50
5240 DATA 5,3,8,0 , 7,3,9,0 , 8,4,7,0 , 5,4,8,60
5250 DATA 3,6,6,0 , 4,7,5,0 , 5,9,5,0 , 4,6,7,60
5260 DATA 3,3,3,0 , 5,5,5,0 , 7,7,7,0 , 4,4,4,70
5270 DATA 6,3,3,0 , 8,4,4,0 , 4,8,3,0 , 3,3,3,40
5280 DATA 5,2,2,0 , 6,4,2,0 , 3,4,2,50
5290 DATA 8,9,9,0 , 8,9,5,0 , 7,8,2,0 , 3,3,6,20
5300 DATA 8,9,2,0 , 6,7,3,90
