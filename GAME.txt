0 REM GAME BY SEAN GUGLER
10 DIM C(15)
20 DIM S(25)
30 GR
40 W=6:H=8:X=4:Y=16
50 CN%=5:GOSUB 700
60 GOSUB 800
70 X=X+20:GOSUB 200
80 GOSUB 300
90 END
200 HOME:PRINT "CHOOSE COLOR:";:HTAB 20
210 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
220 PRINT
230 PRINT "DRAW LINE:":HTAB X-H*2:PRINT "(";
240 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
250 PRINT CHR$(65+I);:NEXT:PRINT ")";
260 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
270 RETURN
300 WAIT 49152,128:GET CH$
310 CH = ASC(CH$)
320 IF CH=27 THEN RETURN:REM ESC
330 CI%=CH-49:REM 1-9
340 IF CI%>=0 AND CI%<CN% THEN 400
350 BI=CH-65:REM A-Z
360 IF BI>=0 AND BI<W+H THEN 500
370 GOTO 300:REM OTHER
400 COLOR=C(CI%):GOTO 300
500 GOSUB 1000:GOTO 300
600 FOR I=0 TO SN%-1:S(I)=I:NEXT
610 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
620 RETURN
700 REM GENERATE COLOR PALETTE
710 SN%=15:GOSUB 600
720 FOR CI=0 TO CN%-1:C(CI)=S(CI)+1:NEXT:RETURN
800 REM DRAW PATCH
810 SN%=W+H:GOSUB 600
820 CI%=0
830 FOR I=0 TO W+H-1
840 COLOR=C(CI%)
850 BI=S(I)
860 GOSUB 1000
870 CI%=CI%+1:IF CI%=CN% THEN CI%=0
880 NEXT I
890 RETURN
1000 REM DRAW A SINGLE BAR
1010 IF BI<H THEN HLIN X,X+W*2 AT 1+Y+BI*2:RETURN
1020 VLIN Y,Y+H*2 AT 1+X+(BI-H)*2:RETURN
1100 SN%=4
1110 GOSUB 600:GOSUB 1300
1120 GOSUB 600:GOSUB 1300
1130 GOSUB 600:GOSUB 1300
1140 GOSUB 600:GOSUB 1300
1150 SN%=16
1160 GOSUB 600:GOSUB 1300
1170 GOSUB 600:GOSUB 1300
1180 GOSUB 600:GOSUB 1300
1190 GOSUB 600:GOSUB 1300
1200 SN%=1
1210 GOSUB 600:GOSUB 1300
1220 RETURN
1300 FOR I=0 TO SN%-1:PRINT(S(I));" ";:NEXT:PRINT:RETURN
