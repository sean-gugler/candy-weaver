0 REM GAME BY SEAN GUGLER
10 GOSUB 300
20 GOSUB 200
30 GR
40 W=6:H=8:X=4:Y=16
50 CN%=5:GOSUB 1000
60 GOSUB 1100
70 X=X+20:GOSUB 400
80 GOSUB 500
90 END
200 REM --- WELCOME
210 TEXT:HOME:PRINT "WEAVER, BY SEAN GUGLER"
220 PRINT:PRINT "CREATE A WEAVE ON THE RIGHT"
230 PRINT "TO MATCH THE WEAVE ON THE LEFT"
240 PRINT:PRINT "GOOD LUCK! ";
250 GET CH$
260 I=RND(-1*(PEEK(78)+256*PEEK(79))):REM SEED FROM HUMAN REACTION TIME
270 RETURN
300 REM --- INITIALIZE VARIABLES
310 DIM C(15)
320 DIM S(25)
330 DIM RO(24):REM GR MEM ADDR BY ROW
340 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
350 RETURN
400 REM --- DRAW GAME MENU
410 HOME:PRINT "CHOOSE COLOR:";:HTAB 20
420 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
430 PRINT
440 PRINT "DRAW LINE:":HTAB X-H*2:PRINT "(";
450 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
460 PRINT CHR$(65+I);:NEXT:PRINT ")";
470 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
480 RETURN
500 REM --- MAIN GAME LOOP
510 WAIT 49152,128:GET CH$
520 CH = ASC(CH$)
530 IF CH=27 THEN RETURN:REM ESC
540 CI%=CH-49:REM 1-9
550 IF CI%>=0 AND CI%<CN% THEN 600
560 BI=CH-65:REM A-Z
570 IF BI>=0 AND BI<W+H THEN 700
580 GOTO 500:REM OTHER
600 REM - CHOOSE COLOR
610 COLOR=C(CI%):GOTO 500
700 REM - CHOOSE LINE
710 GOSUB 1200
720 GOSUB 800
730 IF MA% THEN PRINT MA%;" MORE    ";:GOTO 500
740 PRINT "COMPLETE MATCH!";
750 RETURN
800 REM --- CHECK FOR MATCH
810 VTAB 24:HTAB 1:PRINT "CHECKING...";:HTAB 1
820 MA%=0
830 Y2=Y/2:FOR J=Y2 TO Y2+H-1
840 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
850 MA%=MA%+(PEEK(I)=PEEK(I-20))
860 NEXT I:NEXT J
870 MA%=(W*H-MA%)
880 RETURN
900 REM --- CREATE A SHUFFLE 
910 FOR I=0 TO SN%-1:S(I)=I:NEXT
920 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
930 RETURN
1000 REM --- GENERATE COLOR PALETTE
1010 SN%=15:GOSUB 900
1020 FOR CI=0 TO CN%-1:C(CI)=S(CI)+1:NEXT:RETURN
1100 REM --- DRAW PATCH
1110 SN%=W+H:GOSUB 900
1120 CI%=0
1130 FOR I=0 TO W+H-1
1140 COLOR=C(CI%):BI=S(I):GOSUB 1200
1150 CI%=CI%+1:IF CI%=CN% THEN CI%=0
1160 NEXT I
1170 RETURN
1200 REM --- DRAW A SINGLE BAR
1210 IF BI<H THEN HLIN X,X+W*2 AT 1+Y+BI*2:RETURN
1220 VLIN Y,Y+H*2 AT 1+X+(BI-H)*2:RETURN
1300 SN%=4
1310 GOSUB 900:GOSUB 1500
1320 GOSUB 900:GOSUB 1500
1330 GOSUB 900:GOSUB 1500
1340 GOSUB 900:GOSUB 1500
1350 SN%=16
1360 GOSUB 900:GOSUB 1500
1370 GOSUB 900:GOSUB 1500
1380 GOSUB 900:GOSUB 1500
1390 GOSUB 900:GOSUB 1500
1400 SN%=1
1410 GOSUB 900:GOSUB 1500
1420 RETURN
1500 FOR I=0 TO SN%-1:PRINT(S(I));" ";:NEXT:PRINT:RETURN
1600 POKE(RO(0),242)
1610 POKE(RO(0)+39,42)
1620 POKE(RO(1)+1,242)
1630 POKE(RO(2)+38,42)
1640 POKE(RO(8),242)
1650 POKE(RO(8)+39,42)
1660 POKE(RO(9)+1,242)
1670 POKE(RO(10)+38,42)
1680 POKE(RO(19),242)
1690 POKE(RO(19)+39,42)
1700 RETURN
