0 REM GAME BY SEAN GUGLER
10 DIM C(15)
20 DIM S(25)
30 DIM RO(24):REM GR MEM ADDR BY ROW
40 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
50 GR
60 W=6:H=8:X=4:Y=16
70 CN%=5:GOSUB 800
80 GOSUB 900
90 X=X+20:GOSUB 200
100 GOSUB 300
110 END
200 REM --- DRAW GAME MENU
210 HOME:PRINT "CHOOSE COLOR:";:HTAB 20
220 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
230 PRINT
240 PRINT "DRAW LINE:":HTAB X-H*2:PRINT "(";
250 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
260 PRINT CHR$(65+I);:NEXT:PRINT ")";
270 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
280 RETURN
300 REM --- MAIN GAME LOOP
310 WAIT 49152,128:GET CH$
320 CH = ASC(CH$)
330 IF CH=27 THEN RETURN:REM ESC
340 CI%=CH-49:REM 1-9
350 IF CI%>=0 AND CI%<CN% THEN 400
360 BI=CH-65:REM A-Z
370 IF BI>=0 AND BI<W+H THEN 500
380 GOTO 300:REM OTHER
400 REM - CHOOSE COLOR
410 COLOR=C(CI%):GOTO 300
500 REM - CHOOSE LINE
510 GOSUB 1000
520 GOSUB 600
530 IF MA% THEN PRINT MA%;" MORE    ";:GOTO 300
540 PRINT "COMPLETE MATCH!";
550 RETURN
600 REM --- CHECK FOR MATCH
610 VTAB 24:HTAB 1:PRINT "CHECKING...";:HTAB 1
620 MA%=0
630 Y2=Y/2:FOR J=Y2 TO Y2+H-1
640 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
650 MA%=MA%+(PEEK(I)=PEEK(I-20))
660 NEXT I:NEXT J
670 MA%=(W*H-MA%)
680 RETURN
700 REM --- CREATE A SHUFFLE 
710 FOR I=0 TO SN%-1:S(I)=I:NEXT
720 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
730 RETURN
800 REM --- GENERATE COLOR PALETTE
810 SN%=15:GOSUB 700
820 FOR CI=0 TO CN%-1:C(CI)=S(CI)+1:NEXT:RETURN
900 REM --- DRAW PATCH
910 SN%=W+H:GOSUB 700
920 CI%=0
930 FOR I=0 TO W+H-1
940 COLOR=C(CI%):BI=S(I):GOSUB 1000
950 CI%=CI%+1:IF CI%=CN% THEN CI%=0
960 NEXT I
970 RETURN
1000 REM --- DRAW A SINGLE BAR
1010 IF BI<H THEN HLIN X,X+W*2 AT 1+Y+BI*2:RETURN
1020 VLIN Y,Y+H*2 AT 1+X+(BI-H)*2:RETURN
1100 SN%=4
1110 GOSUB 700:GOSUB 1300
1120 GOSUB 700:GOSUB 1300
1130 GOSUB 700:GOSUB 1300
1140 GOSUB 700:GOSUB 1300
1150 SN%=16
1160 GOSUB 700:GOSUB 1300
1170 GOSUB 700:GOSUB 1300
1180 GOSUB 700:GOSUB 1300
1190 GOSUB 700:GOSUB 1300
1200 SN%=1
1210 GOSUB 700:GOSUB 1300
1220 RETURN
1300 FOR I=0 TO SN%-1:PRINT(S(I));" ";:NEXT:PRINT:RETURN
1400 POKE(RO(0),242)
1410 POKE(RO(0)+39,42)
1420 POKE(RO(1)+1,242)
1430 POKE(RO(2)+38,42)
1440 POKE(RO(8),242)
1450 POKE(RO(8)+39,42)
1460 POKE(RO(9)+1,242)
1470 POKE(RO(10)+38,42)
1480 POKE(RO(19),242)
1490 POKE(RO(19)+39,42)
1500 RETURN
