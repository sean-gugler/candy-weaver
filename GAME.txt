0 REM GAME BY SEAN GUGLER
10 GOSUB 300
20 GOSUB 200
30 GR
40 W=6:H=8:X=4:Y=16
50 CN%=5:GOSUB 1100
60 GOSUB 1200
70 X=X+20:GOSUB 400
80 CP%=1:CI%=0:GOSUB 500
90 GOSUB 600
100 END
200 REM --- WELCOME
210 TEXT:HOME:PRINT "WEAVER, BY SEAN GUGLER"
220 PRINT:PRINT "CREATE A WEAVE ON THE RIGHT"
230 PRINT "TO MATCH THE WEAVE ON THE LEFT"
240 PRINT:PRINT "GOOD LUCK! ";
250 GET CH$
260 I=RND(-1*(PEEK(78)+256*PEEK(79))):REM SEED FROM HUMAN REACTION TIME
270 RETURN
300 REM --- INITIALIZE VARIABLES
310 DIM C(15)
320 DIM S(25)
330 DIM RO(24):REM GR MEM ADDR BY ROW
340 FOR N=0 TO 7:FOR M=0 TO 2:RO(N+8*M)=1024+128*N+40*M:NEXT M:NEXT N
350 RETURN
400 REM --- DRAW GAME MENU
410 HOME:PRINT "CHOOSE COLOR:";:HTAB 20
420 FOR I=0 TO CN%-1:COLOR=C(I):VLIN 37,38 AT 20+I*2:PRINT " ";I+1;:NEXT
430 PRINT
440 PRINT "DRAW LINE:":HTAB X-H*2:PRINT "(";
450 FOR I=0 TO H-1:IF I>0 THEN PRINT " ";
460 PRINT CHR$(65+I);:NEXT:PRINT ")";
470 FOR I=H TO W+H-1:PRINT " ";CHR$(65+I);:NEXT
480 RETURN
500 REM --- ACTIVATE A COLOR
510 IF CI%=CP% THEN RETURN
520 VTAB 21:HTAB 21+CI%*2:INVERSE:PRINT CI%+1;:HTAB 21+CP%*2:NORMAL:PRINT CP%+1;:COLOR=C(CI%)
530 CP%=CI%:RETURN
600 REM --- MAIN GAME LOOP
610 WAIT 49152,128:GET CH$
620 CH = ASC(CH$)
630 IF CH=27 THEN RETURN:REM ESC
640 CI%=CH-49:REM 1-9
650 IF CI%>=0 AND CI%<CN% THEN 700
660 BI=CH-65:REM A-Z
670 IF BI>=0 AND BI<W+H THEN 800
680 GOTO 600:REM OTHER
700 REM - CHOOSE COLOR
710 GOSUB 500:GOTO 600
800 REM - CHOOSE LINE
810 GOSUB 1300
820 GOSUB 900
830 IF MA% THEN PRINT MA%;" MORE    ";:GOTO 600
840 PRINT "COMPLETE MATCH!";
850 RETURN
900 REM --- CHECK FOR MATCH
910 VTAB 24:HTAB 1:PRINT "CHECKING...";:HTAB 1
920 MA%=0
930 Y2=Y/2:FOR J=Y2 TO Y2+H-1
940 A=RO(J)+X:FOR I=A+1 TO A+(W*2) STEP 2
950 MA%=MA%+(PEEK(I)=PEEK(I-20))
960 NEXT I:NEXT J
970 MA%=(W*H-MA%)
980 RETURN
1000 REM --- CREATE A SHUFFLE 
1010 FOR I=0 TO SN%-1:S(I)=I:NEXT
1020 FOR I=SN%-1 TO 0 STEP -1:J=RND(1)*I:T=S(I):S(I)=S(J):S(J)=T:NEXT
1030 RETURN
1100 REM --- GENERATE COLOR PALETTE
1110 SN%=15:GOSUB 1000
1120 FOR CI=0 TO CN%-1:C(CI)=S(CI)+1:NEXT:RETURN
1200 REM --- DRAW PATCH
1210 SN%=W+H:GOSUB 1000
1220 CI%=0
1230 FOR I=0 TO W+H-1
1240 COLOR=C(CI%):BI=S(I):GOSUB 1300
1250 CI%=CI%+1:IF CI%=CN% THEN CI%=0
1260 NEXT I
1270 RETURN
1300 REM --- DRAW A SINGLE BAR
1310 IF BI<H THEN HLIN X,X+W*2 AT 1+Y+BI*2:RETURN
1320 VLIN Y,Y+H*2 AT 1+X+(BI-H)*2:RETURN
1400 SN%=4
1410 GOSUB 1000:GOSUB 1600
1420 GOSUB 1000:GOSUB 1600
1430 GOSUB 1000:GOSUB 1600
1440 GOSUB 1000:GOSUB 1600
1450 SN%=16
1460 GOSUB 1000:GOSUB 1600
1470 GOSUB 1000:GOSUB 1600
1480 GOSUB 1000:GOSUB 1600
1490 GOSUB 1000:GOSUB 1600
1500 SN%=1
1510 GOSUB 1000:GOSUB 1600
1520 RETURN
1600 FOR I=0 TO SN%-1:PRINT(S(I));" ";:NEXT:PRINT:RETURN
1700 POKE(RO(0),242)
1710 POKE(RO(0)+39,42)
1720 POKE(RO(1)+1,242)
1730 POKE(RO(2)+38,42)
1740 POKE(RO(8),242)
1750 POKE(RO(8)+39,42)
1760 POKE(RO(9)+1,242)
1770 POKE(RO(10)+38,42)
1780 POKE(RO(19),242)
1790 POKE(RO(19)+39,42)
1800 RETURN
